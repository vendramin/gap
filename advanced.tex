\chapter{Advanced group theory}

\GAP~contains several useful group databases. We 
can work with small groups, 
non-abelian simple groups, classical groups, 
perfect groups. It is also possible to work with transitive or
primitive groups of small degree. 

\section{Group databases}

%\subsection{SmallGroups}

\GAP~contains a database with all groups of certain small orders. The groups
are sorted by their orders and they are listed up to isomorphism. This database
is part of a library named \lstinline{SmallGroups}. It contains the following
families of groups: 
\begin{enumerate}[label=(\alph*)]
    \item Of order $\leq2000$ except order $1024$. 
    \item Of cube-free order $\leq 50000$.
    \item Of order $p^7$ for $p\in\{3,5,7,11\}$.
    \item Of order $p^n$ for $n\leq 6$ and all primes $p$. 
    \item Of order $q^np$ for $q^n$ dividing $2^8$, $3^6$, $5^5$ or $7^4$ and all     primes $p$ with $p\ne q$. 
    \item Of square-free order.
    \item Of order that factorizes into at most three primes.
\end{enumerate}

The library was written by H. Besche, B. Eick and E. O'Brien.

As one can image, this library is a very useful tool when one needs to look for
examples and counterexamples.  

\begin{example}
Let us see what \lstinline{SmallGroups} knows about groups of order twelve:
\begin{lstlisting}
gap> SmallGroupsInformation(12);                                      

  There are 5 groups of order 12.
    1 is of type 6.2.
    2 is of type c12.
    3 is of type A4.
    4 is of type D12.
    5 is of type 2^2x3.

  The groups whose order factorises in at most 3 primes 
  have been classified by O. Hoelder. This classification is 
  used in the SmallGroups library. 

  This size belongs to layer 1 of the SmallGroups library. 
  IdSmallGroup is available for this size. 
\end{lstlisting}
\end{example}

Some of the examples of this section are from \cite{MR605275}. 

\begin{example}
	Let us check that there exist non-abelian groups of odd order and that the
	smallest of this group has order $21$:
\begin{lstlisting}
gap> First(AllSmallGroups(Size, [1, 3..21]),\
> x->not IsAbelian(x));;
gap> Size(last);
21
\end{lstlisting}
\end{example}

\begin{example}
In one line we check that there are no simple groups of order $84$. We use 
the filter \lstinline{IsSimple} with the function 
\lstinline{AllSmallGroups}:
\begin{lstlisting}
gap> AllSmallGroups(Size, 84, IsSimple, true);
[  ]
\end{lstlisting}
\end{example}

With the function \lstinline{StructureDescription} one explores the structure
of a given group. The function returns a short string which gives some insight
into the structure of the group.  

\begin{example}
Let us see how the groups of order twelve look like:
\begin{lstlisting}
gap> List(AllSmallGroups(Size, 12),\
> StructureDescription);
[ "C3 : C4", "C12", "A4", "D12", "C6 x C2" ]
\end{lstlisting}
The group \lstinline{C3 : C4} denotes a semidirect product 
$C_3\rtimes C_4$ of $C_3$ by $C_4$.  
\end{example}

\begin{example}
Let us explore more group homomorphisms. We know that the symmetric group $\Sym_4$
is generated by the transpositions $(12)$, $(23)$ and $(34)$. We let $f$ be the
group homomorphism $\Sym_4\to\Sym_3$ given by $(12)\mapsto (12)$, $(23)\mapsto
(23)$ and $(34)\mapsto(12)$. 
Let us perform some calculations related to this
group homomorphism:
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> S3 := SymmetricGroup(3);;
gap> f := GroupHomomorphismByImages(S4, S3, [(1,2),(2,3),(3,4)],\
>  [(1,2),(2,3),(1,2)]);;
gap> K := Kernel(f);;
gap> StructureDescription(K);
"C2 x C2"
gap> IsInjective(f);
false
gap> StructureDescription(S4/K);
"S3"
gap> StructureDescription(Image(f));
"S3"
gap> IsSurjective(f);
true
\end{lstlisting}
\end{example}

It is important to remark that the string
returned by \lstinline{StructureDescription} is not an isomorphism invariant:
non-isomorphic groups can have the same string value and two isomorphic groups
in different representations can produce different strings. 

\begin{example}
There are two groups of order $20$ that can be written as 
a semidirect product $C_5\rtimes C_4$. 
\lstinline{StructureDescription} will not distinguish such groups:
\begin{lstlisting}
gap> List(AllSmallGroups(Size, 20),\
> StructureDescription);
[ "C5 : C4", "C20", "C5 : C4", "D20", "C10 x C2" ]
\end{lstlisting}
To identify groups in the database \lstinline{SmallGroups} one uses the
function \lstinline{IdGroup}. Here we have some examples:
\begin{lstlisting}
gap> IdGroup(SymmetricGroup(3));
[ 6, 1 ]
gap> IdGroup(SymmetricGroup(4));
[ 24, 12 ]
gap> IdGroup(AlternatingGroup(4));
[ 12, 3 ]
gap> IdGroup(DihedralGroup(8));
[ 8, 3 ]
gap> IdGroup(QuaternionGroup(8));
[ 8, 4 ]
\end{lstlisting}
\end{example}

\begin{example}
	\index{Lam, T.}
	\index{Leep, D.}
	In~\cite{MR1240362}, T. Lam and D. Leep proved that each index-two subgroup of
	$\Aut(\Sym_6)$ is isomorphic either to $\Sym_6$, $\PGL_2(9)$ or to the Mathieu group
	$M_{10}$. Let's check this claim using the function~\lstinline{IdGroup}:
	\begin{lstlisting}
gap> autS6 := AutomorphismGroup(SymmetricGroup(6));;
gap> lst := SubgroupsOfIndexTwo(autS6);;
gap> List(lst, IdGroup);
[ [ 720, 764 ], [ 720, 763 ], [ 720, 765 ] ]
gap> IdGroup(PGL(2,9));
[ 720, 764 ]
gap> IdGroup(MathieuGroup(10));
[ 720, 765 ]
gap> IdGroup(SymmetricGroup(6));
[ 720, 763 ]
\end{lstlisting}
\end{example}

\begin{example}
\label{exa:Guralnick:96}
Now we prove a theorem of R. Guralnick \cite{MR673806}. The theorem states
that the smallest finite group $G$ such that $\{[x,y]:x,y\in G\}\ne[G,G]$ has
order $96$.
\begin{lstlisting}
gap> G := First(AllSmallGroups(Size, [1..100]),\
> x->Order(DerivedSubgroup(x))<>Size(\
> Set(List(Cartesian(x,x), Comm))));;
gap> Order(G);
96
gap> IdGroup(G);
[ 96, 3 ]
\end{lstlisting}
With \lstinline{IdGroup} (or with \lstinline{IsomorphismGroups}) we check that 
\[
G\simeq\langle (135)(246)(7\,11\,9)(8\,12\,10),(394\,10)(58)(67)(11\,12)\rangle.
\]
How do we find this isomorphism? %the isomorphism 
%\[
%G\simeq\langle (135)(246)(7\,11\,9)(8\,12\,10),(394\,10)(58)(67)(11\,12)\rangle?
%\]
We have constructed our group $G$.  Then we use the function \lstinline{IsomorphismPermGroup} to
construct a faithful representation of $G$ as a permutation group. With
\lstinline{SmallerDegreePermutationRepresentation} we construct (if possible) an isomorphic
permutation group of smaller degree. Be aware that this new degree may not be minimal.
After some attempts, we obtain 
an isomorphic copy of $G$ inside $\Sym_{12}$. To construct a set of generators we then use 
\lstinline{SmallGeneratingSet}. Again, be aware that this set may not be minimal.
\end{example}

For a finite group $G$, let $\cs(G)$ denote the set of sizes of the conjugacy
classes of $G$, that is
\[
\cs(G)\coloneqq \{|g^G|:g\in G\}.
\]  Let us write a function to compute $\cs$. With this function
we show that
  \[
    \cs(\Sym_3)=\{1,2,3\},
    \quad
    \cs(\Alt_5)=\{1,12,15,20\},
    \quad
    \cs(\SL_2(3))=\{1,4,6\}.
  \]
Here is the code:
\begin{lstlisting}
gap> cs := function(group)
> return Set(List(ConjugacyClasses(group), Size));
> end;
function( group ) ... end
gap> cs(SymmetricGroup(3));
[ 1, 2, 3 ]
gap> cs(AlternatingGroup(5));
[ 1, 12, 15, 20 ]
gap> cs(SL(2,3));
[ 1, 4, 6 ]
\end{lstlisting}

We will write $G_{n,k}$ to denote the $k$-th group of size $n$ in the database,
thus $G_{n,k}$ is a group with \lstinline{IdGroup} equal to \lstinline{[ n, k ]}.

\begin{example}
  \index{Navarro, G.}
  \label{example:Navarro}
	This example is taken from~\cite[Theorem A]{MR3210919} and
	answers a question made by R. Brauer~\cite[Question 2(ii)]{MR2875589}.
  G. Navarro proved that there exist finite groups $G$ and $H$ such that $G$ is
  solvable, $H$ is not solvable and $\cs(G)=\cs(H)$.

  Let $G=G_{240,13}\times G_{960,1019}$ and
  $H=G_{960,239}\times G_{480,959}$.  Then $G$ is solvable and $H$ is not. Moreover, 
  $\cs(G)=\cs(H)$ as the following code shows:
\begin{lstlisting}
gap> U := SmallGroup(960,239);;
gap> V := SmallGroup(480,959);;
gap> L := SmallGroup(960,1019);;
gap> K := SmallGroup(240,13);;
gap> UxV := DirectProduct(U,V);;
gap> KxL := DirectProduct(K,L);;
gap> IsSolvable(UxV);
false
gap> IsSolvable(KxL);
true
\end{lstlisting}
One could try to compute $\cs(U\times V)$ directly. However, this calculation
seems to be hard. The trick is to use that
$\cs(U\times V)=\{nm:n\in\cs(U),m\in\cs(V)\}$.

Here is the code:
\begin{lstlisting}
gap> cs(KxL)=Set(List(Cartesian(cs(U),cs(V)), x->x[1]*x[2]));
true
\end{lstlisting}
\end{example}

\begin{example}
This example appeared in~\cite{MR3210919}. It answers another question rised by R. Brauer~\cite[Question 4(ii)]{MR2875589}.
  G. Navarro proved that there exist finite groups $G$ and $H$ such that $G$ is
  nilpotent, $Z(H)=1$ and $\cs(G)=\cs(H)$.

  The groups are $G=\D_8\times G_{243,26}$ and
  $H=G_{486,36}$. Here is the code:
\begin{lstlisting}
gap> K := DihedralGroup(8);;
gap> L := SmallGroup(243,26);;
gap> H := SmallGroup(486,36);;
gap> IsTrivial(Center(H));
true
gap> G := DirectProduct(K,L);;
gap> cs(G)=cs(H);
true
gap> IsNilpotent(G);
true
\end{lstlisting}
\end{example}



\begin{example}
We prove that the smallest finite perfect
group that is not quasisimple 
is a group of the form $C_2^4\rtimes\Alt_5$. 
Recall that a group $G$ is said to be 
\emph{quasisimple} if $G$ is perfect and 
$G/Z(G)$ is (non-abelian) simple. The following function
checks if a given group is quasisimple:
\begin{lstlisting}
gap> IsQuasisimple := function(G)
> return IsPerfectGroup(G) and IsSimple(G/Center(G));
> end;
function( G ) ... end
\end{lstlisting}
Note that we do not need to check that
the quotient $G/Z(G)$ is non-abelian. 

With
\lstinline{SizeNumbersPerfectGroups} 
we list the orders of perfect groups in the database
and find the group needed:
\begin{lstlisting}
gap> for x in SizeNumbersPerfectGroups() do
> G := PerfectGroup(x);
> if not IsQuasisimple(G) then
>   Display(x);
>   break;
> fi;
> od;
[ 960, 1 ]
gap> StructureDescription(G);
"(C2 x C2 x C2 x C2) : A5"
\end{lstlisting}
There are two perfect groups of order 960. Both 
groups are not quasisimple:
\begin{lstlisting}
gap> NrPerfectGroups(960);
2
gap> IsQuasisimple(PerfectGroup(960, 2));
false
\end{lstlisting}
\end{example}

\begin{example}
\label{ex:Blaucommutators}
In \cite{MR1254833} H. Blau proved 
proved that there exist (finitely many) quasisimple 
groups that contain central elements that are non-commutators. The 
smallest of such groups is 
a Schur covering of $\Alt_6$.

We first write an efficient function to compute
the set of commutators of a given group:
\begin{lstlisting}
gap> SetOfCommutators := function(G)
> local T;
> T := Elements(RightTransversal(G, Center(G)));
> return Set(Cartesian(T, T), x->Comm(x[1], x[2]));
> end;
function( G ) ... end
\end{lstlisting}
Note that this function is more efficient than 
the one we presented in page \pageref{ex:commutatorElementsS16}. 

In the following
code is crucial to construct perfect groups
as permutation groups, as calculations will be much faster:
\begin{lstlisting}
gap> for x in SizeNumbersPerfectGroups() do
> G := PerfectGroup(IsPermGroup, x);
> dif := Difference(Center(G), SetOfCommutators(G));
> if Size(dif) > 0 then
>   Display(x);
>   break;
> fi;
> od;
[ 2160, 1 ]
gap> A6 := AlternatingGroup(6);;
gap> not IsomorphismGroups(SchurCover(A6), G) = fail;
true
\end{lstlisting}
\end{example}

\begin{example}
Let us prove the following claim: There is no sharply $4$-transitive group of
degree seven or nine, see~\cite[Exercise 1.18]{MR1721031}.
Recall that
a group $G$ is sharply $n$-transitive if for any two
sequences of distinct points $x_1,\dots,x_n$ and
$y_1,\dots,y_n$ there exists a unique $g\in G$ 
such that $x_i^g=y_i$ for all $i\in\{1,\dots,n\}$. 

We first create a
list of all transitive groups of degrees seven and nine and we keep only those
groups that are at least $4$-transitive:
\begin{lstlisting}
gap> l := Filtered(AllTransitiveGroups(NrMovedPoints, [7,9]), \\
> x->Transitivity(x)>3);                                
[ A7, S7, A9, S9 ]
\end{lstlisting}
Finally, we check that none of these groups 
are sharply $4$-transitive. For that purpose, we see that the action on tuples
is never regular:
\begin{lstlisting}
gap> true in List(l, x->IsRegular(x, \\ 
> Arrangements([1..NrMovedPoints(x)], 4), OnTuples));
false
\end{lstlisting}
It is known that a finite permutation group which does not contain the alternating group is at most 5-transitive. 
Except for the alternating and symmetric groups, the only finite groups which are 4- or 5-transitive are the Mathieu groups $M_{11}$, $M_{12}$, $M_{23}$ and $M_{24}$. 
The proof of this statement depends on the classification of finite simple groups.
\end{example}

\begin{example}
Let us prove the following claim: Primitive groups of degree eight are
double transitive. First we note that there are seven primitive groups of
degree eight:
\begin{lstlisting}
gap> l := AllPrimitiveGroups(NrMovedPoints, 8);
[ AGL(1, 8), AGammaL(1, 8), ASL(3, 2), 
  PSL(2, 7), PGL(2, 7), A(8), S(8) ]
\end{lstlisting}
To check that all these groups are indeed at least $2$-transitive, we use the
function \lstinline{Transitivity}:
\begin{lstlisting}
gap> List(l, Transitivity);
[ 2, 2, 3, 2, 3, 6, 8 ]
gap> ForAll(last, x->x>1);                 
true
\end{lstlisting}
\end{example}


The \emph{commuting probability} of a finite group $G$ is defined as the probability
that a randomly chosen pair of elements of $G$ commute, and it is thus equal to
$k(G)/|G|$. In Problem~\ref{prob:probability} we asked to write a function that
computes the commuting probability of a finite group. 
We first present a solution to this problem:

\begin{lstlisting}
gap> p := x->NrConjugacyClasses(x)/Order(x);
function( x ) ... end
\end{lstlisting}

In 1970 J. C. Dixon observed that the commuting probability of a finite
non-abelian simple group is $\leq 1/12$. This bound is attained for the
alternating simple group $\Alt_5$:

\begin{lstlisting}
gap> p(AlternatingGroup(5));                
1/12
\end{lstlisting}

One can find Dixon's proof in a 1973 volume of the \emph{Canadian Mathematical
Bulletin}. The proof we present here is based on a proof due to Iv\'an Sadofschi
Costa. We first assume that the commuting probability of $G$ is $>1/12$. Since
$G$ is a non-abelian simple group, the identity is the only central element. 
Let us assume first that there is a conjugacy class of $G$ of size $m$, where
$m$ is such that $1<m\leq 12$. Then $G$ is a transitive subgroup of $\Sym_m$.
For these groups the problem is easy: we show that there are no non-abelian simple groups
that act transitively on sets of size $m\in\{2,\dots,12\}$ with commuting
probability $>1/12$. To do this, we list these transitive groups and their commuting
probabilities and verify that all commuting probabilities are $\leq
1/12$:
\begin{lstlisting}
gap> l := AllTransitiveGroups(NrMovedPoints, [2..12], \\
> IsAbelian, false, \\
> IsSimple, true);;
[ A5, L(6) = PSL(2,5) = A_5(6), A6, 
  L(7) = L(3,2), A7, L(8)=PSL(2,7), A8, 
  L(9)=PSL(2,8), A9, A_5(10), L(10)=PSL(2,9), 
  A10, L(11)=PSL(2,11)(11), M(11), A11, A_5(12), 
  L(2,11), M_11(12), M(12), A12 ]
gap> List(l, p);           
[ 1/12, 1/12, 7/360, 1/28, 1/280, 1/28, 1/1440, 
  1/56, 1/10080, 1/12, 7/360, 1/75600, 2/165, 
  1/792, 31/19958400, 1/12, 2/165, 1/792, 1/6336, 
  43/239500800 ]
gap> ForAny(l, x->p(x)>1/12);
false
\end{lstlisting}

Now assume that all non-trivial conjugacy class of $G$ have at least 13 elements. 
Then the class equation implies that
\begin{align*}
	|G|&\geq \frac{13}{12}|G|-12,
	%&=\sum_{k\geq 1}k\times\text{number of conjugacy classes with $k$ elements}\\
	%&\geq 1+13\sum_{k\geq 13}\text{number of conjugacy classes with $k$ elements}\\
\end{align*}
and therefore $|G|\leq 144$. Thus one needs to check what happens with groups
of order $\leq 144$. 
But we know that the only non-abelian simple group of size
$\leq 144$ is the alternating simple group $\Alt_5$.
\begin{lstlisting}
gap> AllGroups(Size, [2..144], \\
> IsAbelian, false, \\
> IsSimple, true);
[ Alt( [ 1 .. 5 ] ) ]
\end{lstlisting}

\section{Solvable groups}

We now describe some algorithms and concepts 
related to solvable groups. 

We know that \lstinline{AllSubgroups} returns the list of all subgroups 
of a given group, but it is intended primarily for small examples, and 
may become inefficient for larger groups.
In general, it is better to use 
\lstinline{ConjugacyClassesSubgroups}. In the case of solvable
groups, there are better algorithms to compute conjugacy classes
of subgroups. The function 
\lstinline{SubgroupsSolvableGroup} returns a list of
representatives of 
subgroups up to conjugation. 

\begin{example}
    Let $P$ be the Sylow 2-subgroup of the Mathieu group $\operatorname{M}_{24}$.
    We want to compute the conjugacy classes of subgroups of $P$ of order $2^5$. We can
    proceed as follows:
\begin{lstlisting}
gap> P := SylowSubgroup(MathieuGroup(24), 2);;
gap> Number(ConjugacyClassesSubgroups(P), \ 
> x->Order(Representative(x))=2^5);;
1403
\end{lstlisting}
Thus there are 1403 conjugacy classes of subgroups of order $2^5$. 

Since $P$ is solvable, we can
try something different. If we use the function 
\lstinline{SubgroupsSolvableGroup} in combination
with \lstinline{ExactSizeConsiderFunction} the calculation
is about much faster (the reader should compare the different methods): 
\begin{lstlisting}
gap> Size(SubgroupsSolvableGroup(P, \ 
> rec(consider:=ExactSizeConsiderFunction(2^5))));time;
1508
\end{lstlisting}
The numbers are different! This happens because \lstinline{SubgroupsSolvableGroup} 
returned a list of subgroups of $P$ 
that includes all subgroups of order $2^5$ and (maybe)
other subgroups of $P$. To obtain the right 
number of subgroups we need to 
remove the unwanted subgroups (this is super fast, no extra time needed): 
\begin{lstlisting}
gap> Number(SubgroupsSolvableGroup(P, rec(consider := \ 
> ExactSizeConsiderFunction(2^5))) ,x->Order(x)=2^5);
1403
\end{lstlisting}

This does not mean that it is always more convenient to use
this function when dealing with solvable groups. For example, 
if we want to compute the list of normal subgroups, 
\lstinline{NormalSubgroups} is much 
faster than \lstinline{SubgroupsSolvableGroup} (again, the reader 
should compare the run-times of these methods): 
\begin{lstlisting}
gap> Size(NormalSubgroups(P));
157
gap> Number(SubgroupsSolvableGroup(P, \ 
> rec(normal:=true)), x->IsNormal(P,x));
157
\end{lstlisting}
Note that, again, \lstinline{SubgroupsSolvableGroup} produced a list
of subgroups that include all normal subgroups of $P$ and (maybe)
some other non-normal subgroups of $P$, so an extra filter is needed. 
\end{example}

Hall's theorem states that if $G$ is a finite solvable group of order $ab$ with
$\gcd(a,b)=1$, then there exists a unique (non-empty) conjugacy class of subgroups of 
of $G$ order $a$.  
Such subgroups of order $a$ are
called Hall $\pi$-subgroups of $G$, 
where $\pi$ is the set of prime divisors of $a$. 


\begin{example}
We now perform some calculations
with the function \lstinline{HallSubgroup}:
\begin{lstlisting}
gap> D60 := DihedralGroup(IsPermGroup, 60);;
gap> IsSolvable(D60);
true
gap> StructureDescription(HallSubgroup(D60, [2,3]));
"D12"
gap> StructureDescription(HallSubgroup(D60, [2,5]));
"D20"
gap> StructureDescription(HallSubgroup(D60, [3,5]));
"C15"
\end{lstlisting}

Now we show that the alternating simple group $\Alt_5$ 
of order 60 does not contain 
Hall subgroups of order 15 and 20: 
\begin{lstlisting}
gap> A5 := AlternatingGroup(5);;
gap> HallSubgroup(A5, [3,5]);
fail
gap> HallSubgroup(A5, [2,5]);
fail
\end{lstlisting}

Hall $\{2,3\}$-subgroups of the simple group $\PSL_2(7)$ of order 168 
are not conjugate (but isomorphic):  
\begin{lstlisting}
gap> G := PSL(2,7);;
gap> hall := HallSubgroup(G, [2,3]);;
gap> IsConjugate(G, hall[1], hall[2]);
false
gap> not IsomorphismGroups(hall[1], hall[2]) = fail;
true
\end{lstlisting}

We finally show that Hall $\{2,3\}$-subgroups of the 
simple group $\PSL_2(11)$ of order 660 
are not isomorphic (and hence not conjugate): 
\begin{lstlisting}
gap> G := PSL(2,11);;
gap> Order(PSL(2,11));
660
gap> hall := HallSubgroup(G, [2,3]);;
gap> IsomorphismGroups(hall[1], hall[2]);
fail
\end{lstlisting}    
\end{example}

\begin{example}
    Schottenfels' theorem \cite[Theorem 8.24]{MR1307623} 
    states that $\PSL_3(4)$ and $\Alt_8$ are simple 
    groups of the same order, but they are not isomorphic: 
\begin{lstlisting}
gap> G := PSL(3,4);;
gap> Order(G);
20160
gap> A8 := AlternatingGroup(8);;
gap> Order(A8);
20160
gap> IsomorphismGroups(G, A8);
fail
\end{lstlisting}
The fact that these groups are not isomorphic also follows
from the structure of Hall's subgroups:
\begin{lstlisting}
gap> HallSubgroup(G, [2, 3]);
fail
gap> StructureDescription(HallSubgroup(A8, [2, 3]));
"(C2 x C2 x C2 x C2) : (S3 x S3)"
\end{lstlisting}
\end{example}

\section{Representations}

%We can construct representations of finite groups. 
%There are different ways of doing this.
In \GAP~ we have different ways of constructing representations of finite groups.
Let us start with some examples. 

\begin{example}
Let us construct the representation $\rho$ of $\Alt_4$ given by 
\begin{equation*}
  (12)(34)\mapsto\begin{pmatrix}
    0 & 1 & -1\\
    1 & 0 & -1\\
    0 & 0 & -1
  \end{pmatrix},
  \quad
  (123)\mapsto\begin{pmatrix}
    0 & 0 & -1\\
    0 & 1 & -1\\
    1 & 0 & -1
  \end{pmatrix}.
\end{equation*}
We use the function \lstinline{GroupHomomorphismByImages}: 
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> a := [[0,1,-1],[1,0,-1],[0,0,-1]];;
gap> b := [[0,0,-1],[0,1,-1],[1,0,-1]];;
gap> rho := GroupHomomorphismByImages(A4,\
> [ (1,2)(3,4), (1,2,3) ], [ a, b ]);;
gap> IsGroupHomomorphism(rho);
true
\end{lstlisting}
This is indeed a faithful representation of $\Alt_4$:
\begin{lstlisting}
gap> IsTrivial(Kernel(rho));
true
\end{lstlisting}
Just to see how it works, let us compute $\rho_{(132)}$, the image of
$(132)\in\Alt_4$ under $\rho$. We are working with $3\times 3$ matrices so it
is better to use the function
\lstinline{Display}.
\begin{lstlisting}
gap> Display(Image(rho, (1,3,2)));                                              
[ [  -1,   0,   1 ],
  [  -1,   1,   0 ],
  [  -1,   0,   0 ] ]
\end{lstlisting}
Now we construct the character $\chi$ of $\rho$. We also check that $\rho$ is
irreducible since 
\[
\langle\chi,\chi\rangle=\frac{1}{|\Alt_4|}\sum_{x\in\Alt_4}\chi(x)\chi(x^{-1})=1.
\]
\begin{lstlisting}
gap> chi := x->TraceMat(x^rho);;
gap> 1/Order(A4)*Sum(List(A4, x->chi(x)*chi(x^(-1))));
1
\end{lstlisting}
\end{example}

We now construct irreducible representations of a given group. 
This can be done with the package \lstinline{Repsn}, 
written by V.  Dabbaghian. 

\begin{example}
Let us construct the irreducible representations of $\Sym_3$. 
The irreducible characters of a finite group
can be constructed with \lstinline{Irr}:
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> irr := Irr(S3);
[ Character( CharacterTable( Sym( [ 1 .. 3 ] ) ), [ 1, -1, 1 ] ), 
  Character( CharacterTable( Sym( [ 1 .. 3 ] ) ), [ 2, 0, -1 ] ), 
  Character( CharacterTable( Sym( [ 1 .. 3 ] ) ), [ 1, 1, 1 ] ) ]
\end{lstlisting}
To construct irreducible representations we need to load the package
\lstinline{repsn}:
\begin{lstlisting}
gap> LoadPackage("repsn");
\end{lstlisting}

The package contains \lstinline{IrreducibleAffordingRepresentation}. This function produces 
irreducible representations from irreducible characters. Since we are working with $\Sym_3$, we will only need
to consider the character of degree two. We will produce 
the faithful representation $\Sym_3\to\GL_2(\C)$ given by 
\[
	(123)\mapsto\begin{pmatrix}
		\omega^2 & 0\\
		0 & \omega
	\end{pmatrix},
	\quad
	(12)\mapsto\begin{pmatrix}
		0 & \omega \\
		\omega^2 & 0
	\end{pmatrix}.
\]
Here is the code:
\begin{lstlisting}
gap> f := IrreducibleAffordingRepresentation(irr[2]);
[ (1,2,3), (1,2) ] -> [ [ [ E(3)^2, 0 ], [ 0, E(3) ] ], 
  [ [ 0, E(3) ], [ E(3)^2, 0 ] ] ]
gap> Image(f, (1,2,3));
[ [ E(3)^2, 0 ], [ 0, E(3) ] ]
gap> Display(Image(f, (1,2,3)));
[ [  E(3)^2,       0 ],
  [       0,    E(3) ] ]
gap> Display(Image(f, (1,2)));
[ [       0,    E(3) ],
  [  E(3)^2,       0 ] ]
\end{lstlisting}
\end{example}


\index{Brauer, R.}
In~\cite[Problem 1]{MR0178056}, R. Brauer asked which algebras are group
algebras. This question might be very hard to answer in a general situation.
Here we play with some particular examples and combine algebra theorems with \GAP~to get an answer to this question.

\begin{example}
	Is $\C\times M_2(\C)\times M_{5}(\C)$ a (complex) group algebra? The answer is no.  
    By Wedderburn's theorem, if $\C\times M_2(\C)\times M_{5}(\C)$ is the group algebra of some group $G$, then
	$G$ has order 30 with three 
	irreducible characters of 
	degrees one, two and five, respectively. We will
	prove that there are no such groups.  
	
We list the degrees
of irreducible characters of groups of order $30$. We see that there are four
groups of order $30$ and none of them has exactly three 
irreducible characters: 
\begin{lstlisting}
gap> n := 30;;
gap> for G in AllGroups(Size, n) do
> Display(Size(Irr(G)));
> od;
15
12
9
30
\end{lstlisting}
We now compute the degrees of the irreducible characters
of groups of order 30:
\begin{lstlisting}
gap> for G in AllGroups(Size, n) do
> Print(CharacterDegrees(G), "\n");
> od;
[ [ 1, 10 ], [ 2, 5 ] ]
[ [ 1, 6 ], [ 2, 6 ] ]
[ [ 1, 2 ], [ 2, 7 ] ]
[ [ 1, 30 ] ]
\end{lstlisting}
In fact, this shows that the groups algebras of groups of order $30$
are 
\begin{align*}
  &&\C^{10}\times M_2(\C)^5,
  &&\C^{6}\times M_2(\C)^6,
  &&\C^{2}\times M_2(\C)^7,
  &&\C^{30}.
\end{align*}
\end{example}

Remarkably,~\GAP~contains a large database 
of representations and character tables. For example, it includes 
all character tables of sporadic simple groups and several 
of their matrix (over the rationals and finite fields) and permutation
representations. 

\begin{example}
    \label{exa:degreesJ1}
    The package \lstinline{CTblLib}, written by T. Breuer, 
    contains character tables of some groups. The first
    Janko group $\operatorname{J}_1$ is a simple group of 
    order 175560. It has 
    15 conjugacy classes (and hence 15
    irreducible representations). 
    We can get all this information only from the character table:
\begin{lstlisting}
gap> ct := CharacterTable("J1");;
gap> Size(ct);
175560
gap> IsSimple(ct);
true
gap> NrConjugacyClasses(ct);
15
gap> Size(Irr(ct));
15
gap> LinearCharacters(ct);
[ Character( CharacterTable( "J1" ),
  [ 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1 ] ) ]
gap> CharacterDegrees(ct);
[ [ 1, 1 ], [ 56, 2 ], [ 76, 2 ], [ 77, 3 ],
  [ 120, 3 ], [ 133, 3 ], [ 209, 1 ] ]
gap> SizesConjugacyClasses(ct);
[ 1, 1463, 5852, 5852, 5852, 29260, 25080, 17556,
  17556, 15960, 11704, 11704, 9240, 9240, 9240 ]
gap> SizesCentralizers(ct);
[ 175560, 120, 30, 30, 30, 6, 7, 10, 10, 11, 15, 15,
  19, 19, 19 ]
\end{lstlisting}
\end{example}


For a finite group $G$ and a prime $p$ such that $p$ divides $|G|$ one defines
\[
	\Irr_{p'}(G)=\{\chi\in\Irr(G):p\nmid\chi(1)\}.
\]

The following conjecture was posed by J. McKay for simple 
groups and $p=2$ in \cite{McKayNoticesAMS}, and
it was later generalized by M. Isaacs \cite{MR332945}: 

\begin{conjecture}[McKay]
\index{McKay's conjecture}
\label{conj:McKay}
Let $G$ be a finite group. If $P\in\Syl_p(G)$,
then 
  \[
	|\Irr_{p'}(G)|=|\Irr_{p'}(N_G(P))|.
  \]
\end{conjecture}

The conjecture is still open; we refer to 
\cite{MR3753712} for the state-of-the-art. 

\begin{example}[Testing McKay's conjecture]
\label{exa:McKay}
It is believed that the McKay conjecture is true.  Let us check the conjecture
in some small examples. We first write a naive 
function that checks the conjecture:
\begin{lstlisting}
gap> McKay := function(G, p)
> local N, n, m;
> N := Normalizer(G, SylowSubgroup(G, p));
> n := Number(Irr(G), x->Degree(x) mod p <> 0);
> m := Number(Irr(N), x->Degree(x) mod p <> 0);
> if n = m then
>   return true;
> else
>   return false;
> fi;
> end;
function( G, p ) ... end
\end{lstlisting}
With this function is now easy to check the conjecture in several small
examples. Let us verify that the McKay conjecture is true for $\SL_2(3)$. This
group has order 24, so we need to check the conjecture holds for $p\in\{2,3\}$:
\begin{lstlisting}
gap> McKay(SL(2,3), 2);
true
gap> McKay(SL(2,3), 3);
true
\end{lstlisting}

The package \lstinline{CTblLib} 
contains character tables of some normalizers of Sylow subgroups
of sporadic simple groups. 
For example, to obtain the character table of the 
normalizer of the Sylow 2-subgroup of the Thompson sporadic group $\operatorname{Th}$ 
we proceed as follows:
\begin{lstlisting}
gap> CharacterTable("ThN2");
CharacterTable( "ThN2" ) 
\end{lstlisting}
However, not all character tables of normalizer of Sylow subgroups are stored:
\begin{lstlisting}
gap> CharacterTable("Co1N2");
fail
\end{lstlisting}

We can use this database of normalizers of Sylow subgroups to check 
McKay's conjecture for some sporadic simple groups 
more efficiently:
\begin{lstlisting}
gap> McKay := function(name)
> local t, t_N, p;
> t := CharacterTable(name);
> for p in PrimeDivisors(Size(t)) do
>   t_N := CharacterTable(Concatenation(name, "N", String(p)));
>   if t_N = fail then
>     return fail;
>   else
>     if not Number(Irr(t), x->Degree(x) mod p <> 0) = \ 
>     Number(Irr(t_N), x->Degree(x) mod p <> 0) then
>       return false;
>     fi;
>   fi;
> od;
> return true;
> end;
function( name ) ... end
\end{lstlisting}
We use this function to verify the conjecture
for several sporadic simple groups:
\begin{lstlisting}
gap> McKay("Fi23");
true
gap> McKay("Th");
true
\end{lstlisting}
Note that the database we are using 
does not contain all normalizers 
of Sylow subgroups for the Monster
group:
\begin{lstlisting}
gap> McKay("M");
fail
\end{lstlisting}
We remark that in \cite{MR1643110}, R. Wilson proved that
McKay's conjecture is true
for the sporadic simple groups using a description of 
the normalizers of their Sylow subgroups. 
\end{example}

In \cite{MR1935849}, M. Isaacs and G. Navarro 
proposed a refinement of Conjecture \ref{conj:McKay}.
For a prime number $p$, an integer $k\in\Z$ coprime with $p$, and a finite group $G$, let
\[
  M_k(G)=|\{\chi\in\Irr_{p'}(G):\chi(1)\equiv\pm k\bmod p\}|.
\]

The following conjecture proposed by Isaacs and Navarro is still open and implies McKay's conjecture.

\begin{conjecture}[Isaacs--Navarro]
\index{Isaacs, M.}
\index{Navarro, G.}
\index{Isaacs--Navarro's conjecture}
Let $G$ be a finite group and $P$ be a Sylow $p$-subgroup
of $G$. If $k\in\Z$ is coprime with $p$, then 
\[
    M_k(G)=M_k(N_G(P)).
\]
\end{conjecture}

Let us see some examples where we can test the Isaacs-Navarro conjecture.

\begin{example}[Testing Isaacs--Navarro conjecture]
\index{Isaacs--Navarro conjecture}
Here we have a function that checks the Isaacs--Navarro conjecture:
\begin{lstlisting}
gap> IsaacsNavarro := function(G, k, p)
> local mG, mN, N;
> N := Normalizer(G, SylowSubgroup(G, p));
> mG := Number(Filtered(Irr(G), x->Degree(x)\
> mod p <> 0), x->Degree(x) mod p in [-k,k] mod p);
> mN := Number(Filtered(Irr(N), x->Degree(x)\
> mod p <> 0), x->Degree(x) mod p in [-k,k] mod p);
> if mG = mN then
>   return true;
> else
>   return false;
> fi;
> end;
function( G, k, p ) ... end
\end{lstlisting}

Let us check that the Isaacs--Navarro conjecture is true for the group
$\SL_2(3)$. We only need to check the conjecture for $k\in\{1,2\}$ and
$p\in\{2,3\}$. 
\begin{lstlisting}
gap> IsaacsNavarro(SL(2,3), 1, 2);
true
gap> IsaacsNavarro(SL(2,3), 1, 3);
true
gap> IsaacsNavarro(SL(2,3), 2, 3);
true
\end{lstlisting}
\end{example}

In 1951, Ore and, independently, Ito
proved that every element of any alternating simple group is a commutator. Ore also mentioned that ``it is possible that a similar theorem holds for any simple group of finite order, but it seems that at present we do not have the necessary methods to investigate the question".
This gave rise to the Ore conjecture.

\begin{conjecture}[Ore]
\index{Ore's conjecture}
    Every element of any finite simple group is a commutator.
\end{conjecture}

The conjecture is now a theorem thanks 
to the work of M. Liebeck, E. O'Brien, A. Shalev and P. Tiep \cite{MR2654085}.

\begin{example}[Testing Ore's conjecture]
\label{exa:Ore}
\index{Ore's conjecture}
\index{M. Liebeck}
\index{E. O'Brien}
\index{A. Shalev}
\index{P. Tiep}
We will prove Ore's conjecture for some
sporadic simple groups. Let $G$ be a finite simple group.
It is known that $g\in G$ is a commutator if and only if
\[
\sum_{\chi\in\Irr(G)}\frac{\chi(g)}{\chi(1)}\ne 0.
\]
Let us write
a computer script to check whether every element in a group
is a commutator. Our
function needs the character table of a group and returns
\lstinline{true} if every element of the group is a commutator and \lstinline{false} otherwise:
\begin{lstlisting}
gap> Ore := function(ct)
> local s,f,k;
> for k in [1..NrConjugacyClasses(ct)] do
>   s := 0;
>   for f in Irr(ct) do
>     s := s+f[k]/Degree(f);
>   od;
>   if s<=0 then
>     return false;
>   fi;
> od;
> return true;
> end;
function( ct ) ... end
\end{lstlisting}

Now, for example,   
we check the conjecture for the first Janko group, the 
Mathieu simple groups and 
the Monster group:
\begin{lstlisting}
gap> Ore(CharacterTable("J1"));
true
gap> Ore(CharacterTable("M11"));
true
gap> Ore(CharacterTable("M12"));
true
gap> Ore(CharacterTable("M22"));
true
gap> Ore(CharacterTable("M23"));
true
gap> Ore(CharacterTable("M24"));
true
gap> Ore(CharacterTable("M"));
true
\end{lstlisting}
\end{example}

\index{Atlas of finite group representations}
The package \lstinline{AtlasRep} provides
an interface to the \emph{Atlas of Finite Group Representations}. 
It comprises representations 
of some (almost) simple groups and 
information about their maximal subgroups and conjugacy classes. 
It was prepared by 
R. Abbott, J. Bray, S. Linton, S. Nickerson, 
S. Norton, R. Parker, I. Suleiman, J. Tripp, P. Walsh and
R. Wilson.  To load the package we proceed as follows:
\begin{lstlisting}
gap> LoadPackage("atlasrep");
\end{lstlisting}
We refer to the \lstinline{atlasrep} manual 
for information related to the different ways of 
obtaining the database from the Internet. 

\begin{example}
We compute the structure
of the Sylow subgroups of the first Janko group $\operatorname{J}_1$. By default, 
the package uses a permutation representation (inside $\Sym_{175560}$) 
for the group $\operatorname{J}_1$: 
\begin{lstlisting}
gap> J1 := AtlasGroup("J1");
<permutation group of size 175560 with 2 generators>
gap> Order(J1);
175560
gap> Collected(Factors(Order(J1)));
[ [ 2, 3 ], [ 3, 1 ], [ 5, 1 ], [ 7, 1 ], [ 11, 1 ], [ 19, 1 ] ]
gap> for p in PrimeDivisors(Order(J1)) do
> Display(StructureDescription(SylowSubgroup(J1, p)));
> od;
C2 x C2 x C2
C3
C5
C7
C11
C19
\end{lstlisting}
With the command
\begin{lstlisting}
gap> DisplayAtlasInfo("J1");
\end{lstlisting}
we see all the information available on the group
$\operatorname{J}_1$. In particular, 
the package contains 91 (faithful) representations 
for $\operatorname{J}_1$. The function
\lstinline{AtlasGenerators} returns
a record with information on a particular representation. 
For example, the 47th one is a representation
of dimension seven over the field $\F_{11}$: 
\begin{lstlisting}
gap> recJ1 := AtlasGenerators("J1", 47);;
gap> recJ1!.ring;
GF(11)
gap> gens := recJ1!.generators;
[ < immutable compressed matrix 7x7 over GF(11) >,
  < immutable compressed matrix 7x7 over GF(11) > ]
gap> Display(gens[1]);
  .  7  9  9 10  7  9
  7  .  1  4  3  3  4
  9  1  2  8  3  6  2
  9  4  8 10  1  6  .
 10  3  3  1  8  9  1
  7  3  6  6  9  1  3
  9  4  2  .  1  3  .
gap> Display(gens[2]);
  5  7  1  8  5  8  2
 10  4 10  .  3  3  8
  5 10  8  9  1  2  1
  2  3  3  6  1  2  9
  7  4  7  2 10  7  3
  3  9  .  4  2  3  5
  3  4  3  3  9  4  9
gap> Order(Group(gens));
175560
gap> IsSimple(Group(gens));
true
\end{lstlisting}
\end{example}

\begin{example}
    We now construct the first Conway group
    $\operatorname{Co}_1$ and check that 
    each Sylow 2-subgroup is self-normalizing:
\begin{lstlisting}
gap> Co1 := AtlasGroup("Co1");
<permutation group of size 4157776806543360000 with 2 generators>
gap> Order(Co1);
4157776806543360000
gap> IsSimple(Co1);
true
gap> P := SylowSubgroup(Co1, 2);
<permutation group of size 2097152 with 16 generators>
gap> N := Normalizer(Co1, P);
<permutation group with 16 generators>
gap> Order(N)=Order(P);
true
\end{lstlisting}
As an application, we check 
McKay's conjecture for the group $\operatorname{Co}_1$ 
and the prime $p=2$. To compute
$|\Irr_{p'}(\operatorname{Co}_1)|$ efficiently
we use the character table of 
the first Conway group
included in the \lstinline{CTblLib} package:

\begin{lstlisting}
gap> Number(Irr(CharacterTable("Co1")), x->Degree(x) mod 2 <> 0);
32
gap> irr := Irr(N);;
gap> Number(irr, x->Degree(x) mod 2 <> 0);
32
\end{lstlisting}
\end{example}

In the following examples we play with non-isomorphic groups which have the same character tables.
Recall that two finite groups are said to have the same character tables if their character tables differ from permutations of rows and columns.

\begin{example}
    It is well-known that the groups $\D_{8}$ and $Q_8$ 
    are non-isomorphic and yet have the same character table. 
    This can be easily seen as follows:
\begin{lstlisting}
gap> D8 := DihedralGroup(8);;
gap> ct_D8 := CharacterTable(D8);;
gap> Display(ct_D8);
CT1

     2  3  2  2  3  2

       1a 2a 4a 2b 2c

X.1     1  1  1  1  1
X.2     1 -1  1  1 -1
X.3     1  1 -1  1 -1
X.4     1 -1 -1  1  1
X.5     2  .  . -2  .
gap> Q8 := QuaternionGroup(8);;
gap> ct_Q8 := CharacterTable(Q8);;
gap> Display(ct_Q8);
CT2

     2  3  2  2  3  2

       1a 4a 4b 2a 4c
    2P 1a 2a 2a 1a 2a
    3P 1a 4a 4b 2a 4c

X.1     1  1  1  1  1
X.2     1 -1 -1  1  1
X.3     1 -1  1  1 -1
X.4     1  1 -1  1 -1
X.5     2  .  . -2  .
\end{lstlisting}
It can proved that $\D_{8}$ and $Q_8$ are not isomorphic in many different ways.
For example, this follows from counting the number of elements of order two:
\begin{lstlisting}
gap> OrdersClassRepresentatives(ct_D8);
[ 1, 2, 4, 2, 2 ]
gap> OrdersClassRepresentatives(ct_Q8);
[ 1, 4, 4, 2, 4 ]
\end{lstlisting}
With the function \lstinline{TransformingPermutations} 
we verify that 
the matrix of characters 
are equivalent: 
\begin{lstlisting}
gap> Display(Irr(ct_D8));
[ [   1,   1,   1,   1,   1 ],
  [   1,  -1,   1,   1,  -1 ],
  [   1,   1,  -1,   1,  -1 ],
  [   1,  -1,  -1,   1,   1 ],
  [   2,   0,   0,  -2,   0 ] ]
gap> Display(Irr(ct_Q8));
[ [   1,   1,   1,   1,   1 ],
  [   1,  -1,  -1,   1,   1 ],
  [   1,  -1,   1,   1,  -1 ],
  [   1,   1,  -1,   1,  -1 ],
  [   2,   0,   0,  -2,   0 ] ]
gap> TransformingPermutations(Irr(ct_D8), Irr(ct_Q8));
rec( columns := (), group := Group([ (3,5), (2,3) ]),
  rows := (2,3,4) )
\end{lstlisting}
\end{example}

A \emph{Brauer pair} is a pair of non-isomorphic finite groups 
with the same
character tables and power maps (up to permutation); 
we refer to \cite[Definition 2.6.1]{MR2680716} for a more precise definition. 
The notion is based on a question posed by R. Brauer in 
\cite{MR0178056}, where he asked whether such pairs exist. 
The first example of a Brauer pair goes back to E. Dade \cite{MR170957}. 

\begin{example}[Dade's solution to Brauer's problem]
\index{Brauer's pairs}
    Dade's paper shows the existence of Brauer pairs of groups 
    of order $p^7$, for $p\geq5$ a prime number. 
    These groups
    have nilpotency class three 
    and exponent $p$ (with this, the condition on  
    power maps is automatically satisfied once we know the groups
    have the same character table). 
    
    To check character tables up to permutations of rows and columns 
    one uses the function \lstinline{TransformingPermutations}. However, 
    as the use of this function is quite expensive 
    in our groups, we will try to 
    find two non-isomorphic 
    groups of order $5^7$ of nilpotency class three
    and exponent five 
    with \emph{exactly} the same character table:
\begin{lstlisting}
gap> p := 5;;
gap> m := 7;;
gap> list :=  AllSmallGroups(Size, p^m, Exponent, \ 
> p, NilpotencyClassOfGroup, 3);;
> tables := List(list, CharacterTable);;
> for c in Combinations([1..Size(list)], 2) do
>   x := tables[c[1]];
>   y := tables[c[2]];
>   if Irr(x) = Irr(y) then
>     Display(IdGroup(UnderlyingGroup(x)));
>     Display(IdGroup(UnderlyingGroup(y)));
>     break;
>   fi;
> od;
[ 78125, 19912 ]
[ 78125, 19913 ]
\end{lstlisting}
\end{example}

\begin{example}
\index{Brauer's pairs}
    In \cite{MR2256391}, B. Eick and J. M\"uller 
    mention that E. Skrzipczyk found pairs 
    of non-isomorphic 
    groups of order $2^8$ 
    with the same character table and 
    power maps. The following code
    uses Skrzipczyk's ideas in the case of  
    groups of order $3^6$:
\begin{lstlisting}
gap> p := 3;
gap> m := 6;
gap> list := AllSmallGroups(Size, p^m, Exponent, p^2);;
gap> tables := List(list, CharacterTable);;
gap> for c in Combinations([1..Size(list)], 2) do
> x := tables[c[1]];
> y := tables[c[2]];
> if not NrConjugacyClasses(x) = NrConjugacyClasses(y) then
>   continue;
> fi;
> if not Set(CharacterDegrees(x)) = Set(CharacterDegrees(y)) then
>   continue;
> fi;
> m_y := Irr(y);
> PowerMap(y, p^m);
> if not \ 
> TransformingPermutationsCharacterTables(x, y) = fail then
>   Display([IdGroup(UnderlyingGroup(x)), \ 
>   IdGroup(UnderlyingGroup(y))]);
>   break;
> fi;
> od;
[ [  729,   44 ],
  [  729,   45 ] ]
\end{lstlisting}
\end{example}

In \cite{MR783067} the authors list several conjectures
on products of conjugacy classes in finite non-abelian simple groups. 

\begin{conjecture}[Thompson]
\index{Thompson's conjecture}
    Let $G$ be a finite non-abelian simple group. Then there
    exists a conjugacy class $C$ of $G$ such that 
    $C^2=G$, where 
    $C^2=\{xy:x,y\in C\}$. 
\end{conjecture}

The conjecture is still open.

\begin{example}[Testing Thompson's conjecture]
    We first present a function that
    checks the conjecture in a very naive way:
\begin{lstlisting}
gap> Thompson := function(G)
> local squareSet;
> # Return the square of a set
> squareSet := function(s)
>   return Set(Cartesian(s, s), x->x[1]*x[2]);
> end;
> return ForAny(ConjugacyClasses(G), \ 
> x->Size(squareSet(x)) = Order(G));
> end
function( G ) ... end
\end{lstlisting}
Now we test the conjecture is some small simple groups:
\begin{lstlisting}
gap> Thompson(AlternatingGroup(5));
true
gap> Thompson(MathieuGroup(11));
true
gap> Thompson(PSL(2,7));
true
\end{lstlisting}
On the other hand, Thompson's conjecture is about (non-abelian) simple 
groups, and it may not hold for arbitrary groups: 
\begin{lstlisting}
gap> Thompson(SymmetricGroup(4));
false
gap> Thompson(SymmetricGroup(5));
false
gap> Thompson(SL(2,7));
false
\end{lstlisting}

    Note that if $C$ is a conjugacy class of $G$ such that 
    $C^2=G$, then $C$ is a real conjugacy class, which means that 
    $C=C^{-1}=\{x^{-1}:x\in C\}$. 
    Equivalently, $C$ is real if and only if $\chi(x)\in\R$ 
    for $x\in C$. To check if a given conjugacy class is real  
    we can use the following code: 
    
\begin{lstlisting}
gap> IsReal := function(C)
> return Inverse(Random(C)) in C;
> end;
function( C ) ... end
gap> A4 := AlternatingGroup(4);;
gap> cc := ConjugacyClasses(A4);
[ ()^G, (1,2)(3,4)^G, (1,2,3)^G, (1,2,4)^G ]
gap> List(cc, IsReal);
[ true, true, false, false ]
\end{lstlisting}

    The use of real conjugacy classes could make
    our previous function slightly better. However, 
    there is a much better approach that uses
    character tables, as we explain below.

    In \cite{MR3289286} 
    Malle mentions a useful technique 
    to check Thompson's conjecture in sporadic simple groups: If $G$
    is a finite group and $C$ is a real class, then
    $C^2=G$ if and only if, for some fixed $x\in C$, we have that
    \[
        \sum_{\chi\in\Irr(G)}\frac{|\chi(x)|^2\chi(g)}{\chi(1)}\ne0,
    \]
    for all $g\in G$. 
    See \cite[Exercise 2.6.3]{MR2680716}. 
    
    Real conjugacy classes 
    can be obtained from a character table
    using the function \lstinline{RealClasses} of the \lstinline{CTblLib} package:
\begin{lstlisting}
gap> t_A4 := CharacterTable("A4");;
gap> NrConjugacyClasses(t_A4);
4
gap> OrdersClassRepresentatives(t_A4);
[ 1, 2, 3, 3 ]
gap> RealClasses(t_A4);
[ 1, 2 ]
\end{lstlisting}
    We now present another function that checks
    if Thompson's conjecture is true:
\begin{lstlisting}
gap> Thompson := function(t)
> local k, C;
> k := NrConjugacyClasses(t);
> for C in RealClasses(t) do
>   if ForAll([1..k], D->Sum(Irr(t), \ 
>   f->(f[C]^2*f[D])/Degree(f)) <> 0) then
>     return true;
>   fi;
> od;
> return false;
> end;
function( t ) ... end
gap> Thompson(CharacterTable("M"));
true
gap> Thompson(CharacterTable("A10"));
true
gap> Thompson(CharacterTable("Th"));
true
gap> Thompson(CharacterTable("S10"));
false
\end{lstlisting}
\end{example}

During the study of Thompson's conjecture above and Szet's conjecture, in \cite{MR783067} Z. Arad and M. Herzog proposed the following conjecture, which is still open:

\index{Arad--Herzog's conjecture}
\begin{conjecture}[Arad--Herzog]
\label{conj:AradHerzog}
If $G$ is a non-abelian simple group, then 
the product of two non-trivial conjugacy classes of $G$ 
is never a single conjugacy class. 
\end{conjecture}

We refer the reader to \cite{MR3003939} for more details on the state-of-the-art of this conjecture, which is still open.

In the spirit of the previous conjectures, we produce different functions to test the Arad--Herzog conjecture on some simple groups.

\begin{example}[Testing Arad--Herzog's conjecture]
\index{Arad--Herzog conjecture}
We first write a naive function to test if the 
conjecture is true or false:
\begin{lstlisting}
gap> AradHerzog := function(G)
> local classes, c, d, g, s;
> # We only consider non-trivial conjugacy classes
> classes := Filtered(ConjugacyClasses(G), x->Size(x)>1);
> for c in classes do
>   for d in classes do
>     s := Set(List(Cartesian(c, d), x->x[1]*x[2]));
>     g := Random(s);
>     if Size(s) = Size(ConjugacyClass(G, g)) then
>       return false;
>     fi;
>   od;
> od;
> return true;
> end;
function( G ) ... end
\end{lstlisting}
With this function we can test the conjecture
in some small simple groups:
\begin{lstlisting}
gap> AradHerzog(AlternatingGroup(5));
true
gap> AradHerzog(AlternatingGroup(6));
true
\end{lstlisting}
However, the conjecture does not hold in general for non-simple groups:
\begin{lstlisting}
gap> AradHerzog(DihedralGroup(6));
false
gap> AradHerzog(AbelianGroup([8,4]));
true
gap> AradHerzog(QuaternionGroup(32));
false
\end{lstlisting}
Note that conjugacy classes are trivial in abelian groups, so the conjecture vacuously holds for this class of groups.

We will now write a better version of 
the function to test if the conjecture holds. The reader
should check that the following function 
does test the conjecture:
\begin{lstlisting}
gap> AradHerzog := function(G)
> local classes, c, g, h, s
> # We only consider non-trivial conjugacy classes
> classes := Filtered(ConjugacyClasses(G), x->Size(x)>1);
> for c in classes do
>   for g in List(classes, Representative) do
>     s := Set(List(c, x->x*g));
>     h := Random(s);
>     if ForAll(s, x->IsConjugate(G, x, h)) then
>       return false;
>     fi;
>   od;
> od;
> return true;
> end;
function( G ) ... end
\end{lstlisting}
Now we can prove the conjecture holds
in other simple groups such as, for example, 
$\operatorname{Sz}(8)$, 
$\PSL_2(8)$, $\PSL_3(5)$ and $\operatorname{J}_1$:
\begin{lstlisting}
gap> AradHerzog(Sz(IsPermGroup, 8));
true
gap> AradHerzog(PSL(2,8));
true
gap> AradHerzog(PSL(3,5));
true
gap> AradHerzog(AtlasGroup("J1"));
true
\end{lstlisting}
Finally, we present a different function to check the conjecture by only
using character tables. 
The method is based on Lemma 2.2 of \cite{MR3003939}, which gives
a sufficient condition to check whether the conjecture is true. 

\begin{lstlisting}
gap> AradHerzog := function(t)
> local c,i,k;
> k := NrConjugacyClasses(t);
> # We only consider non-trivial conjugacy classes
> c := Filtered([1..k], x->SizesConjugacyClasses(t)[x] > 1);
> for i in IteratorOfTuples(c, 3) do
>   if ForAll(Irr(t), x->x[i[1]]*x[i[2]]=x[i[3]]*Degree(x)) then
>     return;
>   fi;
> od;
> return true;
>end;
function( t ) ... end
\end{lstlisting}
This function returns \lstinline{true} if the conjecture holds. 
However, when the function does not return \lstinline{true}, 
one cannot conclude a priori that the conjecture is false. 

We prove the conjecture for some sporadic simple groups:
\begin{lstlisting}
gap> AradHerzog(CharacterTable("J1"));
true
gap> AradHerzog(CharacterTable("Co1"));
true
gap> AradHerzog(CharacterTable("B"));
true
gap> AradHerzog(CharacterTable("M"));
true
\end{lstlisting}
\end{example}

The package \lstinline{AtlasRep} contains representative
of conjugacy classes of (some) maximal subgroups of (some)
finite sporadic simple groups. 

\begin{example}
    Let $\operatorname{Th}$ be the Thompson sporadic simple group. 
    Recall that its order is 
    \[
    |\operatorname{Th}|= 2^{15}\cdot 3^{10}\cdot 5^3\cdot 7^2\cdot 13\cdot 19\cdot 31.
    \]
    Using maximal subgroups 
    we will easily describe 
    the structure of the Sylow 5-subgroups: 
    these are semidirect products  
    of the form $C_5^2\rtimes C_5$. 
    
    To this aim, we first get the
    generators of the 10th maximal subgroup of $\operatorname{Th}$
    which has order 12000 and hence it contains
    a Sylow 5-subgroup of $\operatorname{Th}$. 
    This maximal subgroup is represented using 
    $248\times248$ matrices over $\F_2$. As
    group calculations
    involving big matrices are in general hard to perform, 
    we use 
    a permutation group representation: 
\begin{lstlisting}
gap> gens := AtlasGenerators("Th", 1, 10).generators;
[ <an immutable 248x248 matrix over GF2>,
  <an immutable 248x248 matrix over GF2> ]
gap> m10 := Image(IsomorphismPermGroup(Group(gens)));;
gap> Order(m10);
12000
gap> StructureDescription(SylowSubgroup(m10, 5));
"(C5 x C5) : C5"
\end{lstlisting}
Can you try to compute the structure of the Sylow 5-subgroups
doing the calculations directly in $\operatorname{Th}$?
\end{example}



\section{Kaplansky's unit conjecture}

This section is to discuss the existence of a counterexample
for the following well-known problem. Let $K$ be a field
and $G$ be a group. An element
$\alpha\in K[G]$ is said to be a \emph{trivial unit}
if $\alpha=\lambda g$ for some non-zero $\lambda\in K$ and
some $g\in G$. 

\index{Kaplanksy's unit}
\begin{conjecture}[Kaplansky]
\label{conj:Kaplansky}
Let $G$ be a torsion-free group and $K$ a field. Then 
all units of $K[G]$ are trivial.
\end{conjecture}

The unit problem is still open for fields of characteristic zero.
However, it was recently solved by Gardam \cite{MR4334981}
in the case of
$K$ the field of two elements. We will present Gardam's theorem
as a computer calculation.
Let $P$ be the group
generated by
\[
a=\begin{pmatrix}
1 & 0 & 0 & 1/2\\
0 & -1 & 0 & 1/2\\
0 & 0 & -1 & 0\\
0 & 0 & 0 & 1
\end{pmatrix},
\quad
b=\begin{pmatrix}
-1 & 0 & 0 & 0\\
0 & 1 & 0 & 1/2\\
0 & 0 & -1 & 1/2\\
0 & 0 & 0 & 1
\end{pmatrix}.
\]
The group $P$ appears in the literature with various names.
For us $P$ will be the Promislow group. It is known
that $P$ is torsion-free and contains 
a free-abelian normal subgroup $N$ of rank three
such that $P/N\simeq C_2\times C_2$. 

\begin{theorem}[Gardam]
\index{Gardam's theorem}
    Let $F$ be the field of two elements. For
    $x=a^2$, $y=b^2$ and $z=(ab)^2$ let
    \begin{align*}
        &p=(1+x)(1+y)(1+z^{-1}),
        &&q = x^{-1}y^{-1}+x+y^{-1}z+z,\\
        &r = 1+x+y^{-1}z+xyz,
        &&s=1+(x+x^{-1}+y+y^{-1})z^{-1}.
    \end{align*}
    Then $u=p+qa+rb+sab$ is a non-trivial unit in $F[P]$.
\end{theorem}

\begin{proof}
    We claim that the inverse of $u$
    is the element $v=p_1+q_1a+r_1b+s_1ab$, where
    \begin{align*}
        p_1=x^{-1}(a^{-1}pa),
        && q_1=-x^{-1}q,
        && r_1=-y^{-1}r,
        && s_1=z^{-1}(a^{-1}sa).
    \end{align*}
    We only need to show that $uv=vu=1$. 
    We first need to create the group $P=\langle a,b\rangle$.
\begin{lstlisting}
gap> a := [[1,0,0,1/2],[0,-1,0,1/2],[0,0,-1,0],[0,0,0,1]];;
gap> b := [[-1,0,0,0],[0,1,0,1/2],[0,0,-1,1/2],[0,0,0,1]];;
gap> P := Group([a,b]);
\end{lstlisting}
    Now we create the group algebra $F[P]$ and
    the embedding $P\hookrightarrow F[P]$.
    The field $F$ will be \lstinline{GF(2)}
    and the embedding will be denoted by \lstinline{f}.
\begin{lstlisting}
gap> R := GroupRing(GF(2),P);;
gap> f := Embedding(P, R);;
\end{lstlisting}
    We first need the elements $x$, $y$ and $z$ that were defined in the
    statement.
\begin{lstlisting}
gap> x := Image(f, a^2);;
gap> y := Image(f, b^2);;
gap> z := Image(f, (a*b)^2);;
\end{lstlisting}
    Now we define the elements $p$, $q$, $r$ and $s$. Note that
    the identity of the group algebra \lstinline{R}
    is \lstinline{One(R)}.
\begin{lstlisting}
gap> p := (One(R)+x)*(One(R)+y)*(One(R)+Inverse(z));;
gap> r := One(R)+x+Inverse(y)*z+x*y*z;;
gap> q := Inverse(x)*Inverse(y)+x+Inverse(y)*z+z;;
gap> s := One(R)+(x+Inverse(x)+y+Inverse(y))*Inverse(z);
\end{lstlisting}
    Rather than trying
    to compute the inverse of $u$ we will show that
    $uv=vu=1$. For that purpose we need to define
    $p_1$, $q_1$, $r_1$ and $s_1$.
\begin{lstlisting}
gap> p1 := Inverse(x)*p^Image(f, a);;
gap> q1 := -Inverse(x)*q;;
gap> r1 := -Inverse(y)*r;;
gap> s1 := Inverse(z)*s^Image(f, a);;
\end{lstlisting}
Now it is time to prove the theorem.
\begin{lstlisting}
gap> u := p+q*a+r*b+s*a*b;;
gap> v := p1+q1*a+r1*b+s1*a*b;;
gap> IsOne(u*v);
true
gap> IsOne(v*u);
true
\end{lstlisting}
This completes the proof of the theorem.
\end{proof}


\begin{definition}
A group $G$ has the \emph{unique product property} if 
for all finite non-empty subsets $A$ and $B$ of $G$ 
there exists $x\in G$ that can be written uniquely as
$x = ab$ with $a\in A$ and $b\in B$.
\end{definition}

Groups with the unique product property satisfy 
Kaplansky's unit conjecture. Moreover, 
left-ordered groups satisfy 
the unique product property. However, the converse
does not hold. Promislow provided the first counterexample.

\begin{example}
    The group $G=\langle a,b\mid a^{-1}b^2a=b^{-2},b^{-1}a^2b=a^{-2}\rangle$
    does not have the unique product property. 
    It is known that $G$ is torsion-free, see... 
    Let 
    \begin{multline}
    \label{eq:Promislow}
    S=\{ a^2b,
    b^2a,
    aba^{-1},
    (b^2a)^{-1},
    (ab)^{-2},
    b,
    (ab)^2x,
    (ab)^2,
    (aba)^{-1},\\
    bab,
    b^{-1},
    a,
    aba,
    a^{-1}
    \}.
    \end{multline}
    We use \textsf{GAP} and the representation $G\to\GL_4(\Q)$ given by 
    \[
a\mapsto\begin{pmatrix}
1 & 0 & 0 & 1/2\\
0 & -1 & 0 & 1/2\\
0 & 0 & -1 & 0\\
0 & 0 & 0 & 1
\end{pmatrix},
\quad
b\mapsto\begin{pmatrix}
-1 & 0 & 0 & 0\\
0 & 1 & 0 & 1/2\\
0 & 0 & -1 & 1/2\\
0 & 0 & 0 & 1
\end{pmatrix}
\]
    to check that 
    $G$ does not have
    unique product property, as each 
    \[
    s\in S^2=\{s_1s_2:s_1,s_2\in S\}
    \]
    admits at least two different decompositions of the 
    form $s=xy=uv$ for $x,y,u,v\in S$. 
    We first create the matrix representations of $a$ and $b$.
\begin{lstlisting}
gap> a := [[1,0,0,1/2],[0,-1,0,1/2],[0,0,-1,0],[0,0,0,1]];;
gap> b := [[-1,0,0,0],[0,1,0,1/2],[0,0,-1,1/2],[0,0,0,1]];;
\end{lstlisting}
    Now we create
    a function that produces the set $S$.
\begin{lstlisting}
gap> Promislow := function(x, y)
> return Set([
> x^2*y,
> y^2*x,
  x*y*Inverse(x),
  (y^2*x)^(-1),
  (x*y)^(-2),
  y,
  (x*y)^2*x,
  (x*y)^2,
  (x*y*x)^(-1),
  y*x*y,
  y^(-1),
  x,
  x*y*x,
  x^(-1)
]);
end;;
\end{lstlisting}
So the set $S$ of \eqref{eq:Promislow} 
will be \lstinline{Promislow(a,b)}. We now
create a function that checks whether
every element of a Promislow subset 
admits more than one representation.
\begin{lstlisting}
gap> is_UPP := function(S)
> local l,x,y;
> l := [];
> for x in S do
> for y in S do
> Add(l,x*y);
> od;
> od;
> if ForAll(Collected(l), x->x[2] <> 1) then
> return false;
> else
> return fail;
> fi;
> end;;
\end{lstlisting}
Finally, we check whether every element of 
$S$ admits more than one representation.
\begin{lstlisting}
gap> S := Promislow(a,b);;
gap> is_UPP(S);
false
\end{lstlisting}
This completes the proof. 
\end{example}

\section{Problems}

\begin{prob}
A Carter subgroup $C$ of a group $G$ is a nilpotent self-normalizing subgroup, that is, $C = N_G(C)$.
\begin{enumerate}
    \item Write a function that returns the Carter subgroups of a given group.
    \item If $G$ is a solvable group, then there exists a unique (non-empty) conjugacy class of Carter subgroups.
    Find the smallest non-solvable group satisfying this property.
\end{enumerate}
By the main result of \cite{MR2045411}, if $p\geq 5$ is a prime and $G$ contains a self-normalizing Sylow $p$-subgroup, then $G$ is solvable.
% Creo que casi cualquier grupo va a tener un subgrupo de Carter, y vale que siempre son todos conjugados!
\end{prob}


\begin{prob}
  Prove that every group of order $<60$ is solvable.
\end{prob}


\begin{prob}
  Prove that a group of order $455$ is cyclic.
\end{prob}

\begin{prob}
  Let $G$ be a simple group of order $168$. Compute the number of elements of
  order seven of $G$.
\end{prob}

\begin{prob}
    Use the function \lstinline{AllSmallNonabelianSimpleGroups} to 
    prove that there are no simple groups of order $2540$ and $9075$.
\end{prob}

\begin{prob}
  Find a group $G$ of order $3^6$ such that 
  $\{[x,y]:x,y\in G\}\ne[G,G]$.
\end{prob}

\begin{prob}
  Find a group $G$ of order $2^7$ such that $\{[x,y]:x,y\in G\}\ne [G,G]$.
\end{prob}

\begin{prob}
  Prove that a group of order $15$, $35$ or $77$ is cyclic.
\end{prob}

\begin{prob}
  Prove that a simple group of order $60$ is isomorphic to $\Alt_5$.
\end{prob}

\begin{prob}
  Prove that the only non-abelian simple group of order $<100$ is $\Alt_5$.
\end{prob}

\begin{prob}
  Is the following true? For any finite group $G$ the set $\{x^2:x\in G\}$ is a
  subgroup of $G$.
% false
\end{prob}



\begin{prob}
  \index{Guralnick, R.}
    \label{prob:Guralnick<201}
	Prove the following theorem of Guralnick~\cite{MR673806}. There exists
	a group $G$ of order $n\leq200$ such that $[G,G]\ne\{[x,y]:x,y\in G\}$
	if and only if
	\[
	  n\in\{96,128,144,162,168,192\}.
	\]
\end{prob}

\begin{prob}
  \index{Guralnick, R.}
    Prove the following extension of Guralnick's theorem (Problem
    \ref{prob:Guralnick<201}). There exists a group $G$ of order $n<1024$ such
    that $[G,G]\ne\{[x,y]:x,y\in G\}$ if and only if $n$ is one of the
    following numbers: 96, 128, 144, 162, 168, 192, 216, 240, 256, 270, 288,
    312, 320, 324, 336, 360, 378, 384, 400, 432, 448, 450, 456, 480, 486, 504,
    512, 528, 540, 560, 576, 594, 600, 624, 640, 648, 672, 702, 704, 720, 729,
    744, 750, 756, 768, 784, 792, 800, 810, 816, 832, 840, 864, 880, 882, 888,
    896, 900, 912, 918, 936, 960, 972, 1000, 1008.
\end{prob}


\begin{prob}
Let $G$ be a finite group.
Create functions to compute the following:
\begin{enumerate}[label=(\alph*)]
    \item The set of subnormal subgroups of $G$.
    \item The set of perfect subgroups of $G$.
    \item The set of quasisimple subgroups of $G$.
    \item The layer of $G$, that is, the subgroup generated by the subnormal quasisimple subgroups of $G$.
    \item The generalized Fitting subgroup of $G$, that is, the subgroup generated by the Fitting subgroup and the layer of $G$.
\end{enumerate}
\end{prob}

\begin{prob}
    Determine all finite groups of order $<2000$ whose composition factors are isomorphic to $C_2$, $C_3$, $C_5$ or $\Alt_5$.
\end{prob}

\begin{prob}
    Let $\pi = \{2,3,5\}$.
    Determine all $\pi$-separable groups of order $<1586$.
\end{prob}

\begin{prob}
    Prove that the smallest Brauer pair 
    corresponds to groups of order $2^8$. To speed up the calculations
    one can use the following fact: If $(G,H)$ is a Brauer pair, then
    $G/[G,G]\simeq H/[H,H]$ and $Z(G)\simeq Z(H)$. 
    See \cite[Lemma 2.6.3]{MR2680716}. 
\end{prob}

\begin{prob}
	\label{prob:groupalgebra}
	Describe the groups algebras $\C[G]$ for $G$ a
	finite group of order $28$.
\end{prob}

\begin{prob}
	Construct the irreducible representations of $\D_8$ and $\SL_2(3)$.
\end{prob}

\begin{prob}
	Construct the irreducible representations of 
	$\Alt_4$, $\Sym_4$ and $\Alt_5$.
\end{prob}

\begin{prob}
	\label{prob:McKay:M11}
	Verify McKay's conjecture for the Mathieu Groups.
\end{prob}

\begin{prob}
	\label{prob:McKay:M11}
	Verify McKay's conjecture for the 
	Conway group $\operatorname{Co}_{1}$.
\end{prob}

\begin{prob}
    Verify Isaacs--Navarro conjecture for $\operatorname{Th}$ 
    and $\operatorname{Co}_1$. 
\end{prob}

\begin{prob}
\index{Rational groups}
    A group $G$ is said to be \emph{rational} if all its characters are rational-valued.
    \begin{enumerate}
        \item Provide a \GAP~function that returns true if a given group is rational, or false otherwise.
        \item Find the rational groups of order $\leq 200$.
        \item Prove that $\Sp_6(2)$ and $\Omega^+_8(2)$ are rational simple groups.
        \item Are there rational sporadic simple groups? 
    \end{enumerate}
    By a theorem of Feit and Seitz  
    \cite{MR974014}, $\Sp_6(2)$ and $\Omega^+_8(2)$
    are the only non-abelian rational finite simple groups. 
\end{prob}

\begin{prob}
    Let $G$ be the group of Example \ref{exa:Dietzmann}. 
    \begin{enumerate}
        \item Prove that $a^3\in Z(G)$ is an element of infinite order.
        \item Prove that $Z(G)\simeq\Z\times C_2$. 
        \item Prove that $[G,G]\simeq Q_8$. 
    \end{enumerate}
\end{prob}

\begin{prob}
    Find $5$ quasisimple groups such that not every central element is a commutator.
\end{prob}

\begin{prob}
    Let $q$ be a prime power and $r > 0$.
    Let $f(x) = x^q$ be the Frobenius map of $\F_{q^r}$.
    Then $f$ is an automorphism of order $r$ which acts on the vector space $V = \F_{q^r}^n$ coordinate-wise.
    Construct the semidirect product $V\rtimes \langle f\rangle$.
\end{prob}