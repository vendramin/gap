\chapter{Basic group theory}

% TODO
% * cosets
% * double cosets

\section{Basic constructions}

A \emph{matrix group} is a subgroup of $\GL_n(R)$ for some positive integer $n$ and some ring $R$. A \emph{permutation group} is a subgroup of some $\Sym_n$. One
constructs groups with the function \lstinline{Group}. 

\begin{example}
With \lstinline{Order} we compute the order of the following groups: 
\begin{enumerate}[label=(\alph*)]
\item The
group generated by the transposition $(12)$.
\item The group generated by the
$5$-cycle $(12345)$.
\item The group generated by the permutations
$\{(12),(12345)\}$.
\end{enumerate}
Here is the code:
\begin{lstlisting}
gap> Order(Group([(1,2)]));
2
gap> Order(Group([(1,2,3,4,5)]));
5
gap> Order(Group([(1,2),(1,2,3,4,5)]));
120
\end{lstlisting}
\end{example}

\index{Group!cyclic}
For a positive integer $n$ let $C_n$ be the (multiplicative) cyclic group of order $n$. One can
construct cyclic groups with \lstinline{CyclicGroup}. With no extra arguments,
this function returns an abstract representation of a cyclic group.

\begin{example}
Let us construct the cyclic group $C_2$ of size two as an abstract group, as a
matrix group and as a permutation group:
\begin{lstlisting}
gap> CyclicGroup(2);
<pc group of size 2 with 1 generators>
gap> CyclicGroup(IsMatrixGroup, 2);
Group([ [ [ 0, 1 ], [ 1, 0 ] ] ])
gap> CyclicGroup(IsPermGroup, 2);
Group([ (1,2) ])
\end{lstlisting}
When using~\GAP~in teaching it is often desirable to 
have a friendly output. This can be achieved by 
turning on the teaching mode:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> Elements(CyclicGroup(5));
[ <identity ...>, a, a^2, a^3, a^4 ]
\end{lstlisting}
\end{example}

We can also construct finite (elementary) abelian groups. Note that
again here our groups are multiplicative groups. 

\begin{example}
We construct the multiplicative group $C_4$ using
the function to construct abelian group:
\begin{lstlisting}
gap> TeachingMode(false);
#I  Teaching mode is turned OFF
gap> AbelianGroup([4]);
<pc group of size 4 with 1 generators>
gap> Elements(last);
[ <identity> of ..., f1, f2, f1*f2 ]
\end{lstlisting}
Again we can use the teaching mode:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> AbelianGroup([4]);
<fp group of size 4 on the generators [ f1 ]>
gap> Elements(last);
[ <identity ...>, f1, f1^2, f1^3 ]
\end{lstlisting}
\end{example}

More generally, we can construct 
every finite abelian group
specifying the orders of the cyclic factors.

\begin{example}
We construct the group $C_2^2\times C_3$ and use
the teaching mode:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> A := AbelianGroup([2,2,3]);
<fp group of size 12 on the generators [ f1, f2, f3 ]>
gap> Elements(A);
[ <identity ...>, f1, f2, f3, f1*f2, f1*f3, f2*f3, f3^2,
  f1*f2*f3, f1*f3^2, f2*f3^2, f1*f2*f3^2 ]
gap> StructureDescription(A);
"C6 x C2"
\end{lstlisting}
\end{example}

\begin{example}
We now construct a big finite abelian group:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> AbelianGroup([1234,567890]);
<fp group of size 700776260 on the generators [ f1, f2 ]>
gap> StructureDescription(last);
"C350388130 x C2"
\end{lstlisting}
\end{example}

We can also construct elementary abelian groups.

\begin{example}
We construct the elementary abelian group $C_2^3$ and
verify that the square of 
every element of the group is the identity:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> ElementaryAbelianGroup(8);
<fp group of size 8 on the generators [ f1, f2, f3 ]>
gap> Elements(last);
[ <identity ...>, f1, f2, f3, f1*f2, f1*f3, f2*f3, f1*f2*f3 ]
gap> Number(last, x->IsOne(x^2));
8
\end{lstlisting}
\end{example}

\index{Group!dihedral}
For a positive integer $n$ the 
\emph{dihedral group} of order $2n$ is the group 
\[
\D_{2n}=\langle r,s \mid srs=r^{-1},\,s^2=r^n=1\rangle.
\]
To construct dihedral groups we use \lstinline{DihedralGroup}. With no extra
arguments, the function returns an abstract representation of a dihedral group. As we did before for cyclic groups, we can construct dihedral groups as
permutation groups. 

\begin{example}
Let us construct $\D_6$, compute its order 
and check that this is not an abelian group:
\begin{lstlisting}
gap> D6 := DihedralGroup(6);;
gap> Order(D6);
6
gap> IsAbelian(D6);
false
\end{lstlisting}
To display the elements of the group we use 
\lstinline{Elements}: 
\begin{lstlisting}
gap> Elements(DihedralGroup(6));
[ <identity> of ..., f1, f2, f1*f2, f2^2, f1*f2^2 ]
gap> Elements(DihedralGroup(IsPermGroup, 6));
[ (), (2,3), (1,2), (1,2,3), (1,3,2), (1,3) ]
\end{lstlisting}
When the teaching mode is active, 
we get a more familiar representation of the dihedral groups:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> D6 := DihedralGroup(6);
<fp group of size 6 on the generators [ r, s ]>
gap> Elements(D6);
[ <identity ...>, r^-1, r, s, r*s, s*r ]
gap> for x in D6 do
> Print("The element ", x, " has order ", Order(x), "\n");
> od;
The element <identity ...> has order 1
The element r has order 3
The element r^-1 has order 3
The element s has order 2
The element r*s has order 2
The element s*r has order 2
\end{lstlisting}
\end{example}

\index{Group!alternating}
\index{$\Alt_4$}
One can construct the symmetric group $\Sym_n$ with \lstinline{SymmetricGroup}.
To construct the alternating group $\Alt_n$ we use the command
\lstinline{AlternatingGroup}. 

\begin{example}
Let us construct $\Sym_4$ and $\Alt_4$ and display their elements:
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> A4 := AlternatingGroup(4);;
gap> Elements(A4);
[ (), (2,3,4), (2,4,3), (1,2)(3,4), (1,2,3), (1,2,4), 
  (1,3,2), (1,3,4), (1,3)(2,4), (1,4,2), (1,4,3), 
  (1,4)(2,3) ]
gap> Elements(S4);
[ (), (3,4), (2,3), (2,3,4), (2,4,3), (2,4), (1,2), 
  (1,2)(3,4), (1,2,3), (1,2,3,4), (1,2,4,3), (1,2,4), 
  (1,3,2), (1,3,4,2), (1,3), (1,3,4), (1,3)(2,4), 
  (1,3,2,4), (1,4,3,2), (1,4,2), (1,4,3), (1,4), 
  (1,4,2,3), (1,4)(2,3) ]
\end{lstlisting}
Now we check if some specific permutations belong to these groups:
\begin{lstlisting}
gap> (1,2,3) in A4;
true
gap> (1,2) in A4;
false
gap> (1,2,3)(4,5) in S4;
false
\end{lstlisting}
\end{example}

\begin{example}
Let us compute the order of the elements of the group 
$\Sym_5$ with the function \lstinline{Order}:
\begin{lstlisting}
gap> S5 := SymmetricGroup(5);;
gap> Collected(List(S5, Order));
[ [ 1, 1 ], [ 2, 25 ], [ 3, 20 ], [ 4, 30 ], [ 5, 24 ],
  [ 6, 20 ] ]
\end{lstlisting}
\end{example}

\begin{example}
Let us show that 
	\[
	G=\left\langle 
	\begin{pmatrix}
		0 & i\\
		i & 0
	\end{pmatrix}
	,\;
	\begin{pmatrix}
		0 & 1\\
		-1 & 0
	\end{pmatrix}\right\rangle
	\]
is a non-abelian group of order eight not isomorphic to a dihedral group.
Recall that for~\GAP, the imaginary unit $i=\sqrt{-1}$ is \lstinline{E(4)}. To
check that $G$ is not isomorphic to $\D_8$ we see that $G$ contains a unique element of order
two and that $\D_8$ has five elements of order two: 
\begin{lstlisting}
gap> a := [[0,E(4)],[E(4),0]];;
gap> b := [[0,1],[-1,0]];;
gap> G := Group([a,b]);;
gap> Order(G);
8
gap> IsAbelian(G);
false
gap> Number(G, x->Order(x)=2);
1
gap> Number(DihedralGroup(8), x->Order(x)=2);
5
\end{lstlisting} 
\end{example}

\begin{example}
The Mathieu group $M_{11}$ is a simple group of order $7920$. It can be defined as 
the subgroup of $\Sym_{11}$ generated by the permutations
\[
(123456789\,10\,11),\quad
(37\,11\,8)(4\,10\,56).
\]
Let us construct $M_{11}$ and check with \lstinline{IsSimple} that $M_{11}$ is
simple: 
\begin{lstlisting}
gap> a := (1,2,3,4,5,6,7,8,9,10,11);;
gap> b := (3,7,11,8)(4,10,5,6);;
gap> M11 := Group([a,b]);;
gap> Order(M11);
7920
gap> IsSimple(M11);
true
\end{lstlisting}
The command \lstinline{MathieuGroup} can also be used to construct Mathieu groups:
\begin{lstlisting}
gap> MathieuGroup(11);
Group([ (1,2,3,4,5,6,7,8,9,10,11), (3,7,11,8)(4,10,5,6) ])
\end{lstlisting}
\end{example}

\begin{example}
\index{Order!of elements}
The function \lstinline{Group} can also be used to construct infinite groups. 
Let us consider two matrices with finite order and such that their product has infinite order:
\begin{lstlisting}
gap> a := [[0,-1],[1,0]];;
gap> b:= [[0,1],[-1,-1]];;
gap> Order(a);
4
gap> Order(b);
3
gap> Order(a*b);
infinity
gap> Order(Group([a,b]));
infinity
\end{lstlisting}
\end{example}

\begin{remark}
	Not always \GAP~will be able to determine whether an element has finite
	order or not! 
\end{remark}

\index{Subgroups}
\index{Index}
With \lstinline{Subgroup} we construct the subgroup of a group generated by a
list of elements. The index of a subgroup can be computed with
\lstinline{Index}.

\begin{example}
Let us check that the subgroup of
$\Sym_3$
generated by $(12)$ is
$\{\id,(12)\}$ and has index three, and the subgroup of $\Sym_3$ generated by
$(123)$ is $\{\id,(123),(132)\}$ and has index two: 
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> Elements(Subgroup(S3, [(1,2)]));
[ (), (1,2) ]
gap> Index(S3, Subgroup(S3, [(1,2)]));
3
gap> Elements(Subgroup(S3, [(1,2,3)]));
[ (), (1,2,3), (1,3,2) ]
gap> Index(S3, Subgroup(S3, [(1,2,3)]));
2
\end{lstlisting}
The function \lstinline{AllSubgroups} returns the list of
subgroups of a given group:
\begin{lstlisting}
gap> AllSubgroups(S3);
[ Group(()), Group([ (2,3) ]), Group([ (1,2) ]),
  Group([ (1,3) ]), Group([ (1,2,3) ]),
  Group([ (1,2,3), (2,3) ]) ]
\end{lstlisting}
\end{example}

Recall that a subset $K$ of $G$ is said to be \emph{normal} if $K$
is invariant under conjugation by elements of $G$ , that is $gKg^{-1}\subseteq K$ for all $g\in G$. If $K$ is a normal subgroup of $G$, then $G/K$ is a group.

\begin{example}
With \lstinline{IsSubgroup} we check that $\Alt_{10}$ is a subgroup of 
$\Sym_{10}$. With \lstinline{IsNormal} we see that 
$\Alt_{10}$ is a subset of $\Sym_{10}$ invariant 
under conjugation:
\begin{lstlisting}
gap> S10 := SymmetricGroup(10);;
gap> A10 := AlternatingGroup(10);;
gap> IsSubgroup(S10,A10);
true
gap> IsNormal(S10,A10);
true
gap> Index(S10,A10);
2
\end{lstlisting}
Since $\Alt_{10}$ is a normal subgroup of $\Sym_{10}$, 
it is possible to construct the quotient group
$\Sym_{10}/\Alt_{10}$:
\begin{lstlisting}
gap> Q := S10/A10;;
gap> StructureDescription(Q);
"C2"
\end{lstlisting}
We see that $\Sym_9$ is a subgroup of $\Sym_{10}$ that
is not normal:
\begin{lstlisting}
gap> S9 := SymmetricGroup(9);;
gap> IsSubgroup(S10,S9);
true
gap> IsNormal(S10,S9);
false
\end{lstlisting}
\end{example}

\begin{example}
Let us show that in $\D_8$ there are subgroups $H$ and $K$ such that $K$ is
normal in $H$, $H$ is normal in $\D_8$ and $K$ is not normal in $\D_8$: 
\begin{lstlisting}
gap> D8 := DihedralGroup(IsPermGroup, 8);;
gap> Elements(D8);
[ (), (2,4), (1,2)(3,4), (1,2,3,4), 
  (1,3), (1,3)(2,4), (1,4,3,2), 
  (1,4)(2,3) ]
gap> K := Subgroup(D8, [(2,4)]);;
gap> Elements(K);
[ (), (2,4) ]
gap> H := Subgroup(D8, [(1,2,3,4)^2,(2,4)]);;
gap> Elements(H);
[ (), (2,4), (1,3), (1,3)(2,4) ]
gap> IsNormal(D8, K);
false
gap> IsNormal(D8, H);
true
gap> IsNormal(H, K);
true
\end{lstlisting}
\end{example}

\begin{example}
Let us compute the quotients of the cyclic group $C_4$. Since every subgroup of
$C_4$ is normal, we can use \lstinline{AllSubgroups} to check that $C_4$
contains a unique non-trivial proper subgroup $K$. The quotient $C_4/K$ has two
elements: 
\begin{lstlisting}
gap> C4 := CyclicGroup(IsPermGroup, 4);;
gap> AllSubgroups(C4);
[ Group(()), Group([ (1,3)(2,4) ]), 
  Group([ (1,2,3,4) ]) ]
gap> K := last[2];;
gap> Order(C4/K);
2
\end{lstlisting}
\end{example}

Recall that for a positive integer $n$, the \emph{generalized quaternion group} is the group 
\[
Q_{4n}=\langle x,y \mid x^{2n} = y^4 = 1, x^n = y^2, y^{-1}xy = x^{-1}\rangle.
\]
We use \lstinline{QuaternionGroup} to construct generalized quaternion groups.
As we did before, we can use the filters \lstinline{IsPermGroup} (resp.
\lstinline{IsMatrixGroup}) to obtain generalized quaternion groups as
permutation (resp. matrix) groups. 

\begin{example}
	Let us check that each subgroup of the quaternion group $Q_8$ of order
	eight is normal and that $Q_8$ is non-abelian: 
\begin{lstlisting}
gap> Q8 := QuaternionGroup(IsMatrixGroup, 8);;                                        
gap> IsAbelian(Q8);
false
gap> ForAll(AllSubgroups(Q8), x->IsNormal(Q8,x));
true
\end{lstlisting}
\end{example}

\index{Center}
If $G$ is a group, its \emph{center} is the subgroup
\[
	Z(G)=\{x\in G:xy=yx\text{ for all $y\in G$}\}.
\]

\index{Commutator} 
The \emph{commutator} of two elements $x,y\in G$ is defined as
$[x,y]=x^{-1}y^{-1}xy$. The \emph{commutator subgroup}, 
or \emph{derived subgroup} of $G$, is the subgroup $[G,G]$
generated by all the commutators of $G$.

\begin{example}
We compute the commutator of two randomly chosen element
of the dihedral group $\D_{26}$ of size 26, 
verify that the center of $\D_{26}$ is trivial and
compute $[\D_{26},\D_{26}]$ with 
the function \lstinline{DerivedSubgroup}:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> D26 := DihedralGroup(26);;
gap> x := Random(D26);
r^4*s
gap> y := Random(D26);
r^5
gap> Comm(x,y); # This computes [x,y]
r^-3
gap> IsTrivial(Center(D26));
true    
gap> der := DerivedSubgroup(D26);;
gap> StructureDescription(der);
"C13"
\end{lstlisting}
\end{example}

\begin{example}
    \index{Group!perfect}
    Recall that a group $G$ is said to be \emph{perfect}
    if $[G,G]=G$. We present some examples of perfect groups:
\begin{lstlisting}
ap> IsPerfect(AlternatingGroup(5));
true
gap> IsPerfect(SymmetricGroup(5));
false
gap> IsPerfect(SL(2,7));
true
gap> IsPerfect(GL(2,7));
false
\end{lstlisting}
\end{example}

With \lstinline{CommutatorSubgroup} we can compute
the \emph{commutator subgroup} 
\[
[H,K]=\langle [h,k]: h\in H,\,k\in K\rangle
\]
of the subgroups $H$ and $K$ of a given group $G$. 

\begin{example}
We compute the commutator subgroup of 
two randomly chosen subgroups of $\D_{16}$:
\begin{lstlisting}
gap> TeachingMode(true);
#I  Teaching mode is turned ON
gap> D16 := DihedralGroup(16);;
gap> all := AllSubgroups(D16);
[ Group([  ]), Group([ r^4 ]), Group([ s ]), Group([ r^2*s ]),
  Group([ s*r^2 ]), Group([ s*r^4 ]), Group([ r*s ]),
  Group([ s*r ]), Group([ r^3*s ]), Group([ s*r^3 ]),
  Group([ r^-2 ]), Group([ s, r^4 ]), Group([ s*r^2, r^4 ]),
  Group([ r*s, r^4 ]), Group([ s*r, r^4 ]), Group([ r^-2, s ]),
  Group([ r^-2, r*s ]), Group([ r ]), Group([ r, s ]) ]
gap> H := Random(all);
Group([ s*r^3 ])
gap> K := Random(all);
Group([ s*r^4 ])
gap> CommutatorSubgroup(H,K);
Group([ r^-2 ])
\end{lstlisting}
\end{example}

\index{Conjugacy class}
\index{Centralizer}
Recall that if $G$ is a group and $g\in G$, the \textbf{conjugacy class} of $g$
in $G$ is the subset $g^G\coloneqq\{x^{-1}gx:x\in G\}$. The \textbf{centralizer} of $g$ in
$G$ is the subgroup
\[
	C_G(g)=\{x\in G:xg=gx\}. 
\]
To compute conjugacy classes we have the following functions:
\lstinline{ConjugacyClasses} and \lstinline{ConjugacyClass}. The centralizer
can be computed with \lstinline{Centralizer}.  

\begin{example}
Let us check that $\Sym_3$ contains three conjugacy classes with
representatives $\id$, $(12)$ and $(123)$, so that 
\[(12)^{\Sym_3}=\{(12),(13),(23)\}, \qquad (123)^{\Sym_3}=\{(123),(132)\}.\]
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> ConjugacyClasses(S3);
[ ()^G, (1,2)^G, (1,2,3)^G ]
gap> Elements(ConjugacyClass(S3, (1,2)));
[ (2,3), (1,2), (1,3) ]
gap> Elements(ConjugacyClass(S3, (1,2,3)));
[ (1,2,3), (1,3,2) ]
\end{lstlisting}
Let us check that $C_{\Sym_3}\left( (123) \right)=\{\id,(123),(132)\}$: 
\begin{lstlisting}
gap> Elements(Centralizer(S3, (1,2,3)));
[ (), (1,2,3), (1,3,2) ]
\end{lstlisting}
\end{example}

\begin{example}
\label{exa:Representative}
In this example we use the function \lstinline{Representative} to construct a
list of representatives of conjugacy classes of $\Alt_4$:
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> List(ConjugacyClasses(A4), Representative);
[ (), (1,2)(3,4), (1,2,3), (1,2,4) ]
\end{lstlisting}
\end{example}

With the function \lstinline{IsConjugate} we can check whether two elements are
conjugate.  If two elements $g$ and $h$ are conjugate, we want to find an
element $x$ such that $g=x^{-1}hx$. For that purpose we use
\lstinline{RepresentativeAction}.

\begin{example}
\label{exa:RepresentativeAction}
Let us check that $(123)$ and $(132)=(123)^2$ are not conjugate in $\Alt_4$: 
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> g := (1,2,3);;
gap> IsConjugate(A4, g, g^2);
false
\end{lstlisting}
Note that $(123)$ and $(132)$ are conjugate both 
in $\Sym_4$ and $\Alt_5$:
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> A5 := AlternatingGroup(5);;
gap> IsConjugate(S4, g, g^2);
true
gap> IsConjugate(A5, g, g^2);
true
\end{lstlisting}
Now we find elements $x\in\Sym_4$ and $y\in\Alt_5$ such that 
$(123)^x=x^{-1}(123)x=(132)$ and
$(123)^y=y^{-1}(123)y=(132)$. Note that conjugation could be written
using the exponential notation. For that purpose we
use \lstinline{RepresentativeAction}:
\begin{lstlisting}
gap> x := RepresentativeAction(S4, g, g^2);
(2,3)
gap> x^(-1)*g*x=g^2;
true
gap> g^x=g^2;
true
gap> y := RepresentativeAction(A5, g, g^2);
(2,3)(4,5)
gap> g^y=g^2;
true
\end{lstlisting}
Note that there are several possible group elements $x\in\Sym_4$ 
such that $(123)^x=(132)$. The 
function \lstinline{RepresentativeAction} returns \emph{one}
of such possible $x$. 
\end{example}

\begin{example}
It is well-known that the converse of Lagrange theorem does not hold.  The
following example is based on~\cite{MR1573427}.  
Let us show that $\Alt_4$ has no subgroups of order six. 
A naive idea to prove that $\Alt_4$ has no subgroups of order six is to study
all the $\binom{12}{6}=924$ subsets of $\Alt_4$ of size six and check that none
of these subsets is a group: 
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> k := 0;;
gap> for x in Combinations(Elements(A4), 6) do
> if Size(Subgroup(A4, x))=Size(x) then
> k := k+1;
> fi;
> od;
gap> k;
0
\end{lstlisting}
This is an equivalent way of performing such a computation:
\begin{lstlisting}
gap> ForAny(Combinations(Elements(A4), 6),\
> x->Size(Subgroup(A4, x))=Size(x));
false
\end{lstlisting}

Now we use a similar idea. We use that every subgroup of order six contains
exactly five elements besides the unit 1.  So we see that none of the $\binom{11}{5}=462$
subsets of $\Alt_4$ with five elements generates a subgroup of order six.  In
the following code we do not use \lstinline{Combinations}. Combinations will be
generated by using an iterator.
\begin{lstlisting}
gap> k := 0;;
gap> for t in IteratorOfCombinations(\
> Filtered(A4, x->not x = ()), 5) do
> if Size(Subgroup(A4, t))=Size(t)+1 then
> k := k+1;
> fi;
> od;
gap> k;
0
\end{lstlisting}

Here we have another idea: if $\Alt_4$ has a subgroup of order six, then the
index of this subgroup in $\Alt_4$ is two. To use the
function 
\lstinline{SubgroupsOfIndexTwo}
we need to load the package
\lstinline{LAGUNA}:
\begin{lstlisting}
gap> LoadPackage("LAGUNA");;
\end{lstlisting}
The package was written by  
V. Bovdi,
   O. Konovalov,
   R. Rossmanith and
   C. Schneider. 
We now check that $\Alt_4$ has no subgroups of
index two:
\begin{lstlisting}
gap> SubgroupsOfIndexTwo(A4);
[  ]
\end{lstlisting}

Of course we can construct all subgroups and check that there are no subgroups
of order six: 
\begin{lstlisting}
gap> List(AllSubgroups(A4), Order);
[ 1, 2, 2, 2, 3, 3, 3, 3, 4, 12 ]
gap> 6 in last;
false
\end{lstlisting}
Moreover, it is enough to construct all conjugacy classes of subgroups:
\begin{lstlisting}
gap> c := ConjugacyClassesSubgroups(A4);;
gap> List(c, x->Order(Representative(x)));
[ 1, 2, 3, 4, 12 ]
gap> 6 in last;
false
\end{lstlisting}

Another approach is to use conjugacy classes of elements in $\Alt_4$. Indeed, the conjugacy classes of $\Alt_4$ are:
\begin{align*}
	&\{\id\},
	&&\{(243), (123), (134), (142)\},\\
	&\{(12)(34),(13)(24),(14)(23)\},
	&&\{(234),(124), (132), (143)\}.
\end{align*}
This is how we construct the conjugacy classes of $\Alt_4$:
\begin{lstlisting}
gap> ConjugacyClasses(A4);
[ ()^G, (1,2)(3,4)^G, (1,2,3)^G, (1,2,4)^G ]
gap> Elements(ConjugacyClass(A4, ()));
[ () ]
gap> Elements(ConjugacyClass(A4, (1,2)(3,4)));
[ (1,2)(3,4), (1,3)(2,4), (1,4)(2,3) ]
gap> Elements(ConjugacyClass(A4, (1,2,3)));
[ (2,4,3), (1,2,3), (1,3,4), (1,4,2) ]
gap> Elements(ConjugacyClass(A4, (1,2,4)));
[ (2,3,4), (1,2,4), (1,3,2), (1,4,3) ]
\end{lstlisting}
Assume that $\Alt_4$ has a subgroup $K$ of order six. Then $K$ has index two in
$\Alt_4$ and hence it is normal in $\Alt_4$.  This means that $K$ is a union of
conjugacy classes of $\Alt_4$ and that $\{1\}\subseteq K$. This is a
contradiction!

Let us now use the commutator to prove that $\Alt_4$ has no subgroups of order
six.  If there exists a subgroup $K$ of order six, then $K$ is normal in
$\Alt_4$ and the quotient $\Alt_4/K$ is cyclic of order two.  This implies that 
\[
[\Alt_4,\Alt_4]=\{\id,(12)(34),(13)(24),(14)(23)\},
\]
is contained in $K$, a contradiction since $4$ does not divide $6$: 
\begin{lstlisting}
gap> DerivedSubgroup(A4);
Group([ (1,4)(2,3), (1,3)(2,4) ])
gap> Elements(last);
[ (), (1,2)(3,4), (1,3)(2,4), (1,4)(2,3) ]
\end{lstlisting}

One more variation. If $K$ is a subgroup of $\Alt_4$ of order six, then there
are two possibilities: either $K\simeq\Sym_3$ or $K\simeq C_6$. The
group $\Alt_4$ has no elements of order six:
\begin{lstlisting}
gap> Filtered(A4, x->Order(x)=6);
[  ]
\end{lstlisting}
Then $K\simeq\Sym_3$ and hence $K$ contains three elements of order two.
Thus \[
	\{\id,(12)(34),(13)(24),(14)(23)\}
\]
is a subgroup of $K$ of order four, which contradicts Lagrange's theorem.  
\end{example}

To study isomorphisms between finite group one uses
\lstinline{IsomorphismGroups}. This function returns \lstinline{fail} if the
groups are not isomorphic, or some isomorphism otherwise. 

\begin{example}
Let us construct a group $G$ such that $G=\langle a\rangle\times\langle
b\rangle$ with $C_4\simeq\langle a\rangle$ and $C_2\simeq\langle b\rangle$. We
also prove that 
\begin{align*}
\langle a^2\rangle\simeq\langle b\rangle,
&&
G/\langle a^2\rangle\not\simeq G/\langle b\rangle.
\end{align*}
Here is the code:
\begin{lstlisting}
gap> G := AbelianGroup(IsPermGroup, [4,2]);
Group([ (1,2,3,4), (5,6) ])
gap> K := Subgroup(G, [(5,6)]);;
gap> L := Subgroup(G, [(1,2,3,4)^2]);;
gap> IsomorphismGroups(K, L);
[ (5,6) ] -> [ (1,3)(2,4) ]
gap> IsomorphismGroups(G/K,G/L);
fail
\end{lstlisting}
%FIXME
One can show that:
\begin{align*}
\langle a^2\rangle\simeq\langle b\rangle\simeq C_2,
&&
G/\langle a^2\rangle\simeq C_4,
&&
G/\langle b\rangle\simeq C_2\times C_2.
\end{align*}
\end{example}

We can also work with classical groups. Use 
\begin{lstlisting}
gap> ?classical groups
\end{lstlisting}
to get more information.

\begin{example}
One can use the function \lstinline{GL} to construct some general linear groups such as
$\GL_n(\Z)$, $\GL_n(\Z/m)$ and $\GL_n(\F_q)$:
\begin{lstlisting}
gap> Order(GL(2,Integers));
infinity
gap> Order(GL(2,ZmodnZ(4)));
96
gap> Order(GL(2,GF(4)));
180
gap> Order(GL(3,GF(4)));
181440
\end{lstlisting}
Similarly, with \lstinline{SL} one constructs 
$\SL_n(\Z)$, $\SL_n(\Z/m)$ and 
$\SL_n(\F_q)$:
\begin{lstlisting}
gap> Order(SL(2,GF(3)));
24
gap> Order(SL(2,Integers));
infinity
gap> Order(SL(2,ZmodnZ(4)));
48
gap> Order(SL(2,GF(4)));
60
\end{lstlisting}

We write $\GL_n(q)$ to denote $\GL_n(\F_q)$. We use
the same notation for other classical groups. Following 
this convenient notation for constructing 
linear groups over finite fields, 
we can specify the size of the field:
\begin{lstlisting}
gap> G := GL(3,4);;
gap> Size(G);
181440
gap> Center(G);
<group of 3x3 matrices over GF(2^2)>
gap> StructureDescription(last);
"C3"
gap> IsSubgroup(G, SL(3,4));
true
gap> DerivedSubgroup(G) = SL(3,4);
true
\end{lstlisting}
We now compute 
the quotient of $\GL_3(4)$ by its center. Note that~\GAP~will return
a permutation representation of this quotient. Also here 
\lstinline{StructureDescription} shows something that might be
different to what we have in mind:
\begin{lstlisting}
gap> Q := GL(3,4)/Center(GL(3,4));;
gap> Order(Q);
60480
gap> StructureDescription(Q);
"PSL(3,4) : C3"
gap> not IsomorphismGroups(Q, PGL(3,4)) = fail;
true
\end{lstlisting}
Since the function \lstinline{IsomorphismGroups} does not 
return \lstinline{fail}, it follows that
our groups are indeed isomorphic. 
\end{example}

It is known that the commutator of a finite group is not always equal to the
set of commutators.  The following example is based on~\cite{MR0075938}. 

\begin{example}
\label{ex:commutatorElementsS16}
Let $G$ be the
subgroup of $\Sym_{16}$ generated by the permutations
\begin{align*}
&a = (13)(24),&&
b = (57)(6,8),\\
&c = (911)(10,12),&&
d = (13,15)(14,16),\\
&e = (13)(5,7)(9,11),&&
f = (12)(3,4)(13,15),\\
&g = (56)(7,8)(13,14)(15,16),&&
h = (9\;10)(11\;12).
\end{align*}
We show that $[G,G]$ has order $16$: 
\begin{lstlisting}
gap> a := (1,3)(2,4);;
gap> b := (5,7)(6,8);;
gap> c := (9,11)(10,12);;
gap> d := (13,15)(14,16);;
gap> e := (1,3)(5,7)(9,11);;
gap> f := (1,2)(3,4)(13,15);;
gap> g := (5,6)(7,8)(13,14)(15,16);;
gap> h := (9,10)(11,12);;
gap> G := Group([a,b,c,d,e,f,g,h]);;
gap> D := DerivedSubgroup(G);;
gap> Size(D);
16
\end{lstlisting}
We now show that the set of commutators 
has $15$ elements. In particular, we show that 
$cd\in[G,G]$ and that $cd$ is not a commutator: 
\begin{lstlisting}
gap> Size(Set(List(Cartesian(G,G), Comm)));
15
gap> c*d in Difference(D,\
> Set(List(Cartesian(G,G), Comm)));
true
\end{lstlisting}
\end{example}

%%\begin{example}
%%Sabemos que $\Alt_4$ es un grupo de orden doce. Los $3$-subgrupos de Sylow de
%%$\Alt_4$ son isomorfos a $C_3$ y hay cuatro: son los subgrupos $\langle
%%(243)\rangle$, $\langle (123)\rangle$, $\langle (142)\rangle$ y $\langle
%%(134)\rangle$. 
%%
%%\begin{lstlisting}
%%gap> A4 := AlternatingGroup(4);;                                      
%%gap> P := SylowSubgroup(A4, 3);;
%%gap> StructureDescription(P);
%%"C3"
%%gap> ConjugacyClassSubgroups(A4, P);
%%Group( [ (1,2,3) ] )^G
%%gap> Size(last);
%%4
%%gap> Elements(ConjugacyClassSubgroups(A4, P));
%%[ Group([ (2,4,3) ]), Group([ (1,2,3) ]), 
%%  Group([ (1,4,2) ]), Group([ (1,3,4) ]) ]
%%gap> Index(A4, Normalizer(A4, P));
%%4	
%%\end{lstlisting}
%%\end{example}

To conclude this section we present an example
that shows how to work with Sylow subgroups. 

\begin{example}
\label{exa:Sylow}
The group $G=\SL_2(3)$ has order 24. We compute
its Sylow subgroups. Let $P\in\Syl_2(G)$ and
$Q\in\Syl_3(G)$. Then $PQ=G$: 
\begin{lstlisting}
gap> G := SL(2,3);;
gap> P := SylowSubgroup(G, 2);;
gap> Q := SylowSubgroup(G, 3);;
gap> PQ := List(Cartesian(P,Q), x->x[1]*x[2]);;
gap> Size(PQ);
24
\end{lstlisting}
We now compute the Sylow 3-subgroups of $G$:
\begin{lstlisting}
gap> Index(G, Normalizer(G, Q));
4
gap> sylows3 := ConjugacyClassSubgroups(G, Q);;
gap> Size(sylows3);
4
gap> for x in sylows3 do
> Display(x);
> od;
Group([ [ [ Z(3)^0, Z(3)^0 ], [ 0*Z(3), Z(3)^0 ] ] ])
Group([ [ [ Z(3), Z(3)^0 ], [ Z(3), 0*Z(3) ] ] ])
Group([ [ [ 0*Z(3), Z(3)^0 ], [ Z(3), Z(3) ] ] ])
Group([ [ [ Z(3)^0, 0*Z(3) ], [ Z(3), Z(3)^0 ] ] ])
\end{lstlisting}
\end{example}

\section{Group actions}

In this section we present several examples of group actions. In different
well-known situations we compute orbits, 
stabilizers, cores and permutation representations. 

\begin{example}
We now how actions on right cosets by right multiplication. In 
this concrete example, we explore 
the case of $\Sym_3$ 
acting by right multiplication
on the set of right cosets of $\Alt_3$ in $\Sym_3$:
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> A3 := AlternatingGroup(3);;
gap> omega := RightCosets(S3,A3);
[ RightCoset(Alt( [ 1 .. 3 ] ),()),
  RightCoset(Alt( [ 1 .. 3 ] ),(2,3)) ]
gap> for x in omega do
> Display(Elements(x));
> od;
[ (), (1,2,3), (1,3,2) ]
[ (2,3), (1,2), (1,3) ]  
gap> Size(Orbits(S3, omega, OnRight));
1
gap> Stabilizer(S3, Random(omega), OnRight);
Group([ (1,2,3) ])
\end{lstlisting}
\end{example}

\begin{example}
We now study the natural action 
of the dihedral group $\D_8$ of order eight 
on the vertices $\{1,2,3,4\}$ of the square:
%, see figure \ref{fig:square}:
\begin{lstlisting}
gap> D8 := DihedralGroup(IsPermGroup, 8);;
gap> GeneratorsOfGroup(D8);
[ (1,2,3,4), (2,4) ]
gap> # Here r=(1,2,3,4) and s=(2,4)
gap> Orbits(D8, [1..4]);
[ [ 1, 2, 3, 4 ] ]
gap> IsTransitive(D8, [1..4]);
true
gap> Stabilizer(D8, 1);
Group([ (2,4) ])
\end{lstlisting}
We now compute the \emph{core} of the action, i.e. 
the set of elements of $\D_8$ acting trivially on $\{1,\dots,4\}$: 
\begin{lstlisting}
gap> Filtered(D8, g->ForAll([1..4], x->x^g=x));
[ () ]
\end{lstlisting}
\end{example}

\begin{example}
    Let $G=\GL_2(5)$ and $V=\F_5^2$. We study 
    the natural action (by right multiplication) 
    of $G$ on $V$:
\begin{lstlisting}
gap> G := GL(2,5);;
gap> omega := AsList(GF(5)^2);;
gap> Size(Orbits(G,omega));
2
gap> v := Random(omega);
[ Z(5)^3, Z(5)^2 ]
gap> Orbit(G,v);
[ [ Z(5)^3, Z(5)^2 ], [ Z(5)^0, Z(5)^2 ],
  [ Z(5)^3, Z(5)^3 ], [ Z(5), Z(5)^2 ],
  [ 0*Z(5), Z(5)^0 ], [ Z(5)^0, Z(5)^3 ],
  [ Z(5)^2, Z(5)^3 ], [ Z(5)^2, Z(5)^2 ],
  [ Z(5)^2, Z(5) ], [ Z(5)^2, 0*Z(5) ],
  [ Z(5), Z(5)^3 ], [ Z(5)^0, Z(5)^0 ],
  [ Z(5)^3, Z(5) ], [ Z(5)^3, 0*Z(5) ],
  [ 0*Z(5), Z(5) ], [ Z(5), Z(5)^0 ],
  [ Z(5)^3, Z(5)^0 ], [ Z(5)^0, Z(5) ],
  [ 0*Z(5), Z(5)^3 ], [ Z(5)^0, 0*Z(5) ],
  [ Z(5)^2, Z(5)^0 ], [ Z(5), Z(5) ],
  [ Z(5), 0*Z(5) ], [ 0*Z(5), Z(5)^2 ] ]
gap> Stabilizer(G,v);
Group([ [ [ Z(5)^0, Z(5) ], [ 0*Z(5), Z(5) ] ],
  [ [ Z(5)^2, Z(5)^0 ], [ Z(5)^2, Z(5)^2 ] ] ])
gap> StructureDescription(last);
"C5 : C4"  
\end{lstlisting}
We now construct by-hand a permutation representation
corresponding to this action (i.e. the image of the
group homomorphism induced by the action):
\begin{lstlisting}
gap> gens := GeneratorsOfGroup(G);
[ [ [ Z(5), 0*Z(5) ], [ 0*Z(5), Z(5)^0 ] ],
  [ [ Z(5)^2, Z(5)^0 ], [ Z(5)^2, 0*Z(5) ] ] ]
gap> a := gens[1];;
gap> Display(a);
 2 .
 . 1
gap> b := gens[2];;
gap> Display(b);
 4 1
 4 .
gap> alpha := PermList(List([1..Size(omega)], \ 
> x->Position(omega, omega[x]*a)));;
gap> alpha;
(6,11,16,21)(7,12,17,22)(8,13,18,23)(9,14,19,24)(10,15,20,25)
gap> beta := PermList(List([1..Size(omega)], \ 
> x->Position(omega, omega[x]*b)));;
gap> beta;
(2,16,9)(3,21,15)(4,6,17)(5,11,23)(7,22,10)(8,12,13)
(14,18,19)(20,24,25)
gap> StructureDescription(Group([alpha,beta]));
"GL(2,5)"
\end{lstlisting}
Alternatively, we can use the function \lstinline{Action}:
\begin{lstlisting}
gap> Action(G, GF(5)^2, OnPoints);
Group([ (6,11,16,21)(7,12,17,22)(8,13,18,23)
  (9,14,19,24)(10,15,20,25), (2,16,9)(3,21,15)
  (4,6,17)(5,11,23)(7,22,10)(8,12,13)(14,18,19)(20,24,25) ])
\end{lstlisting}
\end{example}

\begin{example}
We now consider the action of $G=\GL_2(5)$ on 
one-dimensional subspaces of $V=\F_5^2$. We prove
that of the action is $Z(G)$ and that
$G/Z(G)\simeq\Sym_5$: 
\begin{lstlisting}
gap> G := GL(2,5);;
gap> V := GF(5)^2;;
gap> Size(Subspaces(V,1));
6
gap> core := Filtered(G, \ 
> g->ForAll(Subspaces(V,1), x->x^g=x));
[ [ [ Z(5)^0, 0*Z(5) ], [ 0*Z(5), Z(5)^0 ] ],
  [ [ Z(5)^3, 0*Z(5) ], [ 0*Z(5), Z(5)^3 ] ],
  [ [ Z(5), 0*Z(5) ], [ 0*Z(5), Z(5) ] ],
  [ [ Z(5)^2, 0*Z(5) ], [ 0*Z(5), Z(5)^2 ] ] ]
gap> center := Center(G);;
gap> Group(core)=center;
true
gap> StructureDescription(G/center);
"S5"
gap> Size(Orbits(G, Subspaces(V,1)));
1
gap> Transitivity(G, Subspaces(V,1));
3
gap> W := Random(Subspaces(V,1));;
gap> Random(W);
[ Z(5)^3, Z(5) ]
gap> StructureDescription(Stabilizer(G, W));
"C4 x (C5 : C4)"
\end{lstlisting}
We now construct the permutation representation of this action: 
\begin{lstlisting}
gap> P := Action(G, Subspaces(V,1), OnPoints);
Group([ (3,6,5,4), (1,2,5)(3,4,6) ])
gap> StructureDescription(P);
"S5"
gap> NrMovedPoints(P);
6
gap> Transitivity(P, [1..6]);
3
gap> StructureDescription(Stabilizer(P, 1));
"C5 : C4"
\end{lstlisting}
\end{example}

\begin{example}
    Let $G=\GL_3(3)$ act on the set 
    $\Omega=\{x\in G:|x|=2\}$ by conjugation. We check
    that the action has a unique fixed point: 
\begin{lstlisting}
gap> G := GL(3,3);;
gap> omega := Filtered(G, x->Order(x)=2);;
gap> fix := Intersection(omega, Center(G));;
gap> Size(fix);
1
gap> Display(fix[1]);
 2 . .
 . 2 .
 . . 2
gap> # There is an alternative way to compute fixed points
gap> fix = FixedPoints(omega, G, OnPoints);
true
gap> List(Orbits(G, omega, OnPoints), Size);
[ 117, 117, 1 ]
gap> # Orbit representatives 
gap> for x in Orbits(G, omega, OnPoints) do
> Display(x[1]);
> Print("--\n");
> od;
 1 1 .
 . 2 .
 . . 1
--
 . . 1
 . 2 .
 1 . .
--
 2 . .
 . 2 .
 . . 2
--
gap> x := Random(omega);;
gap> StructureDescription(Stabilizer(G,x));
"C2 x GL(2,3)"
\end{lstlisting}
We now construct a permutation representation of the 
action. Note that we now have two orbits and not 
three, as the permutation representation overlooks 
fixed points. Here is the code:
\begin{lstlisting}
gap> P := Action(G, omega, OnPoints);;
gap> StructureDescription(P);
"PSL(3,3)"
gap> NrMovedPoints(P);
234
gap> List(Orbits(P), Size);
[ 117, 117 ]
gap> y := Random([1..NrMovedPoints(P)]);;
gap> StructureDescription(Stabilizer(P, y));
"GL(2,3)"
\end{lstlisting}
\end{example}

\begin{example}
Let $G=\SL_2(8)$. We construct the projective 
action of $G$ on the set $\F_8\cup\{\infty\}$ of size nine. 
We use \lstinline{OnLines}, which is the function of the 
action of $G$ on the set $\Omega$ of vectors $(x,y)\in\F_8^2\setminus\{(0,0)\}$ 
with the first non-zero coordinate equal to one:
\begin{lstlisting}
gap> G := SL(2,8);;
gap> omega := Concatenation(List(GF(8), \ 
> x->[1,x]*One(GF(8))), [w]);;
gap> Size(omega);
9
gap> g := Random(G);;
gap> Display(g);
z = Z(8)
 z^3 z^4
 z^1 z^1
gap> v := [1,0]*One(GF(8));
[ Z(2)^0, 0*Z(2) ]
gap> OnLines(v,g);
[ Z(2)^0, Z(2^3) ]
gap> w := [0,1]*One(GF(8));
[ 0*Z(2), Z(2)^0 ]
gap> OnLines(w, g);
[ Z(2)^0, Z(2)^0 ]
\end{lstlisting}
Note that \lstinline{OnLines} and \lstinline{OnRight}
are different actions:
\begin{lstlisting}
gap> OnLines(w, g) in omega;
true
gap> OnRight(w, g) in omega;
false
\end{lstlisting}
We now perform some calculations:
\begin{lstlisting}
gap> Size(Orbits(G, omega, OnLines));
1
gap> v := Random(omega);
[ Z(2)^0, Z(2^3)^4 ]
gap> StructureDescription(Stabilizer(G, v, OnLines));
"(C2 x C2 x C2) : C7"
gap> Transitivity(G, omega, OnLines);
3
gap> StructureDescription(Action(G, omega, OnLines));
"PSL(2,8)"
\end{lstlisting}
We will construct an explicit permutation representation as a subgroup
of $\Sym_9$. Let $\Sigma=\F_8\cup\{\infty\}$ and write $\F_8^{\times}=\langle\zeta\rangle$. Let  
$Q$ be the group generated by 
the permutations of $\Sigma$ given by
\[
\alpha(x)=x+1,\quad
\beta(x)=\zeta x,\quad 
\gamma(x)=1/x. 
\]
By convention, $\alpha(\infty)=\beta(\infty)=\infty$, 
$\gamma(0)=\infty$ and $\gamma(\infty)=0$.
Let us construct these permutations:
\begin{lstlisting}
gap> alpha := function(x)
> if x = infinity then
>   return infinity;
> else
>   return x+One(GF(8));
> fi;
> end;
function( x ) ... end
gap> beta := function(x)
> if x = infinity then
>   return infinity;
> else
>   return x*Z(8);
> fi;
> end;
function( x ) ... end
gap> gamma := function(x)
> if IsZero(x) then
>   return infinity;
> elif x = infinity then
>   return Zero(GF(8));
> else
>   return 1/x;
> fi;
> end;
function( x ) ... end
gap> sigma := Concatenation(AsList(GF(8)), [infinity]);;
gap> Size(sigma);
9
gap> a := PermList(List(sigma, x->Position(sigma, alpha(x))));
(1,2)(3,5)(4,8)(6,7)
gap> b := PermList(List(sigma, x->Position(sigma, beta(x))));
(2,3,4,5,6,7,8)
gap> c := PermList(List(sigma, x->Position(sigma, gamma(x))));
(1,9)(3,8)(4,7)(5,6)
\end{lstlisting}
After making the identifications  
\[
\alpha\equiv (12)(35)(48)(67),\quad 
\beta\equiv (2345678),\quad
\gamma\equiv (19)(38)(47)(56),
\]
we conclude that $\langle\alpha,\beta,\gamma\rangle\simeq\PSL_2(8)$:
\begin{lstlisting}
gap> Q := Group([a,b,c]);;
gap> StructureDescription(Q);
"PSL(2,8)"
\end{lstlisting}

\end{example}

One can use the function \lstinline{ActionHomomorphism} to construct
the natural homomorphism induced by an action:
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> f := ActionHomomorphism(S3, [1..3], OnPoints);;
gap> Image(f) = Action(S3, [1..3], OnPoints);
true
\end{lstlisting}
With this function we will be able to construct
all sort of actions. We will explain this in the following section.  

\section{Homomorphisms}

\index{Group!homomorphism}
Now we work with group homomorphisms. There are several ways to construct group homomorphisms. The function
\lstinline{GroupHomomorphismByImages} returns the group homomorphism
constructed from a list of generators of the domain and the value of the image
at each generator. Properties of group homomorphisms can be studied with
\lstinline{Image}, \lstinline{IsInjective}, \lstinline{IsSurjective},
\lstinline{Kernel}, \lstinline{PreImage}, \lstinline{PreImages}, etc. 

\begin{example}
The map $\Sym_4\to\Sym_3$
that maps each transposition of $\Sym_4$ into $(12)$ extends to a group homomorphism $f$. 
This homomorphism $f$ is not injective ($\ker f$ has twelve elements) and it is
not surjective (for example $(123)\not\in f(\Sym_4)$):
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> S3 := SymmetricGroup(3);;
gap> f := GroupHomomorphismByImages(S4, S3,\
> [(1,2),(1,3),(1,4),(2,3),(2,4),(3,4)],\
> [(1,2),(1,2),(1,2),(1,2),(1,2),(1,2)]);
[ (1,2), (1,3), (1,4), (2,3), (2,4), (3,4) ] -> 
[ (1,2), (1,2), (1,2), (1,2), (1,2), (1,2) ]
gap> Size(Kernel(f));
12
gap> IsInjective(f);
false
gap> Size(Image(f));
2
gap> IsSurjective(f);
false
gap> (1,2,3) in Image(f);
false
gap> (1,3)^f;
(1,2)
gap> (1,2,3)^f;
()
gap> (1,2,3,4)^f;
(1,2)
\end{lstlisting}
\end{example}

If $K$ is a normal subgroup of $G$, the canonical map $G\to G/K$ can be
constructed with the function \lstinline{NaturalHomomorphismByNormalSubgroup}.

\begin{example}
Let us construct the cyclic group
$C_{12}$ with generator $g$ 
as a group of permutations, 
the subgroup $K=\langle g^6\rangle$ and the quotient  $C_{12}/K$. 
We also construct the canonical (surjective) map $C_{12}\to C_{12}/K$: 
\begin{lstlisting}
gap> g := (1,2,3,4,5,6,7,8,9,10,11,12);;
gap> C12 := Group(g);;
gap> K := Subgroup(C12, [g^6]);;
gap> f := NaturalHomomorphismByNormalSubgroup(C12, K);
[ (1,2,3,4,5,6,7,8,9,10,11,12) ] -> [ f1 ]
gap> Image(f, g^6);
<identity> of ...
gap> (g^6)^f = Image(f, g^6);
true
\end{lstlisting}
%FIXME
%Podemos verificar fácilmente que los subgrupos de $C_{12}$ que contienen a $K$
%están en correspondencia biyectiva con los subgrupos de $C_{12}/K$: 
%\begin{lstlisting}
%gap> for subgroup in AllSubgroups(C12) do
%> if IsSubgroup(subgroup, K) then
%> Print(Image(f, subgroup), "\n");
%> fi;
%> od;
%Group( [ <identity> of ... ] )
%Group( [ f1*f2^2 ] )
%Group( [ f2^2 ] )
%Group( [ f1 ] )
%gap> AllSubgroups(Image(f));
%[ Group([ <identity> of ... ]), 
%  Group([ f1*f2^2 ]), Group([ f2 ]), 
%  Group([ f1, f2 ]) ]
%\end{lstlisting}
\end{example}

\begin{example}
    With \lstinline{GQuotients} 
    we can determine when 
    a given group is an epimorphic image of another one. 
    For example, $\Sym_3$ is an epimorphic image of $\Sym_4$, but 
    $C_2\times C_2$ is not:
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> GQuotients(S4, SymmetricGroup(3));
[ [ (2,4), (1,2,3) ] -> [ (2,3), (1,2,3) ] ]
gap> GQuotients(S4, AbelianGroup([2,2]));
[  ]
\end{lstlisting}
\end{example}

\begin{example}
    The group $\Sym_3$ is an epimorphic image of $\Sym_4$. In fact, 
    if 
    \[
    K=\{\id,(12)(34),(13)(24),(14)(23)\}, 
    \]
    then 
    $K$ is a normal subgroup of $\Sym_3$ and
    $\Sym_4/K\simeq\Sym_3$. Let us construct
    the canonical map $\Sym_4\to\Sym_4/K$:
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> K := Subgroup(S4, [(1,2)(3,4),(1,3)(2,4)]);;
gap> p := NaturalHomomorphismByNormalSubgroup(S4, K);;
gap> Elements(Kernel(p));
[ (), (1,2)(3,4), (1,3)(2,4), (1,4)(2,3) ]
gap> IsSurjective(p);
true
\end{lstlisting}
\end{example}

\index{Automorphism group}
\index{Inner automorphisms}
The function \lstinline{AutomorphismGroup} computes the automorphism group of a
finite group. If $G$ is a group, the automorphisms of $G$ of the form $x\mapsto
g^{-1}xg$, where $g\in G$, are the \textbf{inner automorphisms} of $G$.  The
function \lstinline{IsInnerAutomorphism} checks whether a given automorphism is
inner. 

\begin{example}
Let us check that $\Aut(\Sym_3)$ is a non-abelian group of six elements:
\begin{lstlisting}
gap> aut := AutomorphismGroup(SymmetricGroup(3));
<group of size 6 with 2 generators>
gap> IsAbelian(aut);
false
\end{lstlisting}
\end{example}

\begin{example}
Let us prove that for $n\in\{2,3,4,5\}$ each automorphism of $\Sym_n$ is inner. Here is the code:
\begin{lstlisting}
gap> for n in [2..5] do
> G := SymmetricGroup(n);;
> if ForAll(AutomorphismGroup(G),\
> x->IsInnerAutomorphism(x)) then
> Print("Each automorphism of S",\
> n, " is inner.\n");
> fi;
> od;
Each automorphism of S2 is inner.
Each automorphism of S3 is inner.
Each automorphism of S4 is inner.
Each automorphism of S5 is inner.
\end{lstlisting}
It is known that in $\Sym_6$ there are non-inner automorphisms:
% FIXME!
\begin{lstlisting}
gap> S6 := SymmetricGroup(6);;
gap> f := First(AutomorphismGroup(S6),\
> x->not IsInnerAutomorphism(x));
[ (1,2,3,4,5,6), (1,2) ] -> [ (2,3)(4,6,5), (1,2)(3,5)(4,6) ]
\end{lstlisting}
The automorphism of $\Sym_6$ such that 
$(123456)\mapsto (23)(465)$ and $(12)\mapsto(12)(35)(46)$
is not inner. 
Let us compute this homomorphism in the transpositions: 
\begin{lstlisting}
gap> for t in ConjugacyClass(S6, (1,2)) do
> Print("f(", t, ")=", Image(f,t), "\n"); 
> od;
f((1,2))=(1,2)(3,5)(4,6)
f((1,3))=(1,6)(2,5)(3,4)
f((1,4))=(1,4)(2,3)(5,6)
f((1,5))=(1,5)(2,4)(3,6)
f((1,6))=(1,3)(2,6)(4,5)
f((2,3))=(1,3)(2,4)(5,6)
f((2,4))=(1,5)(2,6)(3,4)
f((2,5))=(1,6)(2,3)(4,5)
f((2,6))=(1,4)(2,5)(3,6)
f((3,4))=(1,2)(3,6)(4,5)
f((3,5))=(1,4)(2,6)(3,5)
f((3,6))=(1,5)(2,3)(4,6)
f((4,5))=(1,3)(2,5)(4,6)
f((4,6))=(1,6)(2,4)(3,5)
f((5,6))=(1,2)(3,4)(5,6)
\end{lstlisting}
\end{example}

With \lstinline{AllHomomorphisms} one constructs the set of group homomorphisms
between two given groups.  Similarly, one uses \lstinline{AllEndomorphisms} to
compute endomorphisms.

\begin{example}
We prove that there are ten endomorphisms of $\Sym_3$:
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> Size(AllEndomorphisms(S3));
10
\end{lstlisting}
\end{example}

To construct direct products of groups one uses the function
\lstinline{DirectProduct}. With \lstinline{Embedding} and
\lstinline{Projection} we construct canonical maps. 

\begin{example}
We construct the direct product $\Sym_3\times\D_8$ 
and show that the canonical embedding $\Sym_3\to\Sym_3\times\D_8$, $x\mapsto (x,1)$,  
is injective but not surjective, and that the natural projection  
$\Sym_3\times\D_8\to\Sym_3$, $(x,y)\mapsto x$, is surjective but not injective:
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> D8 := DihedralGroup(8);;
gap> S3xD8 := DirectProduct(S3,D8);;
gap> i_S3 := Embedding(S3xD8, 1);;
gap> p_S3 := Projection(S3xD8, 1);;
gap> IsInjective(i_S3);
true
gap> IsSurjective(i_S3);
false
gap> IsInjective(p_S3);
false
gap> IsSurjective(p_S3);
true
\end{lstlisting}
\end{example}

\begin{example}
Let us check that $C_4\times C_4$ and 
$C_2\times Q_8$ have order $16$, have three elements of order two and twelve elements of order four:
\begin{lstlisting}
gap> C4 := CyclicGroup(IsPermGroup, 4);;
gap> C2 := CyclicGroup(IsPermGroup, 2);;
gap> Q8 := QuaternionGroup(8);;
gap> C4xC4 := DirectProduct(C4, C4);;
gap> C2xQ8 := DirectProduct(C2, Q8);;
gap> List(C4xC4, Order);
[ 1, 4, 2, 4, 4, 4, 4, 4, 2, 4, 2, 4, 4, 4, 4, 4 ]
gap> Collected(List(C4xC4, Order));
[ [ 1, 1 ], [ 2, 3 ], [ 4, 12 ] ]
gap> List(C2xQ8, Order);
[ 1, 4, 4, 2, 4, 4, 4, 4, 2, 4, 4, 2, 4, 4, 4, 4 ]
gap> Collected(List(C2xQ8, Order));
[ [ 1, 1 ], [ 2, 3 ], [ 4, 12 ] ]
\end{lstlisting}
The groups
$C_4\times C_4$ and 
$C_2\times Q_8$ are not isomorphic. 
An easy way to see this uses that 
$C_4\times C_4$ is abelian and $C_2\times Q_8$ is not:
\begin{lstlisting}
gap> IsAbelian(C4xC4);
true
gap> IsAbelian(C2xQ8);
false
\end{lstlisting}
\end{example}


\begin{example}
The center of $C_2\times\Sym_3$ is not stable under endomorphisms of 
$C_2\times\Sym_3$. We see that 
$Z(C_2\times\Sym_3)=\{\id,(12)\}$ and 
that there exists at least one endomorphism of $C_2\times\Sym_3$ that permutes 
the non-trivial element of the center:
\begin{lstlisting}
gap> C2 := CyclicGroup(IsPermGroup, 2);;
gap> S3 := SymmetricGroup(3);;
gap> C2xS3 := DirectProduct(C2, S3);;
gap> Center(C2xS3);
Group([ (1,2) ])
gap> ForAll(AllEndomorphisms(C2xS3),\
> f->Image(f,(1,2)) in [(), (1,2)]);
false
\end{lstlisting}
\end{example}

With \lstinline{DirectFactorsOfGroup} we can 
get the decomposition of a given group as a direct product: 
\begin{lstlisting}
gap> D12 := DihedralGroup(12);;
gap> List(DirectFactorsOfGroup(D12), StructureDescription);
[ "S3", "C2" ]
gap> List(DirectFactorsOfGroup(GL(2,4)), StructureDescription);
[ "C3", "A5" ]
\end{lstlisting}

Now we use
the command 
\lstinline{SemidirectProduct(H, phi, K)} to  
construct the semidirect product $K\rtimes_{\phi} H$, 
where $\phi\colon H\to\Aut(K)$ is a group homomorphism. 

\begin{example}
Let $\langle g\rangle\simeq C_2$ be the cyclic group of order two. 
We will construct the semidirect product $C_7\rtimes_{\phi} C_2$, 
where $\phi\colon C_2\to\Aut(C_7)$, $g\mapsto (x\mapsto x^{-1})$, 
and show that $C_7\rtimes_{\phi}C_2\simeq\D_{14}$:
\begin{lstlisting}
gap> C7 := CyclicGroup(7);;
gap> C2 := CyclicGroup(2);;
gap> f := GroupHomomorphismByFunction(C7, C7, x->Inverse(x));;
gap> f in AutomorphismGroup(C7);
true
gap> Order(f);
2
gap> # Take a generator of C2
gap> g := C2.1;;
gap> phi := GroupHomomorphismByImages(C2, \ 
> AutomorphismGroup(C7), [g], [f]);;
gap> G := SemidirectProduct(C2, phi, C7);;
gap> StructureDescription(G);
"D14"
\end{lstlisting}
We construct the canonical maps $C_2\to C_7\rtimes_{\phi}C_2$, 
$C_3\to C_7\rtimes_{\phi}C_2$ and  $C_7\rtimes_{\phi}C_2\to C_2$:
\begin{lstlisting}
gap> i_C2 := Embedding(G, 1);;
gap> StructureDescription(Image(i_C2));
"C2"
gap> i_C7 := Embedding(G, 2);;
gap> StructureDescription(Image(i_C7));
"C7"
gap> p := Projection(G);;
gap> StructureDescription(Image(p));
"C2"
\end{lstlisting}
\end{example}

\begin{example}
    Let us construct all possible semidirect 
    products (up to isomorphism) of the form $C_4^2\rtimes C_2$:
\begin{lstlisting}
gap> C2 := CyclicGroup(2);;
gap> C4xC4 := AbelianGroup([4,4]);;
gap> hom := AllHomomorphisms(C2, AutomorphismGroup(C4xC4));;
gap> list := [];;
gap> for phi in hom do
> Add(list, SemidirectProduct(C2, phi, C4xC4));
> od;
gap> Set(list, IdGroup);
[ [ 32, 11 ], [ 32, 21 ], [ 32, 24 ],
  [ 32, 25 ], [ 32, 31 ], [ 32, 33 ],
  [ 32, 34 ] ]
\end{lstlisting}
\end{example}

\begin{example}
    Let
    \[
    Q=\left\langle\begin{pmatrix}
    4&1\\
    0&3\end{pmatrix},
    \begin{pmatrix}
        0&3\\
        7&10
    \end{pmatrix}\right\rangle\subseteq\SL_2(11).
    \]
    We construct the semidirect 
    product $G=\F_{11}^2\rtimes Q$ with the natural action:
\begin{lstlisting}
gap> a := [[4,1],[0,3]]*One(GF(11));;
gap> b := [[0,3],[7,10]]*One(GF(11));;
gap> Q := Group([a,b]);;
gap> StructureDescription(Q);
"SL(2,5)"
gap> G := SemidirectProduct(Q, GF(11)^2);
<matrix group of size 14520 with 3 generators>
gap> StructureDescription(G);
"(C11 x C11) : SL(2,5)"
\end{lstlisting}
We now prove that $G$ is a Frobenius group. We need, for example, 
to check that the centralizers $C_Q(k)$ are trivial for $k\in K\setminus\{1\}$:
\begin{lstlisting}
gap> i_K := Embedding(G, 2);;
gap> i_Q := Embedding(G, 1);;
gap> K := Image(i_K);;
gap> ForAll(K, k->IsOne(k) or \ 
> IsTrivial(Centralizer(Image(i_Q), k)));
true
\end{lstlisting}
\end{example}


With \lstinline{InnerAutomorphismsAutomorphismGroup} one constructs the inner
automorphism group of a given group.

\begin{example}
Let us check that $\Aut(\Sym_6)/\Inn(\Sym_6)\simeq C_2$: 
\begin{lstlisting}
gap> S6 := SymmetricGroup(6);;
gap> A := AutomorphismGroup(S6);;
gap> Size(A);
1440
gap> I := InnerAutomorphismsAutomorphismGroup(A);;
gap> Order(A/I);
2
\end{lstlisting}
\end{example}

\begin{example}
    Let us perform some calculations with the canonical homomorphism 
    $\GL_2(\Z/4)\to \GL_2(\Z/2)$. We first
    define the groups:
\begin{lstlisting}
gap> gl4 := GL(2, Integers mod 4);;
gap> gl2 := GL(2, Integers mod 2);;
gap> Order(gl4);
96
gap> Order(gl2);
6
\end{lstlisting}
The group $\GL_2(\Z/4)$ is generated by
$\left\{
\begin{pmatrix}
    0&1\\
    1&0
\end{pmatrix},
\begin{pmatrix}
    1&1\\
    0&1
\end{pmatrix},
\begin{pmatrix}
    3&0\\
    0&1
\end{pmatrix}\right\}$.
\begin{lstlisting}
gap> gens := GeneratorsOfGroup(gl4);;
gap> for x in gens do
> Display(x);
> od;
matrix over Integers mod 4:
[ [  0,  1 ],
  [  1,  0 ] ]
matrix over Integers mod 4:
[ [  1,  1 ],
  [  0,  1 ] ]
matrix over Integers mod 4:
[ [  3,  0 ],
  [  0,  1 ] ]
\end{lstlisting}
So we construct the homomorphism given by
\[
\begin{pmatrix}
    0&1\\
    1&0
\end{pmatrix}\mapsto
\begin{pmatrix}
    0&1\\
    1&0
\end{pmatrix},
\quad 
\begin{pmatrix}
    1&1\\
    0&1
\end{pmatrix}\mapsto
\begin{pmatrix}
    1&1\\
    0&1
\end{pmatrix},
\quad\begin{pmatrix}
    3&0\\
    0&1
\end{pmatrix}\mapsto\id.
\]
\begin{lstlisting}
gap> a := [[0,1],[1,0]]*One(gl2);;
gap> b := [[1,1],[0,1]]*One(gl2);;
gap> f := GroupHomomorphismByImages(gl4, gl2, gens, \
> [a, b, One(gl2)]);;
gap> IsSurjective(f);
true
gap> Size(Kernel(f));
16
\end{lstlisting}
\end{example}

A particular type of group homomorphism is given by 
actions.

\begin{example}
Let us construct the action of 
$C_2=\langle g\rangle$ on abelian groups by inversion. 
We first need to construct the function
corresponding to the action: 
\begin{lstlisting}
gap> inversion := function(a, g)
> if IsOne(g) then
>   return a;
> else
>   return Inverse(a);
> fi;
> end;
function( a, g ) ... end
\end{lstlisting}
Now we perform concrete calculations 
on the abelian group $A=C_2\times C_4$: 
\begin{lstlisting}
gap> C2 := CyclicGroup(IsPermGroup, 2);;
gap> A := AbelianGroup(IsPermGroup, [2, 4]);
Group([ (1,2), (3,4,5,6) ])
gap> Orbits(C2, A, inversion);
[ [ () ], [ (3,4,5,6), (3,6,5,4) ], [ (3,5)(4,6) ], [ (1,2) ],
  [ (1,2)(3,4,5,6), (1,2)(3,6,5,4) ], [ (1,2)(3,5)(4,6) ] ]
gap> rho := ActionHomomorphism(C2, A, inversion);;
gap> Image(rho);
Group([ (2,4)(6,8) ])
gap> Kernel(rho);
Group(())
gap> # By definition, the range is the codomain of the map   
gap> Range(rho);
Sym( [ 1 .. 8 ] )
\end{lstlisting}
In this case, it is an action by group automorphisms. To construct
such homomorphism we can use 
\lstinline{GroupHomomorphismByFunction}. However, 
this function does not check that the resulting map is a group homomorphism.
For that reason, we use \lstinline{GroupHomomorphismByImages}:
\begin{lstlisting}
gap> s := AsSet(A);;
gap> f := GroupHomomorphismByImages(A, A, s, List(s, Inverse));;
gap> IsInjective(f) and IsSurjective(f);
true
\end{lstlisting}

If we take instead of $A$ a non-abelian group, for example $\Sym_3$, we have an action 
of $C_2$ that is not an action by group automorphisms:
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> s := AsSet(S3);;
gap> f := GroupHomomorphismByImages(S3, S3, s, List(s, Inverse));
fail
\end{lstlisting}
Note that the assignment $(123)\mapsto (123)^{-1}=(132)$, $(12)\mapsto (12)^{-1}=(12)$,  does extend to a unique endomorphism of $\Sym_3$.
That is, it inverts this set of generators of $\Sym_3$ but it is not the inversion map $x\mapsto x^{-1}$:
\begin{lstlisting}
gap> gens := [(1,2,3),(1,2)];;
gap> f := GroupHomomorphismByImages(S3, S3, gens, \ 
> List(gens, Inverse));
[ (1,2,3), (1,2) ] -> [ (1,3,2), (1,2) ]
gap> for x in S3 do
> Print(x, "->", x^f, "\n");
> od;
()->()
(2,3)->(1,3)
(1,3)->(2,3)
(1,3,2)->(1,2,3)
(1,2,3)->(1,3,2)
(1,2)->(1,2)
\end{lstlisting}
\end{example}

\begin{example}
\label{exa:GL33}
Let $\gamma\colon \GL_n(q)\to \GL_n(q)$, $\gamma(x) = (x^T)^{-1}$, 
be the map that takes a matrix to its transpose-inverse.
Clearly, $\gamma$ is an automorphism of $\GL_n(q)$ such that $\gamma^2=\id$.
Moreover, $\gamma$ leaves $\SL_n(q)$ and the center $Z(G)$ invariant; in particular $\gamma$  induces automorphisms of $\PSL_n(q)$ and $\PGL_n(q)$. We implement
the map $\gamma$ and its action on invertible matrices:
\begin{lstlisting}
gap> gammaFunction := function(mat)
> return TransposedMat(Inverse(mat));
> end;
function( mat ) ... end
gap> gammaAction := function(mat,g)
> if IsOne(g) then
>   return mat;
> else
>   return gammaFunction(mat);
> fi;
> end;
function( mat, g ) ... end
\end{lstlisting}

We consider the action of the cyclic group $\langle\gamma\rangle$ of order $2$  
on $G = \GL_3(3)$:
\begin{lstlisting}
gap> G := GL(3,3);;
gap> Size(G);
11232
gap> s := AsSet(G);;
gap> gamma := GroupHomomorphismByFunction(G, G, gammaFunction);;
gap> Order(gamma);
2
gap> IsSurjective(gamma) and IsInjective(gamma);
true
\end{lstlisting}
We now compute the set 
$\{x\in G:x^\gamma=x\}$ 
of fixed points. Then 
we quickly obtain the number of orbits:  
\begin{lstlisting}
gap> fix := Filtered(s, x-> x^gamma = x);;
gap> StructureDescription(Group(fix));
"C2 x S4"
gap> # This is Burnside's lemma  
gap> (Size(G) + Size(fix))/2;
5640
\end{lstlisting}

Since $\gamma$ acts on $G$ by group automorphisms,
we now construct the semidirect product
$S=G\rtimes\langle\gamma\rangle$. We need
to construct the  
homomorphism $\phi\colon \langle\gamma\rangle\to\Aut(G)$: 
\begin{lstlisting}
gap> C2 := CyclicGroup(IsPermGroup, 2);;
gap> phi := GroupHomomorphismByImages(C2, AutomorphismGroup(G),\ 
> [(1,2)], [gamma]);;
\end{lstlisting}
We now construct $S$ and use \lstinline{DisplayCompositionSeries} 
to display the composition factors:
\begin{lstlisting}
gap> S := SemidirectProduct(C2, phi, G);;
gap> Order(S);
22464
gap> DisplayCompositionSeries(S);
G (3 gens, size 22464)
 | C2
S (5 gens, size 11232)
 | L3(3)
S (1 gens, size 2)
 | C2
1 (0 gens, size 1)
\end{lstlisting}
Note that \lstinline{DisplayCompositionSeries} prints \lstinline{G}
for our semidirect product (of size 22464) 
and \lstinline{S} for the intermediate subgroups. 

We now verify that set of fixed of points
is equal to the centralizer
$C_G(\gamma)$:  
\begin{lstlisting}
gap> i_C2 := Embedding(S, 1);;
gap> i_G := Embedding(S, 2);;
gap> C := Centralizer(Image(i_G), Image(i_C2));;
gap> Image(i_G, fix) = C;
true
gap> StructureDescription(C);
"C2 x S4"
\end{lstlisting}
\end{example}

\begin{example}
\label{exa:quotientGL33}
    We continue with Example \ref{exa:GL33}. 
    Let $S=\GL_3(3)\rtimes\langle \gamma\rangle$ be
    the semidirect product of the linear group $\GL_3(3)$ 
    by the transpose-inverse map $\gamma$. 
    We construct the quotient $Q=S/Z(\GL_3(3))$:  
\begin{lstlisting}
gap> Q := S/Center(Image(i_G));;
gap> StructureDescription(Q);
"PSL(3,3) : C2"
\end{lstlisting}
Note that $\GL_3(3)$ and $Q$ have the same composition series but they are 
not isomorphic: 
\begin{lstlisting}
gap> DisplayCompositionSeries(Q);
G (3 gens, size 11232)
 | C2
S (12 gens, size 5616)
 | L3(3)
1 (0 gens, size 1)
gap> DisplayCompositionSeries(GL(3,3));
G (size 11232)
 | L3(3)
S (1 gens, size 2)
 | C2
1 (size 1)
gap> IsomorphismGroups(GL(3,3),Q);
fail
\end{lstlisting}
\end{example}

\begin{example}
    We now repeat what we did in Examples \ref{exa:GL33} and
    \ref{exa:quotientGL33}, now with the group $G=\GL_2(7)$:
\begin{lstlisting}
gap> G := GL(2,7);;
gap> s := AsSet(G);;
gap> Size(Orbits(C2, G, gammaAction));
1016
gap> C2 := CyclicGroup(IsPermGroup, 2);;
gap> gamma := GroupHomomorphismByFunction(G, G, gammaFunction);;
gap> phi := GroupHomomorphismByImages(C2, AutomorphismGroup(G), \ 
> [(1,2)], [gamma]);;
gap> S := SemidirectProduct(C2, phi, G);;
gap> StructureDescription(S);
"GL(2,7) : C2
\end{lstlisting}
We check the structure of the centralizer $C_G(\gamma)$:
\begin{lstlisting}
gap> i_C2 := Embedding(S, 1);;
gap> i_G := Embedding(S, 2);;
gap> C := Centralizer(Image(i_G), Image(i_C2));
<permutation group with 2 generators>
gap> StructureDescription(C);
"D16"
\end{lstlisting}
In this case, $S/Z(G) \simeq \PGL_2(7)\rtimes C_2$ is indeed isomorphic to $\PGL_2(7)\times C_2$ since the induced action of $\gamma$ on $\PGL_2(7)$ is inner:
\[
\gamma\begin{pmatrix}
    a&b\\
    c&d
    \end{pmatrix}
    =\frac{1}{ad-bc}
    \begin{pmatrix}
        d&-c\\
        -b&a
    \end{pmatrix}
    =\frac{1}{ad-bc}
    \begin{pmatrix}
        0&-1\\
        1&0
    \end{pmatrix}
    \begin{pmatrix}
    a&b\\
    c&d
    \end{pmatrix}
    \begin{pmatrix}
        0&1\\
        -1&0
    \end{pmatrix}.
\]
Here is the code:
\begin{lstlisting}
gap> N := Center(Image(i_G));;
gap> Q := S/N;;
gap> DirectFactorsOfGroup(Q);
[ PSL(3,2) : C2, C2 ]
gap> not IsomorphismGroups(Q, DirectProduct(PGL(2,7),C2)) = fail;
true
gap> not IsomorphismGroups(Q/Center(Q), PGL(2,7)) = fail;
true
\end{lstlisting}

\end{example}

\begin{example}
Let us see how the alternating group
$\Alt_5$ acts on a coset space by
right multiplication. First compute the list of conjugacy classes of subgroups of $\Alt_5$. There
are nine conjugacy classes of subgroups!

\begin{lstlisting}
gap> A5 := AlternatingGroup(5);;
gap> l := ConjugacyClassesSubgroups(A5);;
gap> Size(l);
9
\end{lstlisting}
For example, the fourth element in our list
is the conjugacy class of subgroups
with representative 
$\langle (23)(45),(24)(35)\rangle$. The conjugacy 
class has five subgroups:
\begin{lstlisting}
gap> class := l[4];
Group( [ (2,3)(4,5), (2,4)(3,5) ] )^G
gap> Size(class);
5
gap> for x in class do
> Display(x);
> od;
Group( [ (2,3)(4,5), (2,4)(3,5) ] )
Group( [ (1,2)(4,5), (1,4)(2,5) ] )
Group( [ (1,2)(3,4), (1,3)(2,4) ] )
Group( [ (1,5)(2,3), (1,3)(2,5) ] )
Group( [ (1,5)(3,4), (1,4)(3,5) ] )
\end{lstlisting}

We can learn some information on representatives
of conjugacy classes of subgroups of $\Alt_5$:
\begin{lstlisting}
gap> List(l, x->Order(Representative(x)));
[ 1, 2, 3, 4, 5, 6, 10, 12, 60 ]
gap> List(l, x->Index(A5, Representative(x)));
[ 60, 30, 20, 15, 12, 10, 6, 5, 1 ]
gap> List(l, \
> x->StructureDescription(Representative(x)));
[ "1", "C2", "C3", "C2 x C2", "C5",
  "S3", "D10", "A4", "A5" ]
\end{lstlisting}
        Let $H$ be the subgroup of $\Alt_5$
        isomorphic to the cyclic group $C_5$ of order five.
        We now construct the action of $\Alt_5$ on
        set $\Alt_5/H$ 
        by right multiplication:
\begin{lstlisting}
gap> H := Representative(l[5]);;
gap> Elements(H);
[ (), (1,2,3,4,5), (1,3,5,2,4),
  (1,4,2,5,3), (1,5,4,3,2) ]
gap> f := ActionHomomorphism(A5,\
> RightCosets(A5,H), OnRight);;
gap> Kernel(f);
1
gap> IsInjective(f);
true
gap> IsSurjective(f);
false
\end{lstlisting}
\end{example}


\section{Finitely presented groups}

\index{Free group}
Let us start working with free groups.  The function \lstinline{FreeGroup}
constructs the free group in a finite number of generators. 

\begin{example}
We create the free group $F_2$ in two generators and we create some random
elements with the function 
\lstinline{Random}:
\begin{lstlisting}
gap> f := FreeGroup(2);
<free group on the generators [ f1, f2 ]>
gap> f.1^2;
f1^2
gap> f.1^2*f.1;
f1^3
gap> f.1*f.1^(-1);
<identity ...>
gap> Random(f);
f1^-3
\end{lstlisting}
\end{example}

\begin{example}
The function \lstinline{Length} can be used to compute the length of a word in a
free group. 
In this example we create $10000$ random elements in $F_2$ and compute their lengths.
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> Collected(List(List([1..10000], x->Random(f)), Length));
[ [ 0, 2270 ], [ 1, 1044 ], [ 2, 1113 ], 
  [ 3, 986 ], [ 4, 874 ], [ 5, 737 ], 
  [ 6, 642 ], [ 7, 500 ], [ 8, 432 ], 
  [ 9, 329 ], [ 10, 248 ], [ 11, 189 ], 
  [ 12, 152 ], [ 13, 119 ], [ 14, 93 ], 
  [ 15, 68 ], [ 16, 57 ], [ 17, 34 ], 
  [ 18, 30 ], [ 19, 23 ], [ 20, 19 ], 
  [ 21, 16 ], [ 22, 8 ], [ 23, 3 ], [ 24, 4 ], 
  [ 25, 4 ], [ 26, 2 ], [ 27, 2 ], [ 28, 1 ], 
  [ 31, 1 ] ]
\end{lstlisting}
\end{example}

Some of the functions that we have used before can also be used in free groups. Examples
of these functions are \lstinline{Normalizer},
\lstinline{RepresentativeAction}, \lstinline{IsConjugate},
\lstinline{Intersection}, \lstinline{IsSubgroup}, \lstinline{Subgroup}. 

\begin{example}
Here we perform some elementary calculations in $F_2$, the free group with
generators $a$ and $b$. We also compute the automorphism group of $F_2$. 
\begin{lstlisting}
gap> f := FreeGroup("a", "b");;
gap> a := f.1;;
gap> b := f.2;;
gap> Random(f);
b^-1*a^-5
gap> Centralizer(f, a);
Group([ a ])
gap> Index(f, Centralizer(f, a));
infinity
gap> Subgroup(f, [a,b]);
Group([ a, b ])
gap> Order(Subgroup(f, [a,b]));
infinity
gap> AutomorphismGroup(f);
<group of size infinity with 3 generators>
gap> GeneratorsOfGroup(AutomorphismGroup(f));
[ [ a, b ] -> [ a^-1, b ], 
  [ a, b ] -> [ b, a ], 
  [ a, b ] -> [ a*b, b ] ]
\end{lstlisting}
We now check that the subgroup $S$ generated by $a^2$, $b$ and $aba^{-1}$ has
index two in $F_2$. We compute $\Aut(S)$ and check that this is not a free
group: 
\begin{lstlisting}
gap> S := Subgroup(f, [a^2, b, a*b*a^(-1)]);
Group([ a^2, b, a*b*a^-1 ])
gap> Index(f, S);
2
gap> A := AutomorphismGroup(S);
<group of size infinity with 3 generators>
gap> IsFreeGroup(A);
false
\end{lstlisting}
\end{example}

\begin{example}
\label{xca:Coxeter}
\index{Coxeter, H.}
Let $n\geq3$ and $p\geq2$ be integers. An amazing result of
Coxeter~\cite{MR1330458} states that the group generated by
$\sigma_1,\dots,\sigma_{n-1}$ and 
\begin{align*}
    &\sigma_i\sigma_{i+1}\sigma_i=\sigma_{i+1}\sigma_i\sigma_{i+1} && \text{ if $i\in\{1,\dots,n-2\}$},\\
    &\sigma_i\sigma_j=\sigma_j\sigma_i && \text{ if $|i-j|\geq 2$},\\
    &\sigma_i^p=1 && \text{ if $i\in\{1,\dots,n-1\}$}\rangle,
\end{align*}
is finite if and only if $(p-2)(n-2)<4$.  

We study the case $n=3$. Let 
\[
G=\langle a,b \mid aba=bab,\,a^p=b^p=1\rangle.
\]
We claim that 
\[
G\simeq\begin{cases}
    \Sym_3 & \text{if $p=2$},\\
    \SL_2(3) & \text{if $p=3$},\\
    \SL_2(3)\rtimes C_4 & \text{if $p=4$},\\
    \SL_2(3)\times C_5 & \text{if $p=5$}.
\end{cases}
\]
Here is the proof:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> a := f.1;;
gap> b := f.2;;
gap> p := 2;;
gap> while p-2<4 do
> G := f/[a*b*a*Inverse(b*a*b), a^p, b^p];;
> Display(StructureDescription(G));
> p := p+1;
> od;
S3
SL(2,3)
SL(2,3) : C4
C5 x SL(2,5)    
\end{lstlisting}
\end{example}

\begin{example}
For positive integers $l,m,n$, we define the \textbf{von Dyck group} (or triangular group)
of type $(l,m,n)$ as the group
\[
G(l,m,n)=\langle
a,b \mid a^l=b^m=(ab)^n=1\rangle.
\]
It is known that $G(l,m,n)$ is finite if and only if 
\[
\frac{1}{l}+\frac{1}{m}+\frac{1}{n}>1.
\]
We claim that 
\[
G(2,3,3)\simeq\Alt_4, \quad
G(2,3,4)\simeq\Sym_4,\quad
G(2,3,5)\simeq\Alt_5. 
\]
Here is the code:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> a := f.1;;
gap> b := f.2;;
gap> StructureDescription(f/[a^2,b^3,(a*b)^3]);
"A4"
gap> StructureDescription(f/[a^2,b^3,(a*b)^4]);
"S4"
gap> StructureDescription(f/[a^2,b^3,(a*b)^5]);
"A5"
\end{lstlisting}
\end{example}

\begin{example}
This example is taken from~\cite{MR1786869}. Let us check that the group
\[
\langle a,b,c\mid a^3=b^3=c^4=1,\,ac=ca^{-1},\,aba^{-1}=bcb^{-1}\rangle
\]
is trivial. For that purpose we use \lstinline{IsTrivial}:
\begin{lstlisting}
gap> f := FreeGroup(3);;
gap> a := f.1;;
gap> b := f.2;;
gap> c := f.3;;
gap> G := f/[a^3, b^3, c^4, c^(-1)*a*c*a, \
> a*b*a^(-1)*b*c^(-1)*b^(-1)];;
gap> IsTrivial(G);
true
\end{lstlisting}
\end{example}

\begin{remark}
The \emph{word problem} is the problem of deciding whether two given expressions are equivalent with respect to a set of rewriting identities. 
In 1955 P. Novikov proved that there exists a finitely presented group 
such that the word problem for this group is undecidable.
\end{remark}
%  Novikov, P. S. (1955), "On the algorithmic unsolvability of the word problem in group theory", Proceedings of the Steklov Institute of Mathematics (in Russian), 44: 1–143, Zbl 0068.01301

\begin{example}
In \cite{MR1732210} it is proved that for a positive integer  $n$, 
\[
\langle a,b \mid a^{-1}b^na=b^{n+1},\;a=a^{i_1}b^{j_1}a^{i_2}b^{j_2}\cdots a^{i_k}b^{j_k}\rangle,
\]
is trivial if $i_1+i_2+\cdots i_k=0$. As an example we show that
\[
\langle a,b\mid a^{-1}b^2a=b^3,\,a=a^{-1}ba\rangle
\]
is the trivial group: 
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> a := f.1;;
gap> b := f.2;;
gap> G := f/[a^(-1)*b^2*a*b^(-3),a*(a^(-1)*b*a)];;
gap> IsTrivial(G);
true
\end{lstlisting}
\end{example}

\index{Burnside group}
For each $n\geq2$ the Burnside group $B(2,n)$ is defined as the group 
\[
        B(2,n)=\langle a,b\mid w^n=1\text{ for all word $w$ in the letters $a$ and $b$}\rangle. 
\]

\begin{example}
	We prove that the group $B(2,3)$ is a finite group of order $27$.
	Let $F$ be the free group of rank two. We divide $F$ by the
	normal subgroup generated by $\{w_1^3,\dots,w_{10000}^3\}$, where 
	$w_1,\dots,w_{10000}$ are some randomly chosen words of $F$.
	The following
	code shows that $B(2,3)$ is finite:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> rels := Set(List([1..10000], x->Random(f)^3));;
gap> B23 := f/rels;;
gap> Order(B23);
27
gap> Number(B23, x->IsOne(x^3));
27
\end{lstlisting}
Note that our $G$ is exactly the group $B(2,3)$, as every non-trivial
element has order three. The group $B(2,3)$ is isomorphic
to the Heisenberg group
\[
H_3=\left\{\begin{pmatrix}
    1 & a & b\\
    0 & 1 & c\\
    0 & 0 & 1
    \end{pmatrix}:a,b,c\in\F_3\right\}
    =\left\langle 
    \begin{pmatrix}
    1 & 1 & 0\\
    0 & 1 & 0\\
    0 & 0 & 1
    \end{pmatrix},
    \begin{pmatrix}
    1 & 0 & 0\\
    0 & 1 & 1\\
    0 & 0 & 1
    \end{pmatrix}
    \right\rangle.
\]
Here is the code:
\begin{lstlisting}
gap> a := [[1,1,0],[0,1,0],[0,0,1]]*One(GF(3));;
gap> b := [[1,0,0],[0,1,1],[0,0,1]]*One(GF(3));;
gap> Display(a);
 1 1 .
 . 1 .
 . . 1
gap> Display(b);
 1 . .
 . 1 1
 . . 1
gap> H3 := Group([a,b]);;
gap> Order(H3);
27
gap> not IsomorphismGroups(B23, H3) = fail;
true
\end{lstlisting}
\end{example}

\begin{example}
	It is known that $B(2,4)$ is a finite group of order 4096. 
	Here we present a
	computational proof.  We use the same trick as before:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> rels := Set(List([1..10000], x->Random(f)^4));;
gap> B24 := f/rels;;
gap> Order(B24);
4096
gap> Number(B24, x->IsOne(x^4));
4096
\end{lstlisting}
\end{example}



%\section{Actions}




\section{Problems}


\begin{prob}
Compute the order of the subgroup of $\GL_2(\Z)$ generated by
\begin{align*}
    \begin{pmatrix}
        1 & 0\\
        0 & 1
    \end{pmatrix},
    &&
    \begin{pmatrix}
        -1 & 0\\
        0 & -1
    \end{pmatrix},
    &&
    \begin{pmatrix}
        -1 & 0\\
        0 & 1
    \end{pmatrix},
    &&
    \begin{pmatrix}
        1 & 0\\
        0 & -1
    \end{pmatrix}.
\end{align*}
Can you recognize this group?
\end{prob}

\begin{prob}
  Construct the Heisenberg group 
  \[
H_5=\left\{\begin{pmatrix}
    1 & a & b\\
    0 & 1 & c\\
    0 & 0 & 1
    \end{pmatrix}:a,b,c\in\F_5\right\}
\]
as a permutation group. 
\end{prob}

\begin{prob}
    Prove that all subgroups of $C_4\times Q_8$ are normal.
\end{prob}

\begin{prob}
    Let $G$ be the set of matrices of the form
    \[
        \begin{pmatrix}
            1&c\\
            0&d
        \end{pmatrix},
        \quad
        c,d\in\F_4,\;c\ne0.
    \]
    Prove that $G$ is a group and compute its order. 
\end{prob}

\begin{prob}
    Find all groups of order 12 that are 
    non-trivial semidirect products.
\end{prob}

%\begin{prob}
%    Encuentre todos los subgrupos de $\Sym_4$ que actúan transitivamente en
%    $\{1,2,3,4\}$ y analice la transitividad múltiple.
%\end{prob}

\begin{prob}
	\label{prob:subgroupsA6A7}
	Use the function \lstinline{IsomorphicSubgroups} to prove that
	$\Alt_{6}$ does not contain a subgroup isomorphic to $\Sym_5$ and that
	$\Alt_{7}$ contains a subgroup isomorphic to $\Sym_5$.
\end{prob}

\begin{prob}
Prove that $\Alt_6$ does not contain subgroups of prime index. 
\end{prob}

\begin{prob}
	\label{prob:normal_SL2(3)}
	Prove that $\SL_2(3)$ has a unique normal subgroup of order eight. 
\end{prob}

%\begin{prob}
%	\label{prob:Sylow_SL2(3)}
%	\textcolor{blue}{
%	Prove that $\{I,-I\}$ is the unique Sylow $2$-subgroup of $\SL_2(3)$ and
%	that the quotient group $\SL_2(3)/\{I,-I\}$ has four Sylow $3$-subgroups
%and a unique Sylow $2$-subgroup.}
%\end{prob}

\begin{prob}
	\label{prob:SL2(5)}
	Find a subgroup of $\SL_2(5)$ isomorphic to $\SL_2(3)$.
\end{prob}

\begin{prob}
  Use the functions \lstinline{SylowSubgroup} and 
  \lstinline{ConjugacyClassSubgroups} to construct all Sylow subgroups 
  of $\Alt_4$ and $\Sym_4$.
\end{prob}

\begin{prob}
  Prove that $\Sym_5$ has a Sylow $2$-subgroup isomorphic to the dihedral group
  of eight elements. 
%  Prove that $\Sym_6$ has a $2$-Sylow subgroup isomorphic to 
%	Verifique que $\Sym_5$ tiene un $2$-subgrupo de Sylow isomorfo a $\D_8$ y que 
%	$\Sym_6$ tiene un $2$-subgrupo de Sylow isomorfo a $\D_8\times C_2$.
%%gap> SylowSubgroup(SymmetricGroup(5), 2);
%%Group([ (1,2), (3,4), (1,3)(2,4) ])
%%gap> StructureDescription(last);
%%"D8"
%%gap> SylowSubgroup(SymmetricGroup(6), 2);
%%Group([ (1,2), (3,4), (1,3)(2,4), (5,6) ])
%%gap> StructureDescription(last);
%%"C2 x D8"
\end{prob}

\begin{prob}
  Can you recognize the structure of Sylow $2$-subgroups of $\Sym_6$?
\end{prob}

\begin{prob}
  Use the function \lstinline{Normalizer} to compute the number of conjugates
  of Sylow $2$-subgroups of $\Alt_5$. 
\end{prob}

\begin{prob}
    Find all Sylow subgroups of $C_{27}$, $\SL_2(5)$,
    $\Sym_7$, $\Sym_3\times\Alt_4$ and $\Sym_3\times C_{20}$. 
\end{prob}

\begin{prob}
    Let $p\in\{2,3\}$. 
  Compute the conjugacy classes of subgroups of $\Sym_3\times\Sym_3$ and find
  (if possible) 
  three Sylow $p$-subgroups, say $A,B,C$, such that $A\cap B=\{1\}$ and $A\cap
  C\ne\{1\}$.
\end{prob}

\begin{prob}
    Prove that the group
    \[
        \left\{\begin{pmatrix}
            1&b\\
            0&d
        \end{pmatrix}
        :b,d\in\F_{19},\;d\ne0
        \right\}
    \]
    is not simple.
\end{prob}

\begin{prob}
    Let $G$ be the group generated by the permutations 
    $(12)(6\,11)(8\,12)(9\,13)$, $(5\,139)(6\,10\,11)(78\,12)$ and 
    $(2,4,3)(5,8,9)(6\,10\,13)(7\,11\,12)$. 
    How many elements of $G$ are commutators?
\end{prob}

\begin{prob}
Prove that $\Alt_4\times C_7$ does not contain subgroups of index two.
\end{prob}

\begin{prob}
  \label{prob:A5:8,15,20,24,30,49}
  Prove that $\Alt_5$ does not contain subgroups of order $8,15,20,24,30,40$. 
\end{prob}

\begin{prob}
    %FIXME: referencia
    %http://ysharifi.wordpress.com/2012/04/21/maximum-order-of-abelian-subgroups-in-a-symmetric-group/
  It is known that an abelian subgroup of $\Sym_n$ has order
  $\leq3^{\lfloor n/3\rfloor}$. How good is this bound? For
  $n\in\{5,6,7,8\}$ find (if possible) an abelian subgroup of $\Sym_n$ of
  order $3^{\lfloor n/3\rfloor}$. 
\end{prob}

\begin{prob}
    Prove that $\SL_2(5)$ does not contain subgroups isomorphic to $\Alt_5$. 
\end{prob}

\begin{prob}
  Prove that for each positive divisor $d$ of $24$ there exists a subgroup of $\Sym_4$ of order $d$.
\end{prob}

\begin{prob} 
  Prove that $\SL_2(3)$ contains a unique element of order two. Prove that
  $\SL_2(3)$ does not have subgroups of order $12$.
\end{prob}

\begin{prob}
  Prove that the derived subgroup of $\SL_2(3)$ is isomorphic to $Q_8$.
\end{prob}

\begin{prob}
  Can you recognize the group $\SL_2(3)/Z(\SL_2(3))$? 
\end{prob}

\begin{prob}
  Are the groups $\Sym_5$ and $\SL_2(5)$ isomorphic?
\end{prob}

\begin{prob}
  Let $\langle r,s\mid 
  r^8=s^2=1,\,srs=r^{-1}\rangle$ be the dihedral group of sixteen elements. 
  Find all subgroups containing $r^2$.
\end{prob}

\begin{prob}
  Find all the group homomorphisms $\Sym_3\to\SL_2(3)$. 
\end{prob}

\begin{prob}
    Are there any surjective homomorphisms $\D_{16}\to\D_{8}$? What about
    $\D_{16}\to C_2$?
\end{prob}

\begin{prob}
  \label{prob:Aut(A4)=S4}
   Prove that $\Aut(\Alt_4)\simeq\Sym_4$.
\end{prob}

\begin{prob}
  Prove that $\Aut(\D_8)\simeq\D_8$ and that 
  $\Aut(\D_{16})\not\simeq\D_{16}$.
\end{prob}

\begin{prob}
Compute the order of the group $\Aut(C_{11}\times C_2\times C_3)$. 
\end{prob}

\begin{prob}
  Prove that $\D_{12}\simeq\Sym_3\times C_2$.
\end{prob}

\begin{prob}
  Let $G$ be a group of order $12$ such that $G\not\simeq\Alt_4$. Prove that
  $G$ contains an element of order six.
\end{prob}

%\begin{prob}
%    Determine la cantidad de grupos de Sylow que tienen los grupos no abelianos
%    de orden $34$.
%\end{prob}
%

\begin{prob}
	\label{prob:minimal}
	Compute the list of normal subgroups of $\GL_2(3)$.
\end{prob}

\begin{prob}
	Compute the list of minimal subgroups of $\PGL_2(7)$.
\end{prob}

\begin{prob}
    Use the function \lstinline{CharacteristicSubgroups} to compute
    all characteristic subgroups of $\GL_3(7)$. Can you describe these subgroups?
\end{prob}

\begin{prob}
	\label{prob:socle}
	Compute the socle and the list of minimal normal subgroups of $\GL_2(9)$. 
\end{prob}

\begin{prob}
	\label{prob:fitting}
	Compute the Fitting and the Frattini subgroup of $\SL_2(3)$.
\end{prob}

\begin{prob}
	\label{prob:maximal}
	Compute the list of all maximal normal subgroups of $\SL_2(5)$. 
\end{prob}

\begin{prob}
    Find all characteristic subgroups
    of the Heisenberg group $H_5$. 
\end{prob}

\begin{prob}
	\label{prob:PSL2(7)_max}
	Prove that $\PSL_2(7)$ has a maximal subgroup of order 16.
\end{prob}

\begin{prob}
    Given  a set of prime numbers $\pi$ and a group $G$, we say that $G$ is $\pi$-separable 
    if there exists a chain of subgroups
    \[
    \{1\} = N_0 \subseteq N_1\subseteq \ldots\subseteq N_s = G,
    \]
    where each $N_i$ is normal in $N_{i+1}$ and each $N_{i+1}/N_i$ is either a $\pi$-group 
    or a $\pi'$-group. Write a function that detects $\pi$-separable groups.
\end{prob}

% The socle and minimal subgroups
%gap> G := SymmetricGroup(3);;
%gap> MinimalNormalSubgroups(G);
%[ Group([ (1,2,3) ]) ]
%gap> Socle(G);
%Group([ (1,2,3) ])
%gap> G := AlternatingGroup(4);;
%gap> MinimalNormalSubgroups(G);
%[ Group([ (1,2)(3,4), (1,3)(2,4) ]) ]
%gap> Socle(G);
%Group([ (1,2)(3,4), (1,4)(2,3) ])
%gap> IsCharacteristicSubgroup(G,Socle(G));
%true

% IsSubnormal
% Frattini
% Fitting
% MaximalNormal
% Maximal

\begin{prob}
	\label{prob:ChermakDelgado}
	\index{Chermak--Delgado subgroup}
	Let $G$ be a finite group and $H$ be a subgroup. The \textbf{Chermak--Delgado} measure of $H$ 
	is the number $m_G(H)=|H||C_G(H)|$. 
	\begin{enumerate}[label=(\alph*)]
	\item Write a function to compute the Chermak--Delgado measure.
	\item Compute $m_G(H)$ for $G\in\{\Sym_3,\D_8\}$ and $H$ a subgroup of $G$. 
\end{enumerate}
\end{prob}

\begin{prob}
	\label{prob:holA4}
% 	The code 
% \begin{lstlisting}
% gap> Holomorph := function(group)
% > local aut;
% > aut := AutomorphismGroup(group);
% > return SemidirectProduct(aut, group);
% > end;
% function( group ) ... end	
% \end{lstlisting}
%     returns the holomorph of a given group.
Recall that the holomorph of a group $G$ is the semidirect product $G\rtimes \Aut(G)$.
\begin{enumerate}
\item Write a function that, given a group $G$, returns its holomorph.
\item Compute the holomorph of $\Alt_4$. 
\item Find a permutation representation 
of small degree of the holomorph of $\Alt_4$ and find a minimal normal subgroup of order four. 
\end{enumerate}
This is an exercise of~\cite{MR2478410}.
\end{prob}


\begin{prob}
    Let $G$ be a finite group and 
    $\mathcal{P}$ be the set of subsets of $G$ 
    containing the identity of $G$ and 
    \[
    f\colon\mathcal{P}\to\mathcal{P},
    \quad S\mapsto \{xy^{-1}:x,y\in S\}.
    \]
    \begin{enumerate}
        \item Write a function for the map $f$.
        \item Write a function that, given an element $S\in \mathcal{P}$, returns the smallest non-negative integer $n$
            such that $f^n(S)=f^{n+1}(S)$.
            Which subsets have $n = 0$?
        \item Write a function that computes
        \[
        \mu(G)=\min\{n\geq 0:f^n(S)=f^{n+1}(S)\text{ for all $S\in\mathcal{P}$}\}.
        \]
        \item Compute $\mu(\Sym_3)$, $\mu(\D_8)$ and $\mu(\Alt_4)$.
    \end{enumerate}
\end{prob}



\begin{prob}
Prove that the group $\langle (123\cdots7),(26)(34)\rangle$ is simple, 
has order $168$ and acts transitively on $\{1,\dots,7\}$.
Can you recognize this group?
%%gap> G := Group([(1,2,3,4,5,6,7),(2,6)(3,4)]);;
%onjuga
%%true
%%gap> IsTransitive(G, [1..7]);
%%true
%%gap> Order(G);
%%168
\end{prob}

\begin{prob}
  Compute the order of the group $\langle a,b\mid a^2=b^2=(bab^{-1})^3=1\rangle$. 
\end{prob}

\begin{prob}
  Prove that $\langle a,b\mid a^2=aba^{-1}b=1\rangle$ is an infinite group.
\end{prob}

\begin{prob}
	\label{prob:order16}
  Compute the order of the group
  $\langle a,b\mid  a^8=b^2a^4=ab^{-1}ab=1\rangle$.
\end{prob}

\begin{prob}
\label{prob:recognizeA5}
Can you recognize the group $\langle a,b\mid a^5=1, b^2=(ab)^3, (a^3ba^4b)^2=1\rangle$?
\end{prob}
%
\begin{prob}
Prove that the group $\langle a,b\mid a^2=b^3=a^{-1}b^{-1}ab=1\rangle$ is finite and cyclic.
\end{prob}

\begin{prob}
Prove that the group $\langle a,b\mid a^2=b^3=1\rangle$ is non-abelian.
\end{prob}

\begin{prob}
\label{prob:order=648}
Compute the order of the group
\[
  \langle a,b,c\mid a^3=b^3=c^3=1,\,aba=bab,\,cbc=bcb,\,ac=ca\rangle.
\]
\end{prob}

\begin{prob}
	\label{prob:Serre}
	Prove that the group $\langle a,b,c\mid bab^{-1}=a^{2},\,cbc^{-1}=b^{},\,
	aca^{-1}=c^{2}\rangle$ is trivial. This is an exercise of Serre's
	book~\cite[\S1]{MR1954121}.
\end{prob}

\begin{prob}
	\label{prob:B23perm}
	Find a permutation representation of the group $B(2,3)$.
\end{prob}

\begin{prob}
	\label{prob:B33}
  \index{Burnside group}
	Prove that 
	$B(3,3)$ %=\langle a,b,c:w^3=1\text{ for all word $w$ in $a,b$}\rangle$ 
	is a finite group. Can you compute the
	order of $B(3,3)$?
	%of order $\leq2187$. 
\end{prob}

\begin{prob}
	\label{prob:probability}
	Let $G$ be a finite group with $k$ conjugacy classes.  It is known that the
	probability that two elements of $G$ commute is equal to
	$\mathrm{prob}(G)=k/|G|$. Compute this probability for $\SL_2(3)$,
	$\Alt_4$, $\Alt_5$, $\Sym_4$ and $Q_8$.
\end{prob}


