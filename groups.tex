\chapter{Basic group theory}

% TODO
% * cosets
% * actions
% * double cosets

\section{Basic constructions}

A \textbf{matrix group} is a subgroup of $\GL(n,\K)$ for some $n\in\N$ and some
field $\K$. A \textbf{permutation group} is a subgroup of some $\Sym_n$. One
constructs groups with the function \lstinline{Group}. 

\begin{example}
With \lstinline{Order} we compute the order of the following groups: a) the
group generated by the transposition $(12)$, b) the group generated by the
$5$-cycle $(12345)$, and c) the group generated by the permutations
$\{(12),(12345)\}$: 
\begin{lstlisting}
gap> Order(Group([(1,2)]));
2
gap> Order(Group([(1,2,3,4,5)]));
5
gap> Order(Group([(1,2),(1,2,3,4,5)]));
120
\end{lstlisting}
\end{example}

\index{Group!cyclic}
For $n\in\N$ let $C_n$ be the (multiplicative) cyclic group of order $n$. One
construct cyclic groups with \lstinline{CyclicGroup}. With no extra arguments,
this function returns an abstract presentation of a cyclic group. 

\begin{example}
Let us construct the cyclic group $C_2$ of size two as an abstract group, as a
matrix group and as a permutation group. 
\begin{lstlisting}
gap> CyclicGroup(2);
<pc group of size 2 with 1 generators>
gap> CyclicGroup(IsMatrixGroup, 2);
Group([ [ [ 0, 1 ], [ 1, 0 ] ] ])
gap> CyclicGroup(IsPermGroup, 2);
Group([ (1,2) ])
\end{lstlisting}
\end{example}

\index{Group!dihedral}
For $n\in\N$ the 
\textbf{dihedral group} of order $2n$ is the group 
\[
\D_{2n}=\langle r,s:srs=r^{-1},\,s^2=r^n=1\rangle.
\]
To construct dihedral groups we use \lstinline{DihedralGroup}.  With no extra
arguments, the function returns an abstract presentation of a dihedral group.
As we did before for cyclic groups, we can construct dihedral groups as
permutation groups. 

\begin{example}
Let us construct $\D_6$, compute its order and check that this is an abelian group. 
\begin{lstlisting}
gap> D6 := DihedralGroup(6);;
gap> Order(D6);
6
gap> IsAbelian(D6);
false
\end{lstlisting}
To display the elements of the group we use 
\lstinline{Elements}: 
\begin{lstlisting}
gap> Elements(DihedralGroup(6));
[ <identity> of ..., f1, f2, f1*f2, f2^2, f1*f2^2 ]
gap> Elements(DihedralGroup(IsPermGroup, 6));
[ (), (2,3), (1,2), (1,2,3), (1,3,2), (1,3) ]
\end{lstlisting}
\end{example}

\index{Group!alternating}
One constructs the symmetric group $\Sym_n$ with \lstinline{SymmetricGroup}.
Of course, the elements of $\Sym_n$ are permutations of the set $\{1,\dots,n\}$
To construct the alternating group $\Alt_n$ one uses
\lstinline{AlternatingGroup}. 

\begin{example}
Let us construct $\Alt_4$ and $\Sym_4$ and display their elements. 
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> A4 := AlternatingGroup(4);;
gap> Elements(A4);
[ (), (2,3,4), (2,4,3), (1,2)(3,4), (1,2,3), (1,2,4), 
  (1,3,2), (1,3,4), (1,3)(2,4), (1,4,2), (1,4,3), 
  (1,4)(2,3) ]
gap> Elements(S4);
[ (), (3,4), (2,3), (2,3,4), (2,4,3), (2,4), (1,2), 
  (1,2)(3,4), (1,2,3), (1,2,3,4), (1,2,4,3), (1,2,4), 
  (1,3,2), (1,3,4,2), (1,3), (1,3,4), (1,3)(2,4), 
  (1,3,2,4), (1,4,3,2), (1,4,2), (1,4,3), (1,4), 
  (1,4,2,3), (1,4)(2,3) ]
\end{lstlisting}
Now let us check that 
\begin{lstlisting}
gap> (1,2,3) in A4;
true
gap> (1,2) in A4;
false
gap> (1,2,3)(4,5) in S4;
false
\end{lstlisting}
\end{example}

\begin{example}
Let us check that $\Sym_3$ has two elements of order three and three elements of order two. 
One computes order of elements with \lstinline{Order}. 
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> List(S3, Order);
[ 1, 2, 3, 2, 3, 2 ]
gap> Collected(List(S3, Order));
[ [ 1, 1 ], [ 2, 3 ], [ 3, 2 ] ]
\end{lstlisting}
\end{example}

\begin{example}
Let us show that 
	\[
	G=\left\langle 
	\begin{pmatrix}
		0 & i\\
		i & 0
	\end{pmatrix}
	,\;
	\begin{pmatrix}
		0 & 1\\
		-1 & 0
	\end{pmatrix}\right\rangle
	\]
is a non-abelian group of order eight not isomorphic to a dihedral group.
Recall that for~\GAP, the imaginary unit $i=\sqrt{-1}$ is \lstinline{E(4)}. To
check that $G\not\simeq\D_8$ we see that $G$ contains a unique element of order
two and $\D_8$ has five elements of order two: 
\begin{lstlisting}
gap> a := [[0,E(4)],[E(4),0]];;
gap> b := [[0,1],[-1,0]];;
gap> G := Group([a,b]);;
gap> Order(G);
8
gap> IsAbelian(G);
false
gap> Number(G, x->Order(x)=2);
1
gap> Number(DihedralGroup(8), x->Order(x)=2);
5
\end{lstlisting} 
\end{example}

\begin{example}
The Mathieu group $M_{11}$ is a simple group of order $7920$. It is defined as 
the subgroup of $\Sym_{11}$ generated by 
\[
(123456789\,10\,11),\quad
(37\,11\,8)(4\,10\,56).
\]
Let us construct $M_{11}$ and check with \lstinline{IsSimple} that $M_{11}$ is
simple: 
\begin{lstlisting}
gap> a := (1,2,3,4,5,6,7,8,9,10,11);;
gap> b := (3,7,11,8)(4,10,5,6);;
gap> M11 := Group([a,b]);;
gap> Order(M11);
7920
gap> IsSimple(M11);
true
\end{lstlisting}
\end{example}

\begin{example}
\index{Order!of elements}
The function \lstinline{Group} can also be used to construct infinite groups. 
Let us consider two matrices with finite order and such that their product has infinite order. 
\begin{lstlisting}
gap> a := [[0,-1],[1,0]];;
gap> b:= [[0,1],[-1,-1]];;
gap> Order(a);
4
gap> Order(b);
3
gap> Order(a*b);
infinity
gap> Order(Group([a,b]));
infinity
\end{lstlisting}
\end{example}

\begin{remark}
	Not always \GAP~will be able to determine whether an element has finite
	order or not! 
\end{remark}

\index{Subgroups}
\index{Index}
With \lstinline{Subgroup} we construct the subgroup of a group generated by a
list of elements. The function \lstinline{AllSubgroups} returns the list of
subgroups of a given group. The index of a subgroup can be computed with
\lstinline{Index}.

\begin{example}
Let us check that the subgroup of $\Sym_3$ generated by $(12)$ is
$\{\id,(12)\}$ and has index three, and the subgroup of $\Sym_3$ generated by
$(123)$ is $\{\id,(123),(132)\}$ and has index two: 
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> Elements(Subgroup(S3, [(1,2)]));
[ (), (1,2) ]
gap> Index(S3, Subgroup(S3, [(1,2)]));
3
gap> Elements(Subgroup(S3, [(1,2,3)]));
[ (), (1,2,3), (1,3,2) ]
gap> Index(S3, Subgroup(S3, [(1,2,3)]));
2
\end{lstlisting}
\end{example}

Recall that a subgroup $K$ of $G$ is said to be normal if $gKg^{-1}\subseteq K$
for all $g\in G$. If $K$ is normal in $G$, then $G/K$ is a group.

\begin{example}
With \lstinline{IsSubgroup} we check that $\Alt_4$ is a subgroup of 
$\Sym_4$. With \lstinline{IsNormal} we see that 
$\Alt_4$ is a subset of $\Sym_4$ under conjugation: 
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> A4 := AlternatingGroup(4);;
gap> IsSubgroup(S4,A4);
true
gap> IsNormal(S4,A4);
true
gap> Order(S4/A4);
2
\end{lstlisting}
The subgroup of $\Sym_4$ generated by $(123)$ is not normal in $\Sym_4$:
\begin{lstlisting}
gap> IsNormal(S4, Subgroup(S4, [(1,2,3)]));
false
\end{lstlisting}
\end{example}

\begin{example}
Let us show that in $\D_8$ there are subgroups $H$ and $K$ such that $K$ is
normal in $H$, $H$ is normal in $G$ and $K$ is not normal in $G$. 
\begin{lstlisting}
gap> D8 := DihedralGroup(IsPermGroup, 8);;
gap> Elements(D8);
[ (), (2,4), (1,2)(3,4), (1,2,3,4), 
  (1,3), (1,3)(2,4), (1,4,3,2), 
  (1,4)(2,3) ]
gap> K := Subgroup(D8, [(2,4)]);;
gap> Elements(K);
[ (), (2,4) ]
gap> H := Subgroup(D8, [(1,2,3,4)^2,(2,4)]);;
gap> Elements(H);
[ (), (2,4), (1,3), (1,3)(2,4) ]
gap> IsNormal(D8, K);
false
gap> IsNormal(D8, H);
true
gap> IsNormal(H, K);
true
\end{lstlisting}
\end{example}

% TODO: usar normal?
\begin{example}
Let us compute the quotients of the cyclic group $C_4$. Since every subgroup of
$C_4$ is normal, we can use \lstinline{AllSubgroups} to check that $C_4$
contains a unique non-trivial proper subgroup $K$. The quotient $C_4/K$ has two
elements: 
\begin{lstlisting}
gap> C4 := CyclicGroup(IsPermGroup, 4);;
gap> AllSubgroups(C4);
[ Group(()), Group([ (1,3)(2,4) ]), 
  Group([ (1,2,3,4) ]) ]
gap> K := last[2];;
gap> Order(C4/K);
2
\end{lstlisting}
\end{example}

Recall that for $n\in\N$ the generalized quaternion group is the group 
\[
Q_{4n}=\langle x,y \mid x^{2n} = y^4 = 1, x^n = y^2, y^{-1}xy = x^{-1}\rangle.
\]
We use \lstinline{QuaternionGroup} to construct generalized quaternion groups.
As we did before, we can use the filters \lstinline{IsPermGroup} (resp.
\lstinline{IsMatrixGroup}) to obtain generalized quaternion groups as
permutation (resp. matrix) groups. 

\begin{example}
	Let us check that each subgroup of the quaternion group $Q_8$ of order
	eight is normal and that $Q_8$ is non-abelian: 
\begin{lstlisting}
gap> Q8 := QuaternionGroup(IsMatrixGroup, 8);;                                        
gap> IsAbelian(Q8);
false
gap> ForAll(AllSubgroups(Q8), x->IsNormal(Q8,x));
true
\end{lstlisting}
\end{example}

\index{Center}
If $G$ is a group, its center is the subgroup
\[
	Z(G)=\{x\in G:xy=yx\text{ for all $y\in G$}\}.
\]

\index{Commutator} 
The commutator of two elements $x,y\in G$ is defined as
$[x,y]=x^{-1}y^{-1}xy$. The commutator subgroup, or derived subgroup of $G$, is the subgroup $[G,G]$
generated by all the commutators of $G$. 

\begin{example}
Let us check that $\Alt_4$ has trivial center and that its commutator is the group 
$\{\id,(12)(34),(13)(24),(14)(23)\}$:
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> IsTrivial(Center(A4));
true
gap> Elements(DerivedSubgroup(A4));
[ (), (1,2)(3,4), (1,3)(2,4), (1,4)(2,3) ]
\end{lstlisting}
\end{example}

To construct direct products of groups one uses the function
\lstinline{DirectProduct}. 

\begin{example}
Let us check that $C_4\times C_4$ and 
$C_2\times Q_8$ have order $16$, have three elements of order two and twelve elements of order four. 
\begin{lstlisting}
gap> C4 := CyclicGroup(IsPermGroup, 4);;
gap> C2 := CyclicGroup(IsPermGroup, 2);;
gap> Q8 := QuaternionGroup(8);;
gap> C4xC4 := DirectProduct(C4, C4);;
gap> C2xQ8 := DirectProduct(C2, Q8);;
gap> List(C4xC4, Order);
[ 1, 4, 2, 4, 4, 4, 4, 4, 2, 4, 2, 4, 4, 4, 4, 4 ]
gap> Collected(List(C4xC4, Order));
[ [ 1, 1 ], [ 2, 3 ], [ 4, 12 ] ]
gap> List(C2xQ8, Order);
[ 1, 4, 4, 2, 4, 4, 4, 4, 2, 4, 4, 2, 4, 4, 4, 4 ]
gap> Collected(List(C2xQ8, Order));
[ [ 1, 1 ], [ 2, 3 ], [ 4, 12 ] ]
\end{lstlisting}
Are these groups isomorphic? No. An easy way to see this is the following: 
$C_4\times C_4$ is abelian and $C_2\times Q_8$ is not:
\begin{lstlisting}
gap> IsAbelian(C4xC4);
true
gap> IsAbelian(C2xQ8);
false
\end{lstlisting}
\end{example}

\index{Conjugacy class}
\index{Centralizer}
Recall that if $G$ is a group and $g\in G$, the \textbf{conjugacy class} of $g$
in $G$ is the subset $g^G\coloneqq\{x^{-1}gx:x\in G\}$. The \textbf{centralizer} of $g$ in
$G$ is the subgroup
\[
	C_G(g)=\{x\in G:xg=gx\}. 
\]
To compute conjugacy classes we have the following functions:
\lstinline{ConjugacyClasses} and \lstinline{ConjugacyClass}. The centralizer
can be computed with \lstinline{Centralizer}.  

\begin{example}
Let us check that $\Sym_3$ contains three conjugacy classes with
representatives $\id$, $(12)$ and $(123)$, so that 
\[(12)^{\Sym_3}=\{(12),(13),(23)\}, \qquad (123)^{\Sym_3}=\{(123),(132)\}.\]
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> ConjugacyClasses(S3);
[ ()^G, (1,2)^G, (1,2,3)^G ]
gap> Elements(ConjugacyClass(S3, (1,2)));
[ (2,3), (1,2), (1,3) ]
gap> Elements(ConjugacyClass(S3, (1,2,3)));
[ (1,2,3), (1,3,2) ]
\end{lstlisting}
Let us check that $C_{\Sym_3}\left( (123) \right)=\{\id,(123),(132)\}$: 
\begin{lstlisting}
gap> Elements(Centralizer(S3, (1,2,3)));
[ (), (1,2,3), (1,3,2) ]
\end{lstlisting}
\end{example}

\begin{example}
\label{exa:Representative}
In this example we use the function \lstinline{Representative} to construct a
list of representatives of conjugacy classes of $\Alt_4$:
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> List(ConjugacyClasses(A4), Representative);
[ (), (1,2)(3,4), (1,2,3), (1,2,4) ]
\end{lstlisting}
\end{example}

With the function \lstinline{IsConjugate} we can check whether two elements are
conjugate.  If two elements $g$ and $h$ are conjugate, we want to find an
element $x$ such that $g=x^{-1}hx$. For that purpose we use
\lstinline{RepresentativeAction}.

\begin{example}
\label{exa:RepresentativeAction}
Let us check that $(123)$ and $(132)=(123)^2$ are not conjugate in $\Alt_4$: 
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> g := (1,2,3);;
gap> IsConjugate(A4, g, g^2);
false
\end{lstlisting}
Now we check that $(123)$ and $(134)$ are conjugate in $\Alt_4$. We also find an element 
$x=(234)$ such that $(134)=x^{-1}(123)x$:
\begin{lstlisting}
gap> h := (1,3,4);;
gap> IsConjugate(A4, g, h);
true
gap> x := RepresentativeAction(A4, g, h);
(2,3,4)
gap> x^(-1)*g*x=h;
true
\end{lstlisting}
\end{example}

%%FIXME: remark: puede dar distinto porque se usa representative (action)

\begin{example}
It is well-known that the converse of Lagrange theorem does not hold.  The
following example is based on~\cite{MR1573427}.  
Let us show that $\Alt_4$ has no subgroups of order six. 
A naive idea to prove that $\Alt_4$ has no subgroups of order six is to study
all the $\binom{12}{6}=924$ subsets of $\Alt_4$ of size six and check that none
of these subsets is a group: 
\begin{lstlisting}
gap> A4 := AlternatingGroup(4);;
gap> k := 0;;
gap> for x in Combinations(Elements(A4), 6) do
> if Size(Subgroup(A4, x))=Size(x) then
> k := k+1;
> fi;
> od;
gap> k;
0
\end{lstlisting}
This is an equivalent way of doing the same thing:
\begin{lstlisting}
gap> ForAny(Combinations(Elements(A4), 6),\
> x->Size(Subgroup(A4, x))=Size(x));
false
\end{lstlisting}

Now we use a similar idea. We use that every subgroup of order six contains
exactly five elements besides the unit 1.  So we see that none of the $\binom{11}{5}=462$
subsets of $\Alt_4$ with five elements generates a subgroup of order six.  In
the following code we do not use \lstinline{Combinations}. Combinations will be
generated by using an iterator.
\begin{lstlisting}
gap> k := 0;;
gap> for t in IteratorOfCombinations(\
> Filtered(A4, x->not x = ()), 5) do
> if Size(Subgroup(A4, t))=Size(t)+1 then
> k := k+1;
> fi;
> od;
gap> k;
0
\end{lstlisting}

Here we have another idea: if $\Alt_4$ has a subgroup of order six, then the
index of this subgroup in $\Alt_4$ is two.  With
\lstinline{SubgroupsOfIndexTwo} we check that $\Alt_4$ has no subgroups of
index two:
\begin{lstlisting}
gap> SubgroupsOfIndexTwo(A4);
[  ]
\end{lstlisting}

Of course we can construct all subgroups and check that there are no subgroups
of order six: 
\begin{lstlisting}
gap> List(AllSubgroups(A4), Order);
[ 1, 2, 2, 2, 3, 3, 3, 3, 4, 12 ]
gap> 6 in last;
false
\end{lstlisting}
Moreover, it is enough to construct all conjugacy classes of subgroups:
\begin{lstlisting}
gap> c := ConjugacyClassesSubgroups(A4);;
gap> List(c, x->Order(Representative(x)));
[ 1, 2, 3, 4, 12 ]
gap> 6 in last;
false
\end{lstlisting}

Another approach is to use conjugacy classes of elements in $\Alt_4$. Indeed, the conjugacy classes of $\Alt_4$ are:
\begin{align*}
	&\{\id\},
	&&\{(243), (123), (134), (142)\},\\
	&\{(12)(34),(13)(24),(14)(23)\},
	&&\{(234),(124), (132), (143)\}.
\end{align*}
This is how we construct the conjugacy classes of $\Alt_4$:
\begin{lstlisting}
gap> ConjugacyClasses(A4);
[ ()^G, (1,2)(3,4)^G, (1,2,3)^G, (1,2,4)^G ]
gap> Elements(ConjugacyClass(A4, ()));
[ () ]
gap> Elements(ConjugacyClass(A4, (1,2)(3,4)));
[ (1,2)(3,4), (1,3)(2,4), (1,4)(2,3) ]
gap> Elements(ConjugacyClass(A4, (1,2,3)));
[ (2,4,3), (1,2,3), (1,3,4), (1,4,2) ]
gap> Elements(ConjugacyClass(A4, (1,2,4)));
[ (2,3,4), (1,2,4), (1,3,2), (1,4,3) ]
\end{lstlisting}
Assume that $\Alt_4$ has a subgroup $K$ of order six. Then $K$ has index two in
$\Alt_4$ and hence it is normal in $\Alt_4$.  This means that $K$ is a union of
conjugacy classes of $\Alt_4$ and that $\{1\}\subseteq K$. This is a
contradiction!

Let us now use the commutator to prove that $\Alt_4$ has no subgroups of order
six.  If there exists a subgroup $K$ of order six, then $K$ is normal in
$\Alt_4$ and the quotient $\Alt_4/K$ is cyclic of order two.  This implies that 
\[
[\Alt_4,\Alt_4]=\{\id,(12)(34),(13)(24),(14)(23)\},
\]
is contained in $K$, a contradiction since $4$ does not divide $6$: 
\begin{lstlisting}
gap> DerivedSubgroup(A4);
Group([ (1,4)(2,3), (1,3)(2,4) ])
gap> Elements(last);
[ (), (1,2)(3,4), (1,3)(2,4), (1,4)(2,3) ]
\end{lstlisting}

One more variation. If $K$ is a subgroup of $\Alt_4$ of order six, then there
are two possibilities: either $K\simeq\Sym_3$ or $K\simeq C_6$. The
group $\Alt_4$ has no elements of order six:
\begin{lstlisting}
gap> Filtered(A4, x->Order(x)=6);
[  ]
\end{lstlisting}
Then $K\simeq\Sym_3$ and hence $K$ contains three elements of order two. Thus 
\[
	\{\id,(12)(34),(13)(24),(14)(23)\}.
\]
Do you have another proof of the fact that $\Alt_4$ has no subgroups of order six?
\end{example}

\begin{example}
If $G$ is a finite abelian group, the structure theorem states that there exist
$n_1,\dots,n_k\in\N$ such that $G\simeq C_{n_1}\times\cdots\times C_{n_k}$.  To
construct finite abelian groups one uses \lstinline{AbelianGroup}. 
Let us construct $C_2\times C_3$ and check that this group is isomorphic to
$C_6$. For that purpose, we see that $C_2\times C_3$ contains an element of
order six:
\begin{lstlisting}
gap> C2xC3 := AbelianGroup(IsPermGroup, [2, 3]);;
gap> First(C2xC3, x->Order(x)=6);
(1,2)(3,4,5)
\end{lstlisting}
\end{example}

To study isomorphisms between finite group one uses
\lstinline{IsomorphismGroups}. This function returns \lstinline{fail} if the
groups are not isomorphic or some isomorphism otherwise. 

\begin{example}
Let us construt a group $G$ such that $G=\langle a\rangle\times\langle
b\rangle$ with $C_4\simeq\langle a\rangle$ and $C_2\simeq\langle b\rangle$. We
also prove that 
\begin{align*}
\langle a^2\rangle\simeq\langle b\rangle,
&&
G/\langle a^2\rangle\not\simeq G/\langle b\rangle.
\end{align*}
Here is the code:
\begin{lstlisting}
gap> G := AbelianGroup(IsPermGroup, [4,2]);
Group([ (1,2,3,4), (5,6) ])
gap> K := Subgroup(G, [(5,6)]);;
gap> L := Subgroup(G, [(1,2,3,4)^2]);;
gap> IsomorphismGroups(K, L);
[ (5,6) ] -> [ (1,3)(2,4) ]
gap> IsomorphismGroups(G/K,G/L);
fail
\end{lstlisting}
%FIXME
One can show that:
\begin{align*}
\langle a^2\rangle\simeq\langle b\rangle\simeq C_2,
&&
G/\langle a^2\rangle\simeq C_4,
&&
G/\langle b\rangle\simeq C_2\times C_2.
\end{align*}
\end{example}

We can also work with classic groups. Use 
\begin{lstlisting}
gap> ?classical groups
\end{lstlisting}
to get more information.

\begin{example}
	One can use the function \lstinline{GL} to construct the groups
	$\GL(n,\Z)$, $\GL(n,\Z/m)$ and $\GL(n,\F_q)$: 
\begin{lstlisting}
gap> Order(GL(2,Integers));
infinity
gap> Order(GL(2,ZmodnZ(4)));
96
gap> Order(GL(2,GF(4)));
180
gap> Order(GL(3,GF(4)));
181440
\end{lstlisting}
Similarly, with \lstinline{SL} one constructs 
$\SL(n,\Z)$, $\SL(n,\Z/m)$ and 
$\SL(n,\F_q)$:
\begin{lstlisting}
gap> Order(SL(2,GF(3)));                                                  
24
gap> Order(SL(2,Integers));
infinity
gap> Order(SL(2,ZmodnZ(4)));
48
gap> Order(SL(2,GF(4)));
60
\end{lstlisting}
\end{example}

It is known that the commutator of a finite group is not always equal to the
set of commutators.  The following example is based on~\cite{MR0075938}. 

\begin{example}
Let $G$ be the
subgroup of $\Sym_{16}$ generated by the permutations
\begin{align*}
&a = (13)(24),&&
b = (57)(6,8),\\
&c = (911)(10,12),&&
d = (13,15)(14,16),\\
&e = (13)(5,7)(9,11),&&
f = (12)(3,4)(13,15),\\
&g = (56)(7,8)(13,14)(15,16),&&
h = (9\;10)(11\;12).
\end{align*}
We show that $[G,G]$ has order $16$: 
\begin{lstlisting}
gap> a := (1,3)(2,4);;
gap> b := (5,7)(6,8);;
gap> c := (9,11)(10,12);;
gap> d := (13,15)(14,16);;
gap> e := (1,3)(5,7)(9,11);;
gap> f := (1,2)(3,4)(13,15);;
gap> g := (5,6)(7,8)(13,14)(15,16);;
gap> h := (9,10)(11,12);;
gap> G := Group([a,b,c,d,e,f,g,h]);;
gap> D := DerivedSubgroup(G);;
gap> Size(D);
16
\end{lstlisting}
We now show that the set of commutators 
has $15$ elements. In particular, we show that 
$cd\in[G,G]$ and that $cd$ is not a commutator: 
\begin{lstlisting}
gap> Size(Set(List(Cartesian(G,G), Comm)));
15
gap> c*d in Difference(D,\
> Set(List(Cartesian(G,G), Comm)));
true
\end{lstlisting}
\end{example}

%%\begin{example}
%%Sabemos que $\Alt_4$ es un grupo de orden doce. Los $3$-subgrupos de Sylow de
%%$\Alt_4$ son isomorfos a $C_3$ y hay cuatro: son los subgrupos $\langle
%%(243)\rangle$, $\langle (123)\rangle$, $\langle (142)\rangle$ y $\langle
%%(134)\rangle$. 
%%
%%\begin{lstlisting}
%%gap> A4 := AlternatingGroup(4);;                                      
%%gap> P := SylowSubgroup(A4, 3);;
%%gap> StructureDescription(P);
%%"C3"
%%gap> ConjugacyClassSubgroups(A4, P);
%%Group( [ (1,2,3) ] )^G
%%gap> Size(last);
%%4
%%gap> Elements(ConjugacyClassSubgroups(A4, P));
%%[ Group([ (2,4,3) ]), Group([ (1,2,3) ]), 
%%  Group([ (1,4,2) ]), Group([ (1,3,4) ]) ]
%%gap> Index(A4, Normalizer(A4, P));
%%4	
%%\end{lstlisting}
%%\end{example}

\section{Homomorphisms}

\index{Group!homomorphism}
Now we work with group homomorphisms. There are several ways to construct group homomorphisms. The function
\lstinline{GroupHomomorphismByImages} returns the group homomorphism
constructed from a list of generators of the domain and the value of the image
at each generator. Properties of group homomorphisms can be studied with
\lstinline{Image}, \lstinline{IsInjective}, \lstinline{IsSurjective},
\lstinline{Kernel}, \lstinline{PreImage}, \lstinline{PreImages}, etc. 

\begin{example}
The map $\Sym_4\to\Sym_3$
that maps each transposition of $\Sym_4$ into $(12)$ extends to a group homomorphism $f$. 
This homomorphism $f$ is not injective ($\ker f$ has twelve elements) and it is
not surjective (for example $(123)\not\in f(\Sym_4)$):
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> S3 := SymmetricGroup(3);;
gap> f := GroupHomomorphismByImages(S4, S3,\
> [(1,2),(1,3),(1,4),(2,3),(2,4),(3,4)],\
> [(1,2),(1,2),(1,2),(1,2),(1,2),(1,2)]);
[ (1,2), (1,3), (1,4), (2,3), (2,4), (3,4) ] -> 
[ (1,2), (1,2), (1,2), (1,2), (1,2), (1,2) ]
gap> Size(Kernel(f));
12
gap> IsInjective(f);
false
gap> Size(Image(f));
2
gap> IsSurjective(f);
false
gap> (1,2,3) in Image(f);
false
\end{lstlisting}
\end{example}

If $K$ is a normal subgroup of $G$, the canonical map $G\to G/K$ can be
constructed with the function \lstinline{NaturalHomomorphismByNormalSubgroup}.

\begin{example}
Let us construct $C_{12}=\langle g:g^{12}=1\rangle$ as a group of permutations,
the subgroup $K=\langle g^6\rangle$ and the quotient  $C_{12}/K$. 
We also construct the canonical (surjective) map $C_{12}\to C_{12}/K$: 
\begin{lstlisting}
gap> g := (1,2,3,4,5,6,7,8,9,10,11,12);;
gap> C12 := Group(g);;
gap> K := Subgroup(C12, [g^6]);;
gap> f := NaturalHomomorphismByNormalSubgroup(C12, K);
[ (1,2,3,4,5,6,7,8,9,10,11,12) ] -> [ f1 ]		
gap> Image(f, g^6);
<identity> of ...
\end{lstlisting}
%FIXME
%Podemos verificar fácilmente que los subgrupos de $C_{12}$ que contienen a $K$
%están en correspondencia biyectiva con los subgrupos de $C_{12}/K$: 
%\begin{lstlisting}
%gap> for subgroup in AllSubgroups(C12) do
%> if IsSubgroup(subgroup, K) then
%> Print(Image(f, subgroup), "\n");
%> fi;
%> od;
%Group( [ <identity> of ... ] )
%Group( [ f1*f2^2 ] )
%Group( [ f2^2 ] )
%Group( [ f1 ] )
%gap> AllSubgroups(Image(f));
%[ Group([ <identity> of ... ]), 
%  Group([ f1*f2^2 ]), Group([ f2 ]), 
%  Group([ f1, f2 ]) ]
%\end{lstlisting}
\end{example}

\index{Automorphism group}
\index{Inner automorphisms}
The function \lstinline{AutomorphismGroup} computes the automorphism group of a
finite group. If $G$ is a group, the automorphisms of $G$ of the form $x\mapsto
g^{-1}xg$, where $g\in G$, are the \textbf{inner automorphisms} of $G$.  The
function \lstinline{IsInnerAutomorphism} checks whether a given automorphism is
inner. 

\begin{example}
Let us check that $\Aut(\Sym_3)$ is a non-abelian group of six elements:
\begin{lstlisting}
gap> aut := AutomorphismGroup(SymmetricGroup(3));
<group of size 6 with 2 generators>
gap> IsAbelian(aut);
false
\end{lstlisting}
\end{example}

\begin{example}
Let us prove that for $n\in\{2,3,4,5\}$ each automorphism of $\Sym_n$ is inner. Here is the code:
\begin{lstlisting}
gap> for n in [2..5] do
> G := SymmetricGroup(n);;
> if ForAll(AutomorphismGroup(G),\
> x->IsInnerAutomorphism(x)) then
> Print("Each automorfism of S",\
> n, " is inner.\n");
> fi;
> od;
Each automorphism of S2 is inner.
Each automorphism of S3 is inner.
Each automorphism of S4 is inner.
Each automorphism of S5 is inner.
\end{lstlisting}
It is known that in $\Sym_6$ there are non-inner automorphisms:
% FIXME!
\begin{lstlisting}
gap> S6 := SymmetricGroup(6);;
gap> f := First(AutomorphismGroup(S6),\
> x->IsInnerAutomorphism(x)=false);
[ (1,2,3,4,5,6), (1,2) ] -> [ (2,3)(4,6,5), (1,2)(3,5)(4,6) ]
\end{lstlisting}
The automorphism of $\Sym_6$ such that 
$(123456)\mapsto (23)(465)$ and $(12)\mapsto(12)(35)(46)$
is not inner. 
Let us compute this homomorphism in the transpositions: 
\begin{lstlisting}
gap> for t in ConjugacyClass(S6, (1,2)) do
> Print("f(", t, ")=", Image(f,t), "\n"); 
> od;
f((1,2))=(1,2)(3,5)(4,6)
f((1,3))=(1,6)(2,5)(3,4)
f((1,4))=(1,4)(2,3)(5,6)
f((1,5))=(1,5)(2,4)(3,6)
f((1,6))=(1,3)(2,6)(4,5)
f((2,3))=(1,3)(2,4)(5,6)
f((2,4))=(1,5)(2,6)(3,4)
f((2,5))=(1,6)(2,3)(4,5)
f((2,6))=(1,4)(2,5)(3,6)
f((3,4))=(1,2)(3,6)(4,5)
f((3,5))=(1,4)(2,6)(3,5)
f((3,6))=(1,5)(2,3)(4,6)
f((4,5))=(1,3)(2,5)(4,6)
f((4,6))=(1,6)(2,4)(3,5)
f((5,6))=(1,2)(3,4)(5,6)
\end{lstlisting}
\end{example}

With \lstinline{AllHomomorphisms} one constructs the set of group homomorphisms
between two given groups.  Similarly, one uses \lstinline{AllEndomorphisms} to
compute endomorphisms.

\begin{example}
We prove that there are ten endomorphisms of $\Sym_3$:
\begin{lstlisting}
gap> S3 := SymmetricGroup(3);;
gap> Size(AllEndomorphisms(S3));
10
\end{lstlisting}
\end{example}

\begin{example}
The center of $C_2\times\Sym_3$ is not stable under endomorphisms of 
$C_2\times\Sym_3$. We see that 
$Z(C_2\times\Sym_3)=\{\id,(12)\}$ and 
that there exists at least one endomorphism of $C_2\times\Sym_3$ that permutes 
the non-trivial element of the center:
\begin{lstlisting}
gap> C2 := CyclicGroup(IsPermGroup, 2);;
gap> S3 := SymmetricGroup(3);;
gap> C2xS3 := DirectProduct(C2, S3);;
gap> Center(C2xS3);
Group([ (1,2) ])
gap> ForAll(AllEndomorphisms(C2xS3),\
> f->Image(f,(1,2)) in [(), (1,2)]);
false
\end{lstlisting}
\end{example}

With \lstinline{InnerAutomorphismsAutomorphismGroup} one constructs the inner
automorphism group of a given group.

\begin{example}
Let us check that $\Aut(\Sym_6)/\Inn(\Sym_6)\simeq C_2$: 
\begin{lstlisting}
gap> S6 := SymmetricGroup(6);;
gap> A := AutomorphismGroup(S6);;
gap> Size(A);
1440
gap> I := InnerAutomorphismsAutomorphismGroup(A);;
gap> Order(A/I);
2
\end{lstlisting}
\end{example}

\section{SmallGroups}
\label{SmallGroups}

\GAP~contains a database with all groups of certain small orders. The groups
are sorted by their orders and they are listed up to isomorphism. This database
is part of a library named \lstinline{SmallGroups}.  It contains the following
groups: a) those of order $\leq2000$ except order $1024$, b) those of cube-free
order $\leq 50000$, c) those of order $p^7$ for $p\in\{3,5,7,11\}$, d) those of
order $p^n$ for $n\leq 6$ and all primes $p$, e) those of order $q^np$ for
$q^n$ dividing $2^8$, $3^6$, $5^5$ or $7^4$ and all primes $p$ with $p\ne q$,
f) those of square-free order, and g) those whose order factorizes into at most
three primes.  

The library was written by H, Besche, B. Eick and E. O'Brien. 

As one can image, this library is a very useful tool when one needs to look for
examples and counterexamples.  

\begin{example}
Let us see what \lstinline{SmallGroups} knows about groups of order twelve:
\begin{lstlisting}
gap> SmallGroupsInformation(12);                                      

  There are 5 groups of order 12.
    1 is of type 6.2.
    2 is of type c12.
    3 is of type A4.
    4 is of type D12.
    5 is of type 2^2x3.

  The groups whose order factorises in at most 3 primes 
  have been classified by O. Hoelder. This classification is 
  used in the SmallGroups library. 

  This size belongs to layer 1 of the SmallGroups library. 
  IdSmallGroup is available for this size. 
\end{lstlisting}
\end{example}

Some of the examples of this section are from \cite{MR605275}. 

\begin{example}
	Let us check that there exist non-abelian groups of odd order and that the
	smallest of this group has order $21$:
\begin{lstlisting}
gap> First(AllSmallGroups(Size, [1, 3..21]),\
> x->not IsAbelian(x));;
gap> Size(last);
21
\end{lstlisting}
\end{example}

\begin{example}
In one line we check that there are no simple groups of order $84$. We use 
the filter \lstinline{IsSimple} with the function 
\lstinline{AllSmallGroups}:
\begin{lstlisting}
gap> AllSmallGroups(Size, 84, IsSimple, true);
[  ]
\end{lstlisting}
\end{example}

With the function \lstinline{StructureDescription} one explores the structure
of a given group. The function returns a short string which gives some insight
into the structure of the group.  

\begin{example}
Let us see how the groups of order twelve look like:
\begin{lstlisting}
gap> List(AllSmallGroups(Size, 12),\
> StructureDescription);
[ "C3 : C4", "C12", "A4", "D12", "C6 x C2" ]
\end{lstlisting}
The group \lstinline{C3 : C4} denotes the semidirect product 
$C_3\rtimes C_4$. 
\end{example}

\begin{example}
Let us explore more group homomorphisms. We know that the symmetric group $\Sym_4$
is generated by the transpositions $(12)$, $(23)$ and $(34)$. We let $f$ be the
group homomorphism $\Sym_4\to\Sym_3$ given by $(12)\mapsto (12)$, $(23)\mapsto
(23)$ and $(34)\mapsto(12)$. 
Let us perform some calculations related to this
group homomorphism:
\begin{lstlisting}
gap> S4 := SymmetricGroup(4);;
gap> S3 := SymmetricGroup(3);;
gap> f := GroupHomomorphismByImages(S4, S3, [(1,2),(2,3),(3,4)],\
>  [(1,2),(2,3),(1,2)]);;
gap> K := Kernel(f);;
gap> StructureDescription(K);
"C2 x C2"
gap> IsInjective(f);
false
gap> StructureDescription(S4/K);
"S3"
gap> StructureDescription(Image(f));
"S3"
gap> IsSurjective(f);
true
\end{lstlisting}
\end{example}

It is important to remark that the string
returned by \lstinline{StructureDescription} is not an isomorphism invariant:
non-isomorphic groups can have the same string value and two isomorphic groups
in different representations can produce different strings. 

\begin{example}
There are two groups of order $20$ that can be written as 
a semidirect product $C_5\rtimes C_4$. 
\lstinline{StructureDescription} will not distinguish such groups:
\begin{lstlisting}
gap> List(AllSmallGroups(Size, 20),\
> StructureDescription);
[ "C5 : C4", "C20", "C5 : C4", "D20", "C10 x C2" ]
\end{lstlisting}
To identify groups in the database \lstinline{SmallGroups} one uses the
function \lstinline{IdGroup}. Here we have some examples:
\begin{lstlisting}
gap> IdGroup(SymmetricGroup(3));
[ 6, 1 ]
gap> IdGroup(SymmetricGroup(4));
[ 24, 12 ]
gap> IdGroup(AlternatingGroup(4));
[ 12, 3 ]
gap> IdGroup(DihedralGroup(8));
[ 8, 3 ]
gap> IdGroup(QuaternionGroup(8));
[ 8, 4 ]
\end{lstlisting}
\end{example}

\begin{example}
	\index{Lam, T.}
	\index{Leep, D.}
	In~\cite{MR1240362}, T. Lam and D. Leep proved that each index-two subgroup of
	$\Aut(\Sym_6)$ is isomorphic either to $\Sym_6$, $\PGL_2(9)$ or to the Mathieu group
	$M_{10}$. Let us check this claim using the function~\lstinline{IdGroup}:
	\begin{lstlisting}
gap> autS6 := AutomorphismGroup(SymmetricGroup(6));;
gap> lst := SubgroupsOfIndexTwo(autS6);;
gap> List(lst, IdGroup);
[ [ 720, 764 ], [ 720, 763 ], [ 720, 765 ] ]
gap> IdGroup(PGL(2,9));
[ 720, 764 ]
gap> IdGroup(MathieuGroup(10));
[ 720, 765 ]
gap> IdGroup(SymmetricGroup(6));
[ 720, 763 ]
\end{lstlisting}
\end{example}

\begin{example}
\label{exa:Guralnick:96}
Now we prove a theorem of R. Guralnick \cite{MR673806}. The theorem states
that the smallest finite group $G$ such that $\{[x,y]:x,y\in G\}\ne[G,G]$ has
order $96$.
\begin{lstlisting}
gap> G := First(AllSmallGroups(Size, [1..100]),\
> x->Order(DerivedSubgroup(x))<>Size(\
> Set(List(Cartesian(x,x), Comm))));;
gap> Order(G);
96
gap> IdGroup(G);
[ 96, 3 ]
\end{lstlisting}
With \lstinline{IdGroup} (or with \lstinline{IsomorphismGroups}) we check that 
\[
G\simeq\langle (135)(246)(7\,11\,9)(8\,12\,10),(394\,10)(58)(67)(11\,12)\rangle.
\]
How did we find this isomorphism? %the isomorphism 
%\[
%G\simeq\langle (135)(246)(7\,11\,9)(8\,12\,10),(394\,10)(58)(67)(11\,12)\rangle?
%\]
We have constructed our group $G$.  Then we use the function \lstinline{IsomorphismPermGroup} to
construct a faithful representation of $G$ as a permutation group. With
\lstinline{SmallerDegreePermutationRepresentation} we construct (if possible) an isomorphic
permutation group of smaller degree. Be aware that this new degree may not be minimal.
After some attempts, we obtain 
an isomorphic copy of $G$ inside $\Sym_{12}$. To construct a set of generators we then use 
\lstinline{SmallGeneratingSet}. Again, be aware that this set may not be minimal.
%Una vez que tenemos el grupo $G$ de
%orden $96$, la función \lstinline{IsomorphismPermGroup} nos permite construir
%una representación fiel de $G$ en algún grupo de permutaciones. La imagen del
%morfismo obtenido con \lstinline{IsomorphismPermGroup} se calcula con
%\lstinline{Image} y nos da un grupo de permutaciones isomorfo a $G$.  Este
%grupo bien podría tener grado grande, pero, en nuestro grupo de permutaciones,
%puede utilizarse la función \lstinline{SmallerDegreePermutationRepresentation}
%para encontrar una representación por permutaciones de menor grado (no
%necesariamente minimal). Después de algunos intentos, este procedimiento da 
%como resultado una presentación de $G$ como subgrupo de $\Sym_{12}$. Un
%conjunto (no necesariamente minimal) de generadores de un grupo se construye
%con la función \lstinline{SmallGeneratingSet}. 
\end{example}




For a finite group $G$ let $\cs(G)$ denote the set of sizes of the conjugacy
classes of $G$, that is
\[
\cs(G)\coloneqq \{|g^G|:g\in G\}.
\]  Let us write a function to compute $\cs$. With this function
we show that
  \[
    \cs(\Sym_3)=\{1,2,3\},
    \quad
    \cs(\Alt_5)=\{1,12,15,20\},
    \quad
    \cs(\SL_2(3))=\{1,4,6\}.
  \]
Here is the code:
\begin{lstlisting}
gap> cs := function(group)
> return Set(List(ConjugacyClasses(group), Size));
> end;
function( group ) ... end
gap> cs(SymmetricGroup(3));
[ 1, 2, 3 ]
gap> cs(AlternatingGroup(5));
[ 1, 12, 15, 20 ]
gap> cs(SL(2,3));
[ 1, 4, 6 ]
\end{lstlisting}

We will write $G_{n,k}$ to denote the $k$-th group of size $n$ in the database,
thus $G_{n,k}$ is a group with \lstinline{IdGroup} equal to \lstinline{[ n, k ]}.

\begin{example}
  \index{Navarro, G.}
  \label{example:Navarro}
	This example is taken from~\cite[Theorem A]{MR3210919} and
	answers a question made by R. Brauer~\cite[Question 2(ii)]{MR2875589}.
  G. Navarro proved that there exist finite groups $G$ and $H$ such that $G$ is
  solvable, $H$ is not solvable and $\cs(G)=\cs(H)$.

  Let $G=G_{240,13}\times G_{960,1019}$ and
  $H=G_{960,239}\times G_{480,959}$.  Then $G$ is solvable and $H$ is not. Moreover, 
  $\cs(G)=\cs(H)$ as the following code shows:
\begin{lstlisting}
gap> U := SmallGroup(960,239);;
gap> V := SmallGroup(480,959);;
gap> L := SmallGroup(960,1019);;
gap> K := SmallGroup(240,13);;
gap> UxV := DirectProduct(U,V);;
gap> KxL := DirectProduct(K,L);;
gap> IsSolvable(UxV);
false
gap> IsSolvable(KxL);
true
\end{lstlisting}
One could try to compute $\cs(U\times V)$ directly. However, this calculation
seems to be hard. The trick is to use that
$\cs(U\times V)=\{nm:n\in\cs(U),m\in\cs(V)\}$.

Here is the code:
\begin{lstlisting}
gap> cs(KxL)=Set(List(Cartesian(cs(U),cs(V)), x->x[1]*x[2]));
true
\end{lstlisting}
\end{example}

\begin{example}
This example appeared in~\cite{MR3210919}. It answers 
	another R. Brauer question~\cite[Question 4(ii)]{MR2875589}.
  G. Navarro proved that there exist finite groups $G$ and $H$ such that $G$ is
  nilpotent, $Z(H)=1$ and $\cs(G)=\cs(H)$.

  The groups are $G=\D_8\times G_{243,26}$ and
  $H=G_{486,36}$. Here is the code:
\begin{lstlisting}
gap> K := DihedralGroup(8);;
gap> L := SmallGroup(243,26);;
gap> H := SmallGroup(486,36);;
gap> IsTrivial(Center(H));
true
gap> G := DirectProduct(K,L);;
gap> cs(G)=cs(H);
true
gap> IsNilpotent(G);
true
\end{lstlisting}
\end{example}
	
\section{Finitely presented groups}

\index{Free group}
Let us start working with free groups.  The function \lstinline{FreeGroup}
construct the free group in a finite number of generators. 

\begin{example}
We create the free group $F_2$ in two generators and we create some random
elements with the function 
\lstinline{Random}:
\begin{lstlisting}
gap> f := FreeGroup(2);
<free group on the generators [ f1, f2 ]>
gap> f.1^2;
f1^2
gap> f.1^2*f.1;
f1^3
gap> f.1*f.1^(-1);
<identity ...>
gap> Random(f);
f1^-3
\end{lstlisting}
\end{example}

\begin{example}
	The function \lstinline{Length} can be used to compute the length of words in a
	free group. 
	In this example we create $10000$ random elements in $F_2$ and compute their lengths.
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> Collected(List(List([1..10000], x->Random(f)), Length));
[ [ 0, 2270 ], [ 1, 1044 ], [ 2, 1113 ], 
  [ 3, 986 ], [ 4, 874 ], [ 5, 737 ], 
  [ 6, 642 ], [ 7, 500 ], [ 8, 432 ], 
  [ 9, 329 ], [ 10, 248 ], [ 11, 189 ], 
  [ 12, 152 ], [ 13, 119 ], [ 14, 93 ], 
  [ 15, 68 ], [ 16, 57 ], [ 17, 34 ], 
  [ 18, 30 ], [ 19, 23 ], [ 20, 19 ], 
  [ 21, 16 ], [ 22, 8 ], [ 23, 3 ], [ 24, 4 ], 
  [ 25, 4 ], [ 26, 2 ], [ 27, 2 ], [ 28, 1 ], 
  [ 31, 1 ] ]
\end{lstlisting}
\end{example}

Some of the functions we used before can also be used in free groups. Examples
of these functions are \lstinline{Normalizer},
\lstinline{RepresentativeAction}, \lstinline{IsConjugate},
\lstinline{Intersection}, \lstinline{IsSubgroup}, \lstinline{Subgroup}. 

\begin{example}
Here we perform some elementary calculations in $F_2$, the free group with
generators $a$ and $b$. We also compute the automorphism group of $F_2$. 
\begin{lstlisting}
gap> f := FreeGroup("a", "b");;
gap> a := f.1;;
gap> b := f.2;;
gap> Random(f);
b^-1*a^-5
gap> Centralizer(f, a);
Group([ a ])
gap> Index(f, Centralizer(f, a));
infinity
gap> Subgroup(f, [a,b]);
Group([ a, b ])
gap> Order(Subgroup(f, [a,b]));
infinity
gap> AutomorphismGroup(f);
<group of size infinity with 3 generators>
gap> GeneratorsOfGroup(AutomorphismGroup(f));
[ [ a, b ] -> [ a^-1, b ], 
  [ a, b ] -> [ b, a ], 
  [ a, b ] -> [ a*b, b ] ]
\end{lstlisting}
We now check that the subgroup $S$ generated by $a^2$, $b$ and $aba^{-1}$ has
index two in $F_2$. We compute $\Aut(S)$ and check that it is not a free
group: 
\begin{lstlisting}
gap> S := Subgroup(f, [a^2, b, a*b*a^(-1)]);
Group([ a^2, b, a*b*a^-1 ])
gap> Index(f, S);
2
gap> A := AutomorphismGroup(S);
<group of size infinity with 3 generators>
gap> IsFreeGroup(A);
false
\end{lstlisting}
\end{example}

\begin{example}
\label{xca:Coxeter}
\index{Coxeter, H.}
Let $n\geq3$ and $p\geq2$ be integers. An amazing result of
Coxeter~\cite{MR1330458} states that the group generated by
$\sigma_1,\dots,\sigma_{n-1}$ and 
\begin{align*}
    &\sigma_i\sigma_{i+1}\sigma_i=\sigma_{i+1}\sigma_i\sigma_{i+1} && \text{ if $i\in\{1,\dots,n-2\}$},\\
    &\sigma_i\sigma_j=\sigma_j\sigma_i && \text{ if $|i-j|\geq 2$},\\
    &\sigma_i^p=1 && \text{ if $i\in\{1,\dots,n-1\}$}\rangle,
\end{align*}
is finite if and only if $(p-2)(n-2)<4$.  

We study the case $n=3$. Let 
\[
G=\langle a,b:aba=bab,\,a^p=b^p=1\rangle.
\]
We claim that 
\[
G\simeq\begin{cases}
    \Sym_3 & \text{if $p=2$},\\
    \SL_2(3) & \text{if $p=3$},\\
    \SL_2(3)\rtimes C_4 & \text{if $p=4$},\\
    \SL_2(3)\times C_5 & \text{if $p=5$}:
\end{cases}
\]
Here is the proof:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> a := f.1;;
gap> b := f.2;;
gap> p := 2;;
gap> while p-2<4 do
> G := f/[a*b*a*Inverse(b*a*b), a^p, b^p];;
> Display(StructureDescription(G));
> p := p+1;
> od;
S3
SL(2,3)
SL(2,3) : C4
C5 x SL(2,5)    
\end{lstlisting}
\end{example}

\begin{example}
For $l,m,n\in\N$, we define the \textbf{von Dyck group} (or triangular group)
of type $(l,m,n)$ as the group
\[
G(l,m,n)=\langle
a,b:a^l=b^m=(ab)^n=1\rangle.
\]
It is known that $G(l,m,n)$ is finite if and only if 
\[
\frac{1}{l}+\frac{1}{m}+\frac{1}{n}>1.
\]
We claim that 
\[
G(2,3,3)\simeq\Alt_4, \quad
G(2,3,4)\simeq\Sym_4,\quad
G(2,3,5)\simeq\Alt_5. 
\]
Here is the code:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> a := f.1;;
gap> b := f.2;;
gap> StructureDescription(f/[a^2,b^3,(a*b)^3]);
"A4"
gap> StructureDescription(f/[a^2,b^3,(a*b)^4]);
"S4"
gap> StructureDescription(f/[a^2,b^3,(a*b)^5]);
"A5"
\end{lstlisting}
\end{example}

\begin{example}
This example is taken from~\cite{MR1786869}. Let us check that the group
\[
\langle a,b,c:a^3=b^3=c^4=1,\,ac=ca^{-1},\,aba^{-1}=bcb^{-1}\rangle
\]
is trivial. For that purpose we use \lstinline{IsTrivial}:
\begin{lstlisting}
gap> f := FreeGroup(3);;
gap> a := f.1;;
gap> b := f.2;;
gap> c := f.3;;
gap> G := f/[a^3, b^3, c^4, c^(-1)*a*c*a, \
> a*b*a^(-1)*b*c^(-1)*b^(-1)];;
gap> IsTrivial(G);
true
\end{lstlisting}
\end{example}

\begin{example}
In \cite{MR1732210} it is proved that for $n\in\N$, 
\[
\langle a,b:a^{-1}b^na=b^{n+1},\;a=a^{i_1}b^{j_1}a^{i_2}b^{j_2}\cdots a^{i_k}b^{j_k}\rangle,
\]
is trivial if $i_1+i_2+\cdots i_k=0$. As an example let us see that
\[
\langle a,b:a^{-1}b^2a=b^3,\,a=a^{-1}ba\rangle
\]
is the trivial group: 
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> a := f.1;;
gap> b := f.2;;
gap> G := f/[a^(-1)*b^2*a*b^(-3),a*(a^(-1)*b*a)];;
gap> IsTrivial(G);
true
\end{lstlisting}
\end{example}

\index{Burnside group}
For each $n\geq2$ the Burnside group $B(2,n)$ is defined as the group 
\[
        B(2,n)=\langle a,b:w^n=1\text{ for all word $w$ in the letters $a$ and $b$}\rangle. 
\]

\begin{example}
	We prove that the group $B(2,3)$ is a finite group of order $27$. 
	Let $F$ be the free group of rank two. We divide $F$ by the
	normal subgroup generated by $\{w_1^3,\dots,w_{10000}^3\}$, where 
	$w_1,\dots,w_{10000}$ are some randomly chosen words of $F$. The following
	code shows that $B(2,3)$ is finite:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> rels := Set(List([1..10000], x->Random(f)^3));;
gap> G := f/rels;;
gap> Order(G);
27
\end{lstlisting}
\end{example}

%\begin{corollary}
%	The group $B(2,3)$ is isomorphic to the Heisenberg group $H(\Z/3)$.
%	%group generated by $(123)(478)(569)$ and $(245)(367)$. 
%\end{corollary}
%
%\begin{proof}
%	The permutation representation of the finite group constructed in the proof
%	of Theorem~\ref{theorem:B(2,3)} is constructed as follows:
%\begin{lstlisting}
%gap> f := IsomorphismPermGroup(G);;
%gap> Q := Image(f);;
%gap> GeneratorsOfGroup(Q);
%[ (1,2,3)(4,7,8)(5,6,9), (2,4,5)(3,6,7) ]
%\end{lstlisting}
%This finishes the proof.
%\end{proof}

\begin{example}
	It is known that $B(2,4)$ is a finite group. Here we present here a
	computational proof.  We use the same trick as before.  The following code
	shows that $B(2,4)$ is finite and has order $\leq4096$:
\begin{lstlisting}
gap> f := FreeGroup(2);;
gap> rels := Set(List([1..10000], x->Random(f)^4));;
gap> B24 := f/rels;;
gap> Order(B24);
4096
\end{lstlisting}
\end{example}



%\section{Actions}




\section{Problems}


\begin{prob}
Compute the order of the group generated by
\begin{align*}
    \begin{pmatrix}
        1 & 0\\
        0 & 1
    \end{pmatrix},
    &&
    \begin{pmatrix}
        -1 & 0\\
        0 & -1
    \end{pmatrix},
    &&
    \begin{pmatrix}
        -1 & 0\\
        0 & 1
    \end{pmatrix},
    &&
    \begin{pmatrix}
        1 & 0\\
        0 & -1
    \end{pmatrix}.
\end{align*}
Can you recognize this group?
\end{prob}

\begin{prob}
  Contruct the Heisenberg group $H(\Z/3)$.
\end{prob}

\begin{prob}
  Construct the Klein group as a group of permutations.
\end{prob}

\begin{prob}
    Prove that all subgroups of $C_4\times Q_8$ are normal.
\end{prob}

\begin{prob}
    Let $G$ be the set of matrices of the form
    \[
        \begin{pmatrix}
            1&c\\
            0&d
        \end{pmatrix},
        \quad
        c,d\in\F_4,\;c\ne0.
    \]
    Prove that $G$ is a group and compute its order. 
\end{prob}

%\begin{prob}
%    Encuentre todos los subgrupos de $\Sym_4$ que actúan transitivamente en
%    $\{1,2,3,4\}$ y analice la transitividad múltiple.
%\end{prob}

\begin{prob}
	\label{prob:subgroupsA6A7}
	Use the function \lstinline{IsomorphicSubgroups} to prove that
	$\Alt_{6}$ does not contain a subgroup isomorphic to $\Sym_5$ and that
	$\Alt_{7}$ contains a subgroup isomorphic to $\Sym_5$.
\end{prob}

\begin{prob}
Prove that $\Alt_6$ does not contain subgroups of prime index. 
\end{prob}

\begin{prob}
	\label{prob:normal_SL2(3)}
	\textcolor{blue}{
	Prove that $\SL_2(3)$ has a unique normal subgroup of order eight. }
\end{prob}

%\begin{prob}
%	\label{prob:Sylow_SL2(3)}
%	\textcolor{blue}{
%	Prove that $\{I,-I\}$ is the unique Sylow $2$-subgroup of $\SL_2(3)$ and
%	that the quotient group $\SL_2(3)/\{I,-I\}$ has four Sylow $3$-subgroups
%and a unique Sylow $2$-subgroup.}
%\end{prob}

\begin{prob}
	\label{prob:SL2(5)}
	\textcolor{blue}{Find a subgroup of $\SL_2(5)$ isomorphic to $\SL_2(3)$.}
\end{prob}

\begin{prob}
  Use the functions \lstinline{SylowSubgroup} and 
  \lstinline{ConjugacyClassSubgroups} to construct all Sylow subgroups 
  of $\Alt_4$ and $\Sym_4$.
\end{prob}

\begin{prob}
  Prove that $\Sym_5$ has a $2$-Sylow subgroup isomorphic to the dihedral group
  of eight elements. 
%  Prove that $\Sym_6$ has a $2$-Sylow subgroup isomorphic to 
%	Verifique que $\Sym_5$ tiene un $2$-subgrupo de Sylow isomorfo a $\D_8$ y que 
%	$\Sym_6$ tiene un $2$-subgrupo de Sylow isomorfo a $\D_8\times C_2$.
%%gap> SylowSubgroup(SymmetricGroup(5), 2);
%%Group([ (1,2), (3,4), (1,3)(2,4) ])
%%gap> StructureDescription(last);
%%"D8"
%%gap> SylowSubgroup(SymmetricGroup(6), 2);
%%Group([ (1,2), (3,4), (1,3)(2,4), (5,6) ])
%%gap> StructureDescription(last);
%%"C2 x D8"
\end{prob}

\begin{prob}
  Can you recognize the structure of $2$-Sylow subgroups of $\Sym_6$?
\end{prob}

\begin{prob}
  Use the function \lstinline{Normalizer} to compute the number of conjugates
  of $2$-Sylow subgroups of $\Alt_5$. 
\end{prob}

\begin{prob}
    Find all Sylow subgroups of $C_{27}$, $\SL_2(5)$,
    $\Sym_7$, $\Sym_3\times\Alt_4$ and $\Sym_3\times C_{20}$. 
\end{prob}

\begin{prob}
  Compute the conjugacy classes of subgroups of $\Sym_3\times\Sym_3$ and find
  three $p$-Sylow subgroups, say $A,B,C$, such that $A\cap B=1$ and $A\cap
  C\ne1$.
\end{prob}

\begin{prob}
    Prove that the group
    \[
        \left\{\begin{pmatrix}
            1&b\\
            0&d
        \end{pmatrix}
        :b,d\in\F_{19},\;d\ne0
        \right\}
    \]
    is not simple.
\end{prob}

\begin{prob}
    Let $G$ be the group generated by the permutations 
    $(12)(6\,11)(8\,12)(9\,13)$, $(5\,139)(6\,10\,11)(78\,12)$ and 
    $(2,4,3)(5,8,9)(6\,10\,13)(7\,11\,12)$. 
    How many elements of $G$ are commutators?
\end{prob}

\begin{prob}
Prove that $\Alt_4\times C_7$ does not contain subgroups of index two.
\end{prob}

\begin{prob}
  \label{prob:A5:8,15,20,24,30,49}
  Prove that $\Alt_5$ does not contain subgroups of order $8,15,20,24,30,40$. 
\end{prob}

\begin{prob}
    %FIXME: referencia
    %http://ysharifi.wordpress.com/2012/04/21/maximum-order-of-abelian-subgroups-in-a-symmetric-group/
  It is known that an abelian subgroup of $\Sym_n$ has order
  $\leq3^{\lfloor n/3\rfloor}$. How good is this bound? For
  $n\in\{5,6,7,8\}$ find (if possible) an abelian subgroup of $\Sym_n$ of
  order $3^{\lfloor n/3\rfloor}$. 
\end{prob}

\begin{prob}
    Prove that $\SL_2(5)$ does not contain subgroups isomorphic to $\Alt_5$. 
\end{prob}

\begin{prob}
  Prove that for each $d$ that divides $24$ there exists a subgroup of $\Sym_4$ of order $d$.
\end{prob}

\begin{prob} 
  Prove that $\SL_2(3)$ contains a unique element of order two. Prove that
  $\SL_2(3)$ does not have subgroups of order $12$.
\end{prob}

\begin{prob}
  Prove that the derived subgroup of $\SL_2(3)$ is isomorphic to $Q_8$.
\end{prob}

\begin{prob}
  Can you recognize the group $\SL_2(3)/Z(\SL_2(3))$? 
\end{prob}

\begin{prob}
  Are the groups $\Sym_5$ and $\SL_2(5)$ isomorphic?
\end{prob}
%
%%%% Morfismos
%
\begin{prob}
  Let $\langle
  r^4=s^2=1,\,srs=r^{-1}\rangle$ be the dihedral group of eight elements. 
  Find all subgroups containing $\langle 1,r^2\rangle$.
\end{prob}

\begin{prob}
  Find all the group homomorphisms $\Sym_3\to\SL_2(3)$. 
\end{prob}

\begin{prob}
    Are there any surjective homomorphism $\D_{16}\to\D_{8}$? What about
    $\D_{16}\to C_2$?
\end{prob}

\begin{prob}
  \label{prob:Aut(A4)=S4}
   Prove that $\Aut(\Alt_4)\simeq\Sym_4$.
\end{prob}

\begin{prob}
  Prove that $\Aut(\D_8)\simeq\D_8$ and that 
  $\Aut(\D_{16})\not\simeq\D_{16}$.
\end{prob}

\begin{prob}
Compute the order of the group $\Aut(C_{11}\times C_2\times C_3)$. 
\end{prob}

\begin{prob}
  Prove that $\D_{12}\simeq\Sym_3\times C_2$.
\end{prob}

\begin{prob}
  Prove that every group of order $<60$ is solvable.
\end{prob}

\begin{prob}
  Let $G$ be a group of order twelve such that $G\not\simeq\Alt_4$. Prove that
  $G$ contains an element of order six.
\end{prob}

\begin{prob}
  Prove that a group of order $455$ is cyclic.
\end{prob}

%\begin{prob}
%    Determine la cantidad de grupos de Sylow que tienen los grupos no abelianos
%    de orden $34$.
%\end{prob}
%
\begin{prob}
  Let $G$ be a simple group of order $168$. Compute the number of elements of
  order seven of $G$.
\end{prob}

\begin{prob}
  Prove that there are no simple groups of order $2540$ and $9075$.
\end{prob}

\begin{prob}
  Find a group $G$ of order $3^6$ such that 
  $\{[x,y]:x,y\in G\}\ne[G,G]$.
\end{prob}

\begin{prob}
  Find a group $G$ of order $2^7$ such that $\{[x,y]:x,y\in G\}\ne [G,G]$ 
\end{prob}

\begin{prob}
  Prove that a group of order $15$, $35$ or $77$ is cyclic.
\end{prob}

\begin{prob}
  Prove that a simple group of order $60$ is isomorphic to $\Alt_5$.
\end{prob}

\begin{prob}
  Prove that the only non-abelian simple group of order $<100$ is $\Alt_5$.
\end{prob}

\begin{prob}
  Is the following true? For any finite group $G$ the set $\{x^2:x\in G\}$ is a
  subgroup of $G$.
% false
\end{prob}

\begin{prob}
  \index{Guralnick, R.}
    \label{prob:Guralnick<201}
	Prove the following theorem of Guralnick~\cite{MR673806}. There exists
	a group $G$ of order $n\leq200$ such that $[G,G]\ne\{[x,y]:x,y\in G\}$
	if and only if
	\[
	  n\in\{96,128,144,162,168,192\}.
	\]
\end{prob}

\begin{prob}
  \index{Guralnick, R.}
    Prove the following extension of Guralnick's theorem (Problem
    \ref{prob:Guralnick<201}). There exists a group $G$ of order $n<1024$ such
    that $[G,G]\ne\{[x,y]:x,y\in G\}$ if and only if $n$ is one of the
    following numbers: 96, 128, 144, 162, 168, 192, 216, 240, 256, 270, 288,
    312, 320, 324, 336, 360, 378, 384, 400, 432, 448, 450, 456, 480, 486, 504,
    512, 528, 540, 560, 576, 594, 600, 624, 640, 648, 672, 702, 704, 720, 729,
    744, 750, 756, 768, 784, 792, 800, 810, 816, 832, 840, 864, 880, 882, 888,
    896, 900, 912, 918, 936, 960, 972, 1000, 1008.
\end{prob}

\begin{prob}
	\label{prob:minimal}
	Compute the list of normal subgroups of $\GL_2(3)$.
\end{prob}

\begin{prob}
	Compute the list of minimal subgroups of $\Alt_4$.
\end{prob}

\begin{prob}
	\label{prob:socle}
	Compute the socle and the list of minimal normal subgroups of $\Alt_4$. 
\end{prob}

\begin{prob}
	\label{prob:fitting}
	Compute the Fitting and the Frattini subgroup of $\SL_2(3)$.
\end{prob}

\begin{prob}
	\label{prob:maximal}
	Compute the list of all maximal normal subgroups of $\SL_2(3)$. 
\end{prob}

\begin{prob}
	\label{prob:PSL2(7)_max}
	\textcolor{blue}{Prove that $\PSL_2(7)$ has a maximal subgroup of order 16.}
\end{prob}

% The socle and minimal subgroups
%gap> G := SymmetricGroup(3);;
%gap> MinimalNormalSubgroups(G);
%[ Group([ (1,2,3) ]) ]
%gap> Socle(G);
%Group([ (1,2,3) ])
%gap> G := AlternatingGroup(4);;
%gap> MinimalNormalSubgroups(G);
%[ Group([ (1,2)(3,4), (1,3)(2,4) ]) ]
%gap> Socle(G);
%Group([ (1,2)(3,4), (1,4)(2,3) ])
%gap> IsCharacteristicSubgroup(G,Socle(G));
%true

% IsSubnormal
% Frattini
% Fitting
% MaximalNormal
% Maximal

\begin{prob}
	\label{prob:ChermakDelgado}
	\index{Chermak--Delgado subgroup}
	Let $G$ be a finite group and $H$ be a subgroup. The \textbf{Chermak--Delgado} measure of $H$ 
	is the number $m_G(H)=|H||C_G(H)|$. 
	Write a function to compute the Chermak--Delgado.
\end{prob}

\begin{prob}
	\label{prob:ChermakDelgadoS3D8}
	Compute $m_G(H)$ for $G\in\{\Sym_3,\D_8\}$ and $H$ a subgroup of $G$. 
\end{prob}

\begin{prob}
	\label{prob:holA4}
	Compute order the holomorph of $\Alt_4$. Find a permutation representation
	of small degree and find some minimal normal subgroup of order four. This
	is an exercise of~\cite{MR2478410}.
\end{prob}

\begin{prob}
Prove that the group $\langle (123\cdots7),(26)(34)\rangle$ is simple, 
has order $168$ and acts transitively on $\{1,\dots,7\}$. 
%%gap> G := Group([(1,2,3,4,5,6,7),(2,6)(3,4)]);;
%onjuga
%%true
%%gap> IsTransitive(G, [1..7]);
%%true
%%gap> Order(G);
%%168
\end{prob}

\begin{prob}
  Compute the order of the group $\langle a,b:a^2=b^2=(bab^{-1})^3=1\rangle$. 
\end{prob}

\begin{prob}
  Prove that $\langle a,b:a^2=aba^{-1}b=1\rangle$ is an infinite group.
\end{prob}

\begin{prob}
	\label{prob:order16}
  Compute the order of the group
  $\langle a,b: a^8=b^2a^4=ab^{-1}ab=1\rangle$.
\end{prob}

\begin{prob}
\label{prob:recognizeA5}
Can you recognize the group $\langle a,b:a^5=1, b^2=(ab)^3, (a^3ba^4b)^2=1\rangle$?
\end{prob}
%
\begin{prob}
Prove that the group $\langle a,b:a^2=b^3=a^{-1}b^{-1}ab=1\rangle$ is finite and cyclic.
\end{prob}

\begin{prob}
Prove that the group $\langle a,b:a^2=b^3=1\rangle$ is non-abelian.
\end{prob}

\begin{prob}
\label{prob:order=648}
Compute the order of the group
\[
  \langle a,b,c:a^3=b^3=c^3=1,\,aba=bab,\,cbc=bcb,\,ac=ca\rangle.
\]
\end{prob}

\begin{prob}
	\label{prob:Serre}
	Prove that the group $\langle a,b,c:bab^{-1}=a^{2},\,cbc^{-1}=b^{},\,
	aca^{-1}=c^{2}\rangle$ is trivial. This is an exercise of Serre's
	book~\cite[\S1]{MR1954121}:
\end{prob}

\begin{prob}
  \index{Burnside group}
  \index{Heisenberg group}
  \label{prob:B23}
  Prove that $B(2,3)$ is isomorphic to the Heisenberg group $H(\Z/3)$.
\end{prob}

\begin{prob}
	\label{prob:B23perm}
	Find a permutation representation of the group $B(2,3)$. 
\end{prob}

\begin{prob}
	\label{prob:B33}
  \index{Burnside group}
	Prove that 
	$B(3,3)$ %=\langle a,b,c:w^3=1\text{ for all word $w$ in $a,b$}\rangle$ 
	is a finite group of order $\leq2187$. 
\end{prob}

\begin{prob}
	\label{prob:probability}
	Let $G$ be a finite group with $k$ conjugacy classes.  It is known that the
	probability that two elements of $G$ commute is equal to
	$\mathrm{prob}(G)=k/|G|$. Compute this probability for $\SL_2(3)$,
	$\Alt_4$, $\Alt_5$, $\Sym_4$ and $Q_8$.
\end{prob}


